DEFINITION MODULE LowReal;

(* required separate library module *)

__DEF_SWITCHES__
#ifdef HM2
#define REAL SHORTREAL
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* ISO-LowReal                                                               *)
(* --------------------------------------------------------------------------*)
(* Zugriff auf die Interna reeller Zahlen                                    *)
(* --------------------------------------------------------------------------*)
(* 27-Aug-96, Holger Kleinschmidt                                            *)
(*****************************************************************************)

(*============== IMPLEMENTIERUNGSHINWEIS ====================================*)
(* Fuer diese Implementierung gilt:                                          *)
(* - IEEE: Denormalisierte Zahlen, negative Null und NaN werden nicht        *)
(*         unterstuetzt. Von den Funktionen "succ()", "pred()", "round()"    *)
(*         und "scale()" kann allerdings +/- Inf erzeugt werden.             *)
(*         Eine Zahl wird als Null betrachtet, wenn der Exponent Null ist.   *)
(* - MM2 : Denormalisierte Zahlen werden nicht unterstuetzt.                 *)
(*         Eine Zahl wird als Null betrachtet, wenn Bit 31 nicht gesetzt ist.*)
(*                                                                           *)
(* "setMode()" und "currentMode()" haben nur eine Funktion, wenn eine FPU    *)
(* vorhanden ist; sie beziehen sich auf die gleichnamigen Bits in FPCR.      *)
(* Es werden keine (emulierten) Ausnahmen ausgeloest -- "IsLowException()"   *)
(* = FALSE. Wenn jedoch die entsprechenden FPU-Exceptions durch "setMode()"  *)
(* aktiviert werden, kann die FPU Interrupts erzeugen.                       *)
(*===========================================================================*)

(* Die folgenden Konstanten gelten fuer die interne Defaultdarstellung reeller
   Zahlen, falls es mehrere Moeglichkeiten gibt, und auch nur fuer die
   Darstellung gespeicherter Werte.

  radix      : Basis der verwendeten Zahlendarstellung
  places     : Anzahl der 'radix'-Stellen zur Speicherung der Werte
  expoMin    : Kleinstmoeglicher Exponent
  expoMax    : Groesstmoeglicher Exponent
  large      : Groesste darstellbare Zahl
  small      : Kleinste (normalisierte) darstellbare Zahl
  IEC559     : vollstaendig IEC 559:1989 (IEEE 754:1987) kompatibel, d.h.
               NaN, neg. Null usw.
  LIA1       : vollstaendig ISO/IEC 10967-1:199x-kompatibel,
               (Information technology -- Language Independent Arithmetic)
  rounds     : Liefert jede Operation die dem mathematischen Resultat
               naechstmoegliche Darstellung ?
  gUnderflow : Gibt es Werte zwischen 0.0 und 'small' (denormalisierte Zahlen) ?
  exception  : Loest jede Operation, die ein Resultat ausserhalb des
               Wertebereichs erzeugt, eine Ausnahme aus ?
  extend     : Werden Werte mit einer hoeheren Genauigkeit berechnet als
               gespeichert ?
  nModes     : Anzahl der Bits, die fuer das Statusfeld benoetigt werden
 *)

CONST
#if (defined LPRM2)
  radix      = 2;
  places     = 24; (* 23 + 'hidden bit' *)
  expoMin    = -126;
  expoMax    = 127;
  large      = MAX(REAL);
  small      = 1.17549436E-38;
  IEC559     = FALSE;
  LIA1       = FALSE;
  rounds     = FALSE;
  gUnderflow = FALSE;
  exception  = FALSE;
  extend     = FALSE;
  nModes     = 16;
#elif (defined SPCM2)
  radix      = 2;
  places     = 24; (* 23 + 'hidden bit' *)
  expoMin    = -126;
  expoMax    = 127;
  large      = MAX(REAL);
  small      = 1.17549436E-38;
  IEC559     = FALSE;
  LIA1       = FALSE;
  rounds     = FALSE;
  gUnderflow = FALSE;
  exception  = FALSE;
  extend     = FALSE;
  nModes     = 16;
#elif (defined HM2)
  radix      = 2;
  places     = 24; (* 23 + 'hidden bit' *)
  expoMin    = -126;
  expoMax    = 127;
  large      = MAX(REAL);
  small      = MIN(REAL);
  IEC559     = FALSE;
  LIA1       = FALSE;
  rounds     = FALSE;
  gUnderflow = FALSE;
  exception  = FALSE;
  extend     = FALSE;
  nModes     = 16;
#elif (defined TDIM2)
  radix      = 2;
  places     = 24; (* 23 + 'hidden bit' *)
  expoMin    = -126;
  expoMax    = 127;
VAR (* READ ONLY ! *)
  large      : REAL;
CONST
  small      = 1.1754944E-38;
  IEC559     = FALSE;
  LIA1       = FALSE;
  rounds     = FALSE;
  gUnderflow = FALSE;
  exception  = FALSE;
  extend     = FALSE;
  nModes     = 16;
#elif (defined MM2)
  radix      = 2;
  places     = 24;
  expoMin    = -64;
  expoMax    = 63;
  large      = MAX(REAL);
  small      = MIN(REAL);
  IEC559     = FALSE;
  LIA1       = FALSE;
  rounds     = FALSE;
  gUnderflow = FALSE;
  exception  = FALSE;
  extend     = FALSE;
  nModes     = 16;
#endif

TYPE
  Modes = PACKEDSET OF [0..nModes-1];

(* Nicht von ISO festgelegt: *)
CONST
  (* Exception Enable *)
  fpcrBSUN  = 15; (* Branch/Set on Unordered *)
  fpcrSNAN  = 14; (* Signalling Not a Number *)
  fpcrOPERR = 13; (* Operand Error *)
  fpcrOVFL  = 12; (* Overflow *)
  fpcrUNFL  = 11; (* Underflow *)
  fpcrDZ    = 10; (* Divide by Zero *)
  fpcrINEX2 = 9;  (* Inexact Operation *)
  fpcrINEX1 = 8;  (* Inexact Decimal Input *)

  (* Rounding Precision *)
  fpcrPREC1 = 7;
  fpcrPREC0 = 6;

  (* Rounding Mode *)
  fpcrMODE1 = 5;
  fpcrMODE0 = 4;

CONST
  PrecMask     = Modes{fpcrPREC1,fpcrPREC0};

  PrecExtended = Modes{};
    (* Rundung auf max. Genauigkeit *)
  PrecSingle   = Modes{fpcrPREC0};
    (* Rundung auf einfache Genauigkeit *)
  PrecDouble   = Modes{fpcrPREC1};
    (* Rundung auf doppelte Genauigkeit *)


CONST
  ModeMask    = Modes{fpcrMODE1,fpcrMODE0};

  ModeNearest = Modes{};
    (* Rundung zur naechsten geraden Zahl *)
  ModeZero    = Modes{fpcrMODE0};
    (* Rundung nach Null (= Abschneiden, Trunc) *)
  ModeNegInf  = Modes{fpcrMODE1};
    (* Rundung nach Minus Unendlich (floor(), entier()) *)
  ModePosInf  = Modes{fpcrMODE1,fpcrMODE0};
    (* Rundung nach Plus Unendlich (ceil()) *)

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

PROCEDURE exponent ((* EIN/ -- *) x : REAL ): INTEGER;

(* Liefert den Exponenten von <x> im Bereich [expoMin..expoMax].
   Eine Ausnahme kann ausgeloest werden, falls <x> = 0.0.
 *)


PROCEDURE fraction ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den signifikanten Teil (Mantisse) von <x> *)


PROCEDURE sign ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert folgende Werte.

   -1.0, falls <x> kleiner als Null
    1.0, falls <x> groesser als Null
   und 1.0 oder -1.0, falls x gleich 0.0, um zwischen +0.0 und
   -0.0 unterscheiden zu koennen, falls die Darstellung reeller
   Zahlen dies erlaubt.
 *)


PROCEDURE succ ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den naechstgroesseren Wert von <x>, falls dieser existiert,
   andernfalls kann eine Ausnahme ausgeloest werden.
 *)


PROCEDURE ulp ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den Wert der letzten Stelle von <x>, falls ein solcher
   Wert existiert, andernfalls kann eine Ausnahme ausgeloest werden.
   (ulp <=> Unit in the Last Place.)

   Es gilt: ulp(x) = succ(x) - x, oder ulp(x) = x - pred(x), oder beides.
 *)


PROCEDURE pred ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den naechstkleineren Wert von <x>, falls dieser existiert,
   andernfalls kann eine Ausnahme ausgeloest werden.
 *)


PROCEDURE intpart ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den ganzzahligen Anteil von <x>. Falls <x> negativ ist, wird
   -intpart(abs(x)) zurueckgeliefert.
 *)


PROCEDURE fractpart ((* EIN/ -- *) x : REAL ): REAL;

(* Liefert den gebrochenen Anteil von <x>.

   Es gilt: x = intpart(x) + fractpart(x).
 *)


PROCEDURE scale ((* EIN/ -- *) x : REAL;
                 (* EIN/ -- *) n : INTEGER  ): REAL;

(* Liefert: x * radix^n, falls ein solcher Wert existiert, andernfalls
   kann eine Ausnahme ausgeloest werden.
 *)


PROCEDURE trunc ((* EIN/ -- *) x : REAL;
                 (* EIN/ -- *) n : INTEGER  ): REAL;

(* Liefert die <n> hoechstwertigen signifikanten Stellen von <x>.
   Falls n <= 0, kann eine Ausnahme ausgeloest werden.
 *)


PROCEDURE round ((* EIN/ -- *) x : REAL;
                 (* EIN/ -- *) n : INTEGER  ): REAL;

(* Liefert <x> auf die <n> hoechstwertigen signifikanten Stellen gerundet.
   Falls ein solcher Wert nicht existiert, oder n <= 0 ist, kann
   eine Ausnahme ausgeloest werden.
 *)


PROCEDURE synthesize ((* EIN/ -- *) expart  : INTEGER;
                      (* EIN/ -- *) frapart : REAL    ): REAL;

(* Setzt einen Wert aus <frapart> und <expart> zusammen.

   Es gilt: x = synthesize(exponent(x), fraction(x))
 *)


PROCEDURE setMode ((* EIN/ -- *) m : Modes );

(* Setzt die Statusflags auf den Wert <m> entsprechend der verwendeten
   Gleitkommaarithmetik.

   Imp: Setzt das "Exception Enable Byte" und das "Mode Control Byte"
        des Registers FPCR, wenn eine FPU vorhanden ist.
 *)


PROCEDURE currentMode ( ): Modes;

(* Liefert die momentane Einstellung der Statusflags.

   Imp: Fragt das "Exception Enable Byte" und das "Mode Control Byte"
        des Registers FPCR ab, wenn eine FPU vorhanden ist. Sonst
        wird konstant "Rundung zur naechsten geraden Zahl mit einfacher
        Genauigkeit" zurueckgeliefert.
 *)


PROCEDURE IsLowException ( ): BOOLEAN;

(* Testet, ob die aufrufende Koroutine in einer Ausnahmebehandlung ist,
   weil die 'LowReal'-Exception ausgeloest wurde.
 *)

END LowReal.
