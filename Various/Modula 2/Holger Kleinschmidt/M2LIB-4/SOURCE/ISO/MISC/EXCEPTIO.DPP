DEFINITION MODULE EXCEPTIONS;

(* system module *)

__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif

(* Stellt Moeglichkeiten zur Verfuegung, benutzerdefinierte Ausnahmen
   auszuloesen, und den augenblicklichen Ausfuehrungszustand zu erfragen.
 *)


(* Werte dieses Typs werden von Bibliotheksmodulen benutzt, um den Ursprung
   ausgeloester Ausnahmen zu kennzeichnen.
 *)
TYPE
  ExceptionSource;

  ExceptionNumber = CARDINAL;



PROCEDURE AllocateSource ((* -- /AUS *) VAR newSource : ExceptionSource );

(* Weist <newSource> einen eindeutigen Wert zu, der den Ursprung einer
   Ausnahme kennzeichnet. Diese Prozedur wird normalerweise im Initiali-
   sierungsteil eines Moduls aufgerufen, und der Wert wird bei Aufrufen
   von "RAISE()" benutzt, um anzuzeigen, dass die Ausnahme von diesem
   Modul ausgeloest wurde. Falls kein neuer Wert erzeugt werden kann,
   wird eine Ausnahme ausgeloest.
 *)


PROCEDURE RAISE ((* EIN/ -- *) source  : ExceptionSource;
                 (* EIN/ -- *) number  : ExceptionNumber;
                 (* EIN/ -- *) message : ARRAY OF CHAR   );

(* Assoziiert <source>, <number> und <message> mit dem laufenden Kontext,
   und loest eine Ausnahme aus.
 *)


PROCEDURE CurrentNumber
  ((* EIN/ -- *) source : ExceptionSource ): ExceptionNumber;

(* Falls die aufrufende Koroutine in einer Ausnahmebehandlung ist, weil
   eine Ausnahme durch <source> ausgeloest wurde, wird die entsprechende
   Nummer (<number> bei "RAISE()") zurueckgeliefert, ansonsten wird eine
   Ausnahme ausgeloest.
 *)


PROCEDURE GetMessage ((* -- /AUS *) VAR text : ARRAY OF CHAR );

(* Falls die aufrufende Koroutine in einer Ausnahmebehandlung ist, wird
   der bei "RAISE()" uebergebene String zurueckgeliefert, moeglicherweise
   gekuerzt, ansonsten wird der Leerstring zurueckgeliefert.
 *)


PROCEDURE IsCurrentSource ((* EIN/ -- *) source : ExceptionSource ): BOOLEAN;

(* Testet, ob die aufrufende Koroutine in einer Ausnahmebehandlung ist, weil
   eine Ausnahme durch <source> ausgeloest wurde.
 *)


PROCEDURE IsExceptionalExecution ( ): BOOLEAN;

(* Testet, ob die aufrufende Koroutine in einer Ausnahmebehandlung ist, weil
   eine Ausnahme ausgeloest wurde.
 *)

END EXCEPTIONS.
