DEFINITION MODULE WholeIO;

(* standard library module *)

__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* ISO-WholeIO                                                               *)
(* --------------------------------------------------------------------------*)
(* Ein- und Ausgabe ganzer Zahlen in dezimaler Stringrepraesentation auf dem *)
(* angegebenen Kanal.                                                        *)
(* Das Ergebnis von Eingabeoperationen kann ueber "IOResult.ReadResult()"    *)
(* erfragt werden.                                                           *)
(* --------------------------------------------------------------------------*)
(* 04-Dez-95, Holger Kleinschmidt                                            *)
(*****************************************************************************)

(*============== IMPLEMENTIERUNGSHINWEIS ====================================*)
(* Fuer diese Implementierung gilt:                                          *)
(* Eingabe: Es werden maximal 'WConvBase.MAXREAD' Zeichen beruecksichtigt.   *)
(*          Ist die Zahlendarstellung laenger,liefert "IOResult.ReadResult()"*)
(*          'wrongFormat' als Ergebnis.                                      *)
(*===========================================================================*)

FROM PORTAB IMPORT
(* TYPE *) UNSIGNED, SIGNED;

IMPORT IOChan;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

(* Die Textform einer ganzen Zahl mit Vorzeichen ist:
      ["+" | "-"], decimal digit, {decimal digit}

   Die Textform einer ganzen Zahl ohne Vorzeichen ist:
      decimal digit, {decimal digit}
 *)


PROCEDURE ReadInt ((* EIN/ -- *)     cid : IOChan.ChanId;
                   (* -- /AUS *) VAR int : SIGNED       );

(* Entfernt fuehrende Leerzeichen aus dem Eingabestrom <cid> und liest
   dann alle Zeichen, die eine gueltige Darstellung einer ganzen Zahl
   mit Vorzeichen bilden. Der Wert wird <int> zugewiesen.

   "IOResult.ReadResult()" liefert:

   'allRight'   : Es wurde ein gueltige Repraesentation einer ganzen Zahl
                  mit Vorzeichen gelesen, und der Wert ueberschreitet nicht
                  den Wertebereich von <int>. <int> enthaelt den Wert.
   'outOfRange' : Es wurde eine gueltige Repraesentation einer ganzen Zahl
                  mit Vorzeichen gelesen, aber der Wert ueberschreitet den
                  Wertebereich von <int>. <int> enthaelt den maximal oder
                  minimal moeglichen Wert, je nach Vorzeichen.
   'wrongFormat': Es wurden Zeichen gelesen, oder sind noch zu lesen, aber
                  diese bilden keine gueltige Repraesentation einer ganzen
                  Zahl mit Vorzeichen. <int> ist undefiniert.
   'endOfLine'  : Es wurde kein Nicht-Leer-Zeichen gelesen, da das naechste
                  Zeichen das Zeilenende ist.
   'endOfInput' : Es wurde kein Nicht-Leer-Zeichen gelesen, da der
                  Eingabestrom zuende ist.
 *)


PROCEDURE WriteInt ((* EIN/ -- *) cid   : IOChan.ChanId;
                    (* EIN/ -- *) int   : SIGNED;
                    (* EIN/ -- *) width : CARDINAL      );

(* Schreibt den Wert von <int> in Textform auf den Ausgabestrom <cid>.
   Falls die Repraesentation weniger als <width> Zeichen hat, wird mit
   fuehrenden Leerzeichen aufgefuellt. Falls <width> = 0, wird genau ein
   fuehrendes Leerzeichen geschrieben. Ein Vorzeichen wird nur bei
   negativem <int> geschrieben.
 *)


PROCEDURE ReadCard ((* EIN/ -- *)     cid  : IOChan.ChanId;
                    (* -- /AUS *) VAR card : UNSIGNED      );

(* Entfernt fuehrende Leerzeichen aus dem Eingabestrom <cid> und liest
   dann alle Zeichen, die eine gueltige Darstellung einer ganzen Zahl
   ohne Vorzeichen bilden. Der Wert wird <card> zugewiesen.

   "IOResult.ReadResult()" liefert:

   'allRight'   : Es wurde ein gueltige Repraesentation einer ganzen Zahl
                  ohne Vorzeichen gelesen, und der Wert ueberschreitet nicht
                  den Wertebereich von <card>. <card> enthaelt den Wert.
   'outOfRange' : Es wurde eine gueltige Repraesentation einer ganzen Zahl
                  ohne Vorzeichen gelesen, aber der Wert ueberschreitet den
                  Wertebereich von <card>. <card> enthaelt den maximal
                  moeglichen Wert.
   'wrongFormat': Es wurden Zeichen gelesen, oder sind noch zu lesen, aber
                  diese bilden keine gueltige Repraesentation einer ganzen
                  Zahl ohne Vorzeichen. <card> ist undefiniert.
   'endOfLine'  : Es wurde kein Nicht-Leer-Zeichen gelesen, da das naechste
                  Zeichen das Zeilenende ist.
   'endOfInput' : Es wurde kein Nicht-Leer-Zeichen gelesen, da der
                  Eingabestrom zuende ist.
 *)


PROCEDURE WriteCard ((* EIN/ -- *) cid   : IOChan.ChanId;
                     (* EIN/ -- *) card  : UNSIGNED;
                     (* EIN/ -- *) width : CARDINAL      );

(* Schreibt den Wert von <card> in Textform auf den Ausgabestrom <cid>.
   Falls die Repraesentation weniger als <width> Zeichen hat, wird mit
   fuehrenden Leerzeichen aufgefuellt. Falls <width> = 0, wird genau ein
   fuehrendes Leerzeichen geschrieben.
 *)

END WholeIO.
