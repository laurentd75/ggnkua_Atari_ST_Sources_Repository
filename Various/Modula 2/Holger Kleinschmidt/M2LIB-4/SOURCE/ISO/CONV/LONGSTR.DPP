DEFINITION MODULE LongStr;

(* standard library module *)

__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* ISO-LongStr                                                               *)
(* --------------------------------------------------------------------------*)
(* Konvertierung zwischen reellen Zahlen und Strings.                        *)
(* --------------------------------------------------------------------------*)
(* 27-Okt-95, Holger Kleinschmidt                                            *)
(*****************************************************************************)

(*============== IMPLEMENTIERUNGSHINWEIS ====================================*)
(* Fuer diese Implementierung gilt:                                          *)
(* Falls fuer die Zahl der signifikanten Stellen ('sigFigs') eine Null ange- *)
(* geben wird, werden 'FConvBase.DEFSIGFIGS' signifikante Stellen verwendet. *)
(* Falls der Real-Wert eine Darstellung von Unendlich ist, oder keine guel-  *)
(* tige Repraesentation ("FConvBase.checkReal()"), wird mit                  *)
(* "FConvBase.formToStr()" eine entsprechende Stringrepraesentation erzeugt. *)
(*===========================================================================*)

IMPORT ConvTypes;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

TYPE
  ConvResults = ConvTypes.ConvResults;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

(* Die Textform einer Fixpunktzahl mit Vorzeichen ist:
      ["+" | "-"], decimal digit, {decimal digit}, [".", {decimal digit}]

   Die Textform einer Gleitkommazahl mit Vorzeichen ist:
      Fixpunktzahl mit Vorzeichen,
      "E" | "e", ["+" | "-"], decimal digit, {decimal digit}
 *)

PROCEDURE StrToReal ((* EIN/ -- *)     str  : ARRAY OF CHAR;
                     (* -- /AUS *) VAR real : LONGREAL;
                     (* -- /AUS *) VAR res  : ConvResults   );

(* Ignoriert fuehrende Leerzeichen in <str> und konvertiert den Rest
   des Strings in eine reelle Zahl mit Vorzeichen, falls dieser eine
   gueltige Repraesentation enthaelt. Die Repraesentation muss durch
   das Ende von <str> abgeschlossen sein, es duerfen keine weiteren
   Zeichen folgen.

   <res> liefert folgende Ergebnisse:

   'strAllRight':    <str> enthaelt eine gueltige Repraesentation einer reellen
                     Zahl mit Vorzeichen, und der Wert unter- oder
                     ueberschreitet nicht den Wertebereich von <real>. <real>
                     enthaelt den Wert.

   'strOutOfRange':  <str> enthaelt eine gueltige Repraesentation einer
                     reellen Zahl mit Vorzeichen, aber der Wert ueber- oder
                     unterschreitet den Wertebereich von <real>.
                     <real> enthaelt den maximal oder minimal moeglichen Wert,
                     je nach Vorzeichen.

   'strWrongFormat': <str> enthaelt noch Zeichen, die keine gueltige
                     Repraesentation einer reellen Zahl mit Vorzeichen bilden.
                     <real> ist undefiniert.

   'strEmpty':       <str> enthaelt keine weiteren Zeichen. <real> ist
                     undefiniert.


   Diese Prozedur kann mit "[S]TextIO.ReadToken()" benutzt werden,
   wenn verlangt ist, dass komplette, durch Leerzeichen begrenzte Token
   im korrekten Format sind.
 *)


PROCEDURE RealToFloat ((* EIN/ -- *)     real    : LONGREAL;
                       (* EIN/ -- *)     sigFigs : CARDINAL;
                       (* -- /AUS *) VAR str     : ARRAY OF CHAR );

(* Wandelt <real> in eine Gleitkomma-Stringrepraesentation mit <sigFigs>
   signifikanten Stellen um, und kopiert das evtl. gekuerzte Resultat
   nach <str>.

   Es wird genau eine Ziffer vor dem Dezimalpunkt (falls vorhanden)
   geschrieben.
   Ein Vorzeichen wird nur bei negativem <real> geschrieben.
   Der Exponent mit Vorzeichen wird nur geschrieben, falls sein Wert
   nicht Null ist. Falls <sigFigs> = Null ist, wird ein Defaultwert
   eingesetzt. Falls es keine signifikanten Stellen hinter dem Dezimalpunkt
   gibt (sigFigs = 1), wird kein Dezimalpunkt geschrieben.

   Beispiele fuer die Ausgabe als Gleitkommazahl:

   <real>       3923009    39.23009   0.0003923009
   <sigFigs>
   1               4E+6        4E+1           4E-4
   2             3.9E+6      3.9E+1         3.9E-4
   5          3.9230E+6   3.9230E+1      3.9230E-4
 *)


PROCEDURE RealToEng ((* EIN/ -- *)     real    : LONGREAL;
                     (* EIN/ -- *)     sigFigs : CARDINAL;
                     (* -- /AUS *) VAR str     : ARRAY OF CHAR );

(* Verhaelt sich wie ein Aufruf von "RealToFloat()", ausser dass der
   Exponent ein Vielfaches von Drei ist, und dementsprechend ein bis
   drei Ziffern vor dem Dezimalpunkt stehen.

   Beispiele fuer die Ausgabe als technische Gleitkommazahl:

   <real>       3923009   39.23009   0.0003923009
   <sigFigs>
   1               4E+6         40         400E-6
   2             3.9E+6         39         390E-6
   5          3.9230E+6     39.230      392.30E-6
 *)


PROCEDURE RealToFixed ((* EIN/ -- *)     real   : LONGREAL;
                       (* EIN/ -- *)     places : INTEGER;
                       (* -- /AUS *) VAR str    : ARRAY OF CHAR );

(* Wandelt <real> in eine Fixpunkt-Stringrepraesentation um, die auf <places>
   Stellen bzgl. des Dezimalpunkts gerundet ist, und kopiert das evtl.
   gekuerzte Resultat nach <str>.

   Ein Vorzeichen wird nur bei negativem <real> geschrieben.
   Es wird mindestens eine Ziffer vor dem Dezimalpunkt (falls vorhanden)
   geschrieben. Wenn <places> negativ ist, wird kein Dezimalpunkt
   geschrieben.

   Beispiele fuer die Ausgabe als Fixpunktzahl:

   <real>        3923009   39.23009   0.0003923009
   <places>
   -5            3920000          0              0
   -2            3923010         40              0
   -1            3923009         39              0
    0           3923009.        39.             0.
    1          3923009.0       39.2            0.0
    4       3923009.0000    39.2301         0.0004
 *)


PROCEDURE RealToStr ((* EIN/ -- *)     real : LONGREAL;
                     (* -- /AUS *) VAR str  : ARRAY OF CHAR );

(* Falls in <str> Platz genug ist, wird <real> mit "RealToFixed()" in
   eine Fixpunktrepraesentation umgewandelt, wobei <places> so gewaehlt
   wird, dass <str> vollstaendig gefuellt wird; ansonsten wird mit
   "RealToFloat()" in eine Gleitkommarepraesentation umgewandelt, und das
   evtl. gekuerzte Resultat wird nach <str> kopiert. <sigFigs> wird so
   gewaehlt, dass <str> vollstaendig gefuellt wird.
 *)

END LongStr.
