10 rem ***************************************************************
20 rem ***** STOS intro from KelAUG diskmag 5 (c)The Sandman/SAS *****
30 rem *****                                                     *****
40 rem ***** Requires MISTY,MISSING LINK EXTENSIONS              *****
50 rem ***************************************************************
60 mode 0 : key off : curs off : click off : flash off : hide on : mouseoff 
70 T$=space$(11) : gosub 2000 : T$=T$+space$(11)
80 palette 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
90 fastcopy start(10),physic : fastcopy start(10),back
100 fade 5,0,0,0,0,0,0,0,0,$111,$111,$222,$333,$444,$555,$666,$777
110 wait 100 : wait vbl 
120 fade 10 to 10 : wait 50 : wait vbl 
130 wait 150 : wait vbl 
140 ink 3
150 for Y=0 to 99
160  : draw 0,Y to 319,Y
170  : draw 0,(199-Y) to 319,(199-Y)
180  : wait vbl 
190 next Y
200 ink 0
210 for Y=99 to 0 step-1
220  : draw 0,Y to 319,Y
230  : draw 0,(199-Y) to 319,(199-Y)
240  : wait vbl 
250 next Y
255 DUM=musauto(start(13),1,length(13))
260 wait 50 : wait vbl 
270 get palette (11)
280 kopy start(11)+(160*4),logic,160 : wait vbl 
290 kopy start(11)+(160*3),logic,320 : wait vbl 
300 kopy start(11)+(160*2),logic,480 : wait vbl 
310 kopy start(11)+(160*1),logic,640 : wait vbl 
320 kopy start(11)+(160*0),logic,800 : wait vbl 
330 for Y=5 to 155
340  : skopy 4,start(11),0,0,320,5,logic,0,Y
350  : kopy start(11)+(160*Y),logic+(160*Y),160
360  : wait vbl 
370 next Y
380 logic=back : ST=start(12)
390 joey 0,162,320,200,0,0,1
400 fastcopy physic,back
410 repeat 
420  : for L=1 to len(T$)-11
430  :  : TXT$=mid$(T$,L,11) : X=0
440  :  : for L2=1 to 8
450  :  :  : DUM=klear(logic,0,164,320,32,4)
460  :  :  : gosub 1000
470  :  :  : screen swap : wait vbl 
480  :  :  : X=X-4
490  :  : next L2
500  :  : H=hardkey : if H=57 then goto 520
510  : next L
520 until H=57
530 clear key 
540 XIT$=right$(TXT$,10)+"  LOADING.  "
550 for L=1 to len(XIT$)-11
560  : TXT$=mid$(XIT$,L,11) : X=0
570  : for L2=1 to 8
580  :  : DUM=klear(logic,0,164,320,32,4)
590  :  : gosub 1000
600  :  : screen swap : wait vbl 
610  :  : X=X-4
620  : next L2
630 next L
635 DUM=musauto(start(13),0,0)
640 fastcopy physic,logic
650 for Y=155 to 84 step-1
660  : kopy logic+(160*155),physic+(160*Y),(160*44)
670  : wait vbl 
680 next Y
690 for Y=83 to 0 step-1
700  : kopy logic+(160*155),physic+(160*Y),(160*6)
710  : wait vbl 
720 next Y
730 kopy logic+(160*156),physic,160*5
740 kopy logic+(160*157),physic,160*4
750 kopy logic+(160*158),physic,160*3
760 kopy logic+(160*159),physic,160*2
770 kopy logic+(160*160),physic,160
780 mouseon : logic=physic
790 fade 5,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0 : wait 50
800 end 
1000 rem ***************************************************
1010 rem ***** Draw the current string on the scroller *****
1020 rem ***************************************************
1030 for L3=1 to 11
1040  : DUM$=mid$(TXT$,L3,1)
1050  : IM=((asc(DUM$)-32))
1060  : joey logic,ST,IM,(X+((L3-1)*32)),164,15,0
1070 next L3
1080 return 
2000 rem **********************************************************
2010 rem ***** This is the scroller text - UPPER CASE PLEASE! *****
2020 rem **********************************************************
2030 T$=T$+"THIS IS WERE YOU PUT THE TEXT FOR THE SCROLLER.MOST "
2040 T$=T$+"CHARACTERS ARE ALLOWED,BUT REMEMBER TO USE UPPER CASE!"
2999 return 
