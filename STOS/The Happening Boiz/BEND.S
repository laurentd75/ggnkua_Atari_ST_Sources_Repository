**********************************
*EDDIES BYTE BENDER EXTENSION	 *
*FOR STOS BASIC                  *
*SCROLLS A 32 HIGH STRIP ROUND A *
*STATED CURVE ETC, ETC           *
*AREG(0) POINTS TO SCREEN        *
*AREG(1) POINTS TO BEND DATA     *
*AREG(2) POINTS TO STRIP OF      *
*A 32*32 FONT(4 STRIPS PER LETTER*
*WHICH IS CREATED WITH BYTEMAKE  *
*STOS PROGRAM                    *
*2 DECEMBER 1991   AT CHELMSFORD *
**********************************

BYTE_IT
	MOVE.W	#38,D1		*39 STRIPS TO COPY
BYTE_1
	MOVE.W	(A1),D2
	MOVE.W	2(A1),D3	*BYTE OFFSETS
	ADD.L	#2,A1		*NEXT LOT

	MOVE.L	A0,A4
	MOVE.L	A0,A3		*SOURCE/DEST ADDRESSES
	ADD.W	D2,A4
	ADD.W	D3,A3
	JSR	BYTE_COPY
	DBF	D1,BYTE_1
	JSR	NEXT_STRIP	*DRAW PART OF FONT
	RTS			*BACK TO STOS... .  .  
BYTE_COPY
	MOVE.W	#31,D7
B_COPY
	MOVE.B	(A3),(A4)
	ADD.W	#160,A3
	ADD.W	#160,A4
	DBF	D7,B_COPY
	RTS

NEXT_STRIP
	MOVE.L	A0,A3
	ADD.W	D3,A3		*40'TH STRIP
	MOVE.W	#31,D7
NEXT_STR
	MOVE.B	(A2),(A3)
	ADD.W	#160,A2
	ADD.W	#160,A3
	DBF 	D7,NEXT_STR
	RTS