1 rem *************************************************************************
2 rem * IF YOU HAVE THE STE EXTENSION FOR STOS, THIS LISTING IS NOT NESCESARY *
3 rem *************************************************************************
450 cls
460 dim JM$(4)
500 repeat 
501 JM$(1)="STE A0 " : JM$(2)="STE A1 "
502 JM$(3)="STE B0 " : JM$(4)="STE B1 "
505 SEJ=deek($FF9202) : SEF=peek($FF9201) : SEF=not(SEF) : SEJ=not(SEJ)
510 for T=1 to 4
520 JP=0 : JD=0 : JL=0 : JR=0 : FIR=0
550 if T<>1 then goto 560 : rem here STE A0
551 if SEJ and 8 then JP=true
552 if SEJ and 4 then JD=true
553 if SEJ and 2 then JL=true
554 if SEJ and 1 then JR=true
555 if SEF and 1 then FIR=true
560 if T<>2 then goto 570 : rem here STE A1
561 if SEJ and 2048 then JP=true
562 if SEJ and 1024 then JD=true
563 if SEJ and 512 then JL=true
564 if SEJ and 256 then JR=true
565 if SEF and 2 then FIR=true
570 if T<>3 then goto 580 : rem here STE B0
571 if SEJ and 128 then JP=true
572 if SEJ and 64 then JD=true
573 if SEJ and 32 then JL=true
574 if SEJ and 16 then JR=true
575 if SEF and 4 then FIR=true
580 if T<>4 then goto 590 : rem here STE B1
581 if SEJ and 32768 then JP=true
582 if SEJ and 16384 then JD=true
583 if SEJ and 8192 then JL=true
584 if SEJ and 4096 then JR=true
585 if SEF and 8 then FIR=true
590 if JP then JM$(T)=JM$(T)+"up "
600 if JD then JM$(T)=JM$(T)+"down "
610 if JL then JM$(T)=JM$(T)+"left "
620 if JR then JM$(T)=JM$(T)+"right "
630 if FIR then JM$(T)=JM$(T)+"FIRE! "
700 locate 0,7+T : print JM$(T);"                  "
800 next T
900 until upper$(inkey$)="Q"
