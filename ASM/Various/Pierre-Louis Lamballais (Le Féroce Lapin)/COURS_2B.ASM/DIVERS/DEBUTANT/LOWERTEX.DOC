THE SECRET OF THE FISH  by Udo of TEX

1) How to switch off the lower border:

To  irritate  the  video shifter you  must  switch  the  video 
frequency  from  50 to 60 Hz in the last line of  the  screen. 
This  will irritate the video chip so it forgets to  draw  the 
last  2  Bytes  of the screen memory (this  causes  the  right 
border   to  start  16  pixels  earlier  and  to   hide   this 
malfunction the air bubbles were painted in the screen). After 
displaying  200 lines the MMU is checking if 32000  Byte  have 
been  displayed.  But due to the 2 Byte error there are  still 
some left.  If switch back to 50 Hz in the left border of  the 
next  line,  the MMU will continue sending memory data to  the 
shifter  without stopping.  It depends on the monitor and  the 
ST's  video  transistor but in the best case you will  see  64 
more  lines  of video data till the monitor starts  his  blank 
gap. 
 
 First  we need a level 4 routine which is called at every  VBL 
 (that means 50 times per second).
 
 new4:
    clr.b  $fffffa1b.w              ;stop timer
    move.b #199,$fffffa21.w         ;set data register
    move.b #8,$fffffa1b.w           ;start timer
    new4b:
    jmp    $000000
 
    This  rout sets the timer c data register to 199 so  our  main 
    program  will  be interrupted to handle  the  border  removing 
    properly.
 
    Now the Timer C handler:
 newtb:
    clr.b   $fffffa1b.w             ;timer stop
    movem.l d0/d3-d7/a0-a6,-(sp)
    move.w  #$fa21,a4
    move.b  #180,(a4)               ;set counter
    move.b  #8,-6(a4)
 
This  will  install  a new Timer C data so we  are  no  longer 
disturbed by the Timer C, but it is still running.

   moveq   #0,d0                   ;wait till middle of line
   mulu    d0,d0
   mulu    d0,d0
   move.b  #0,$ffff820a.w          ;on to 60 Hz
 
 We  now  spent a lot of time multiplying nonsense to  let  the 
 electron  beam  run  in  the middle of  the  screen  and  then 
 switched to 60 Hz.
 
    move.b  (a4),d0                 ;wait for right border
 wait:
    cmp.b   (a4),d0
    beq     wait
 
 As the Timer C is still running,  we use this loop to wait for 
 the right border.
 
    moveq   #0,d0                   ;wait on left border
    mulu    d0,d0
    mulu    d0,d0
    move.b  #2,$ffff820a.w          ;back to 50 Hz
 
 With this commands we spent some time waiting for the HBL then 
 we switched back to 50 Hz.
    
    movem.l (sp)+,d0/d3-d7/a0-a6
    bclr    #0,$fffffa0f.w          ;end of interrupt
    rte
 
 Now we have ended the Interruptrout.
