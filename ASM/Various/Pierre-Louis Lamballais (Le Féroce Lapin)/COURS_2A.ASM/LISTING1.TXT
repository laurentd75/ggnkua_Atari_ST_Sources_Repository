******************************************************************
*                                                                *
*             COURS D'ASSEMBLEUR 68000 SUR ATARI ST              *
*                                                                *
*                 par Le F‚roce Lapin (from 44E)                 *
*                                                                *
*                         Seconde s‚rie                          *
*                                                                *
*                       Listing  num‚ro 1                        *
*                                                                *
******************************************************************

* Affichage d'une image incluse dans le programme.

         LEA       IMAGE,A6          adresse image
         ADDA.L    #2,A6             saute l'en-tˆte DEGAS
         MOVE.L    A6,-(SP)          on est donc sur les couleurs
         MOVE.W    #6,-(SP)          mise en place par XBIOS(6)
         TRAP      #14
         ADDQ.L    #6,SP

         MOVE.W    #3,-(SP)          cherche l'adresse de l'‚cran
         TRAP      #14
         ADDQ.L    #2,SP
         MOVE.L    D0,A5             la sauve en A5

         ADDA.L    #32,A6            saute les couleurs (16 words)
         MOVE.W    #7999,D0          init le compteur
COPIE    MOVE.L    (A6)+,(A5)+       transfŠre image vers l'‚cran
         DBF       D0,COPIE

         MOVE.W    #7,-(SP)          attend un appui sur une touche
         TRAP      #1
         ADDQ.L    #2,SP

         MOVE.W    #0,-(SP)          et bye bye
         TRAP      #1

IMAGE    INCBIN    "A:\TRUC.PI1"     l'image 
