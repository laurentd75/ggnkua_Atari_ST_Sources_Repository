SCREEN=$76300
REALSCREEN1 = SCREEN+160*$A0

SPACEBLOK=$68000
SCREEN0 = $69000
SCREEN1 = SCREEN0+$1400
SCREEN2 = SCREEN1+$1400
SCREEN3 = SCREEN2+$1400
SCREEN4 = SCREEN3+$1400
SCREEN5 = SCREEN4+$1400
SCREEN6 = SCREEN5+$1400
SCREEN7 = SCREEN6+$1400
BUFFER1 = SCREEN7+$1400

	OPT O+,OW-

	DC.W $A00A

	MOVE.L $4(A7),BASE

	MOVE.W #$2,-(SP)
	TRAP #$0E
	ADDQ.L #$2,SP
	MOVE.L D0,PHYSBASE

	MOVE.W #$3,-(SP)
	TRAP #$0E
	ADDQ.L #$2,SP
	MOVE.L D0,LOGBASE

	MOVE.W #$4,-(SP)
	TRAP #$0E
	ADDQ.L #$2,SP
	MOVE.W D0,REZ

	CLR.W -(SP)
	PEA SCREEN
	PEA SCREEN
	MOVE.W #$5,-(SP)
	TRAP #$0E
	LEA $C(SP),SP

	PEA INT_SET2(PC)
	MOVE.W #$26,-(SP)
	TRAP #$0E
	ADDQ.L #$6,SP

	MOVE.W REZ(PC),-(SP)
	MOVE.L PHYSBASE(PC),-(SP)
	MOVE.L LOGBASE(PC),-(SP)
	MOVE.W #$5,-(SP)
	TRAP #$0E
	LEA $C(SP),SP

	MOVE.L BASE(PC),A5
	MOVE.L #GAME,A3
	MOVE.L $2(A3),A0
	ADD.L $6(A3),A0
	LEA $1C(A0),A0
	ADD.L A3,A0
	MOVE.L $8(A5),D0
	MOVE.L #GAME+$1C,A1
	TST.L (A0)
	BEQ QUIT
	ADD.L (A0)+,A1
NEXT1	ADD.L D0,(A1)
NEXT2	CLR.L D1
	MOVE.B (A0)+,D1
	TST.W D1
	BEQ QUIT
	CMP.B #$1,D1
	BNE NEXT3
	LEA $FE(A1),A1
	BRA NEXT2

NEXT3	ADD.L D1,A1
	BRA NEXT1

QUIT	MOVE.L $2(A3),$C(A5)
	MOVE.L $6(A3),$14(A5)
	MOVE.L $A(A3),$1C(A5)
	MOVE.L $8(A5),D0
	MOVE.L $C(A5),D1
	ADD.L D0,D1
	MOVE.L D1,$10(A5)
	MOVE.L $14(A5),D0
	ADD.L D0,D1
	MOVE.L D1,$18(A5)

	LEA COPY2(PC),A0
	LEA $7FF00,A1
	MOVEQ #$30,D0
COPY_SET	MOVE.B (A0)+,(A1)+
	DBF D0,COPY_SET

	MOVE.L #GAME+$1C,A0
	MOVE.L BASE(PC),A1
	LEA $100(A1),A1
	MOVE.L A1,A3
	MOVE.L #EOF,A2
	JMP $7FF00

COPY2	MOVE.L (A0)+,(A1)+
	CMP.L A2,A0
	BLT COPY2

CLEAR	CLR.L (A1)+
	CMP.L A2,A1
	BLT CLEAR
	JMP (A3)

BASE	DC.L $0

MUSIC	INCBIN EXTRAS\DUGGER.MUS

PHYSBASE	DC.L $0
LOGBASE	DC.L $0
REZ	DC.W $0
STATUS	DC.W $0
SAVE_COL	DS.W $10

INT_SET2	LEA STATUS(PC),A0
	MOVE.W SR,(A0)

	LEA MUSIC(PC),A0
	MOVE.L #$7E000,A1
	MOVE.W #$1FFD,D0
MUS_COPY	MOVE.B (A0)+,(A1)+
	DBF D0,MUS_COPY

	BSET #$1,$FF820A

	MOVE.W #$5,-(SP)
	MOVE.W #$1A,-(SP)
	TRAP #$0E
	ADDQ.L #$4,SP

	MOVE.W #$6,-(SP)
	MOVE.W #$1A,-(SP)
	TRAP #$0E
	ADDQ.L #$4,SP

	MOVEQ #$2,D0
	JSR $7E000

	MOVE.W #$2700,SR

	LEA SCREEN,A0
	LEA $7D00(A0),A0
	LEA SPACEBLOK,A1
CLEAR_SCR	CLR.B (A1)+
	CMP.L A0,A1
	BNE CLEAR_SCR

	LEA $FF8240,A0
	LEA SAVE_COL(PC),A1
	MOVE.W #$0F,D0
COPY_COL1	MOVE.W (A0),(A1)+
	CLR.W (A0)+
	DBF D0,COPY_COL1

	LEA STORE1(PC),A0
	MOVE.L $70.W,(A0)+
	MOVE.L $120.W,(A0)+
	MOVE.B $FFFA07,(A0)+
	MOVE.B $FFFA13,(A0)+
	MOVE.B $FFFA21,(A0)+
	MOVE.B $FFFA1B,(A0)+
	MOVE.B $FFFA09,(A0)+

	LEA INT(PC),A0
	LEA RASTER(PC),A1
	MOVE.L A0,$70.W
	MOVE.L A1,$120.W

	CLR.B $FFFA09
	MOVE.B #$1,$FFFA07
	OR.B #$01,$FFFA13
	MOVE.B #$1,$FFFA21
	MOVE.B #$8,$FFFA1B

	MOVE.B #$00,$FFFC02

	MOVE.L #LOGO,A0
	LEA SCREEN+61*$A0,A1
	MOVE.W #$4FFF,D0
SET_LOGO1	MOVE.B (A0)+,D1
	OR.B D1,(A1)+
	DBF D0,SET_LOGO1

	MOVE.W #$2300,SR

KEY	TST.W SYNCH
	BEQ KEY

	CLR.W SYNCH

	CMP.B #$3B,$FFFC02
	BEQ F1_KEY
	CMP.B #$3C,$FFFC02
	BEQ F2_KEY
	CMP.B #$3D,$FFFC02
	BEQ F3_KEY
	CMP.B #$39,$FFFC02
	BNE KEY

	MOVE.W #$2700,SR

	LEA STORE1(PC),A0
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.B (A0)+,$FFFA07
	MOVE.B (A0)+,$FFFA13
	MOVE.B (A0)+,$FFFA21
	MOVE.B (A0)+,$FFFA1B
	MOVE.B (A0)+,$FFFA09

	LEA $FF8240,A1
	LEA SAVE_COL(PC),A0
	MOVE.W #$0F,D0
COPY_COL2	MOVE.W (A0)+,(A1)+
	DBF D0,COPY_COL2

	MOVE.W STATUS(PC),SR

	MOVE.W #$5,-(SP)
	MOVE.W #$1B,-(SP)
	TRAP #$0E
	ADDQ.L #$4,SP

	MOVE.W #$6,-(SP)
	MOVE.W #$1B,-(SP)
	TRAP #$0E
	ADDQ.L #$4,SP

	JSR $7E00C

	RTS

F1_KEY	BSR F1
	BRA KEY

F2_KEY	BSR F2
	BRA KEY
	
F3_KEY	BSR F3
	BRA KEY

F1	RTS
	MOVE.W #$4E75,F1
	MOVE.W #$4E71,F2
	MOVE.W #$4E71,F3
	MOVEQ #$2,D0
	BRA MUS_SET

F2	NOP
	MOVE.W #$4E71,F1
	MOVE.W #$4E75,F2
	MOVE.W #$4E71,F3
	MOVEQ #$3,D0
	BRA MUS_SET

F3	NOP
	MOVE.W #$4E71,F1
	MOVE.W #$4E71,F2
	MOVE.W #$4E75,F3
	MOVEQ #$4,D0
	BRA MUS_SET

MUS_SET	JSR $7E000
	RTS
	
INDEX	DC.W $0
SYNCH	DC.W $0
STORE1	DC.L $0,$0
	DC.B $0,$0,$0,$0,$0

	EVEN

INT	MOVE.W #$2500,SR
	CLR.W INDEX
	MOVE.W #$1,SYNCH
	MOVE.L #RASTER,$120.W
	BCLR #$0,$FFFA0F

	BSR MUS_PLAY
	BSR COLORROL
	BSR BAR_ROL1
	BSR BAR_ROL2
	BSR SCROLL2
	BSR SCROLL1

	RTE

MUS_PLAY	RTS
	JMP $7E012

BARHELP	DC.W $0

BAR_ROL1	NOP
	MOVE.W BARHELP,D0
	LEA SCR_COL1,A0
	LEA BAR_COL1,A1
	LEA SCR_COL2,A2
	LEA BAR_COL2,A3
	MOVE.W (A1,D0.W),(A0,D0.W)
	MOVE.W (A3,D0.W),(A2,D0.W)
	ADDQ.W #$2,BARHELP
	CMP.W #56*2,BARHELP
	BNE NO_QUIT1
	CLR.W BARHELP
	MOVE.W #$4E75,BAR_ROL1
	MOVE.W #$4E71,BAR_ROL2
NO_QUIT1	RTS

BAR_ROL2	RTS
	MOVE.W BARHELP,D0
	LEA PIC_COL1,A1
	LEA PIC_COL2,A3
	MOVE.W (A1,D0.W),LOGO_COL1
	MOVE.W (A3,D0.W),LOGO_COL2
	ADDQ.W #$2,BARHELP
	CMP.W #100*2,BARHELP
	BNE NO_QUIT2
	MOVE.W #$4E75,BAR_ROL2
	MOVE.W #$4E71,MUS_PLAY
NO_QUIT2	RTS

COLORROL	LEA BLUE_COL,A0
	LEA $2(A0),A1
	MOVE.W (A0),D0
	MOVE.W #54,D1
COL_ROL1	MOVE.W (A1)+,(A0)+
	DBF D1,COL_ROL1
	MOVE.W D0,(A0)

	LEA RED_COL,A0
	LEA $2(A0),A1
	MOVE.W (A0),D0
	MOVE.W #54,D1
COL_ROL2	MOVE.W (A1)+,(A0)+
	DBF D1,COL_ROL2
	MOVE.W D0,(A0)

	LEA LOGO_COL1+200,A0
	LEA -2(A0),A1
	MOVE.W (A1),D0
	MOVE.W #100,D1
COL_ROL3	MOVE.W -(A1),-(A0)
	DBF D1,COL_ROL3
	MOVE.W D0,(A0)

	LEA LOGO_COL2,A0
	LEA $2(A0),A1
	MOVE.W (A0),D0
	MOVE.W #98,D1
COL_ROL4	MOVE.W (A1)+,(A0)+
	DBF D1,COL_ROL4
	MOVE.W D0,(A0)
	RTS

RASTER	MOVE.W INDEX,D7
	MOVE.L #SCR_COL1,A6
	MOVE.W (A6,D7.W),$FF8240
	MOVE.L #BLUE_COL,A6
	MOVE.W (A6,D7.W),$FF8244
	ADDQ.W #$2,INDEX
	CMP.W #56*2,INDEX
	BEQ RASTERA
	BCLR #0,$FFFA0F
	RTE

RASTERA	MOVE.L #RASTERB,$120.W
	CLR.W INDEX
	BCLR #0,$FFFA0F
	RTE

RASTERB	CMP.W #3,INDEX
	BEQ RASTERC
	ADDQ.W #$1,INDEX
	BCLR #$0,$FFFA0F
	RTE

RASTERC	MOVE.L #RASTERD,$120.W
	CLR.W INDEX
	BCLR #0,$FFFA0F
	RTE

RASTERD	MOVE.W INDEX,D7
	MOVE.L #LOGO_COL1,A6
	MOVE.W (A6,D7.W),$FF8248
	MOVE.L #LOGO_COL2,A6
	MOVE.W (A6,D7.W),$FF824C
	ADDQ.W #$2,INDEX
	CMP.W #80*2,INDEX
	BEQ RASTERE
	BCLR #0,$FFFA0F
	RTE

RASTERE	MOVE.L #RASTERF,$120.W
	CLR.W INDEX
	BCLR #0,$FFFA0F
	RTE

RASTERF	CMP.W #$4,INDEX
	BEQ RASTERG
	ADDQ.W #$1,INDEX
	BCLR #$0,$FFFA0F
	RTE

RASTERG	MOVE.L #RASTERH,$120.W
	CLR.W INDEX
	BCLR #0,$FFFA0F
	RTE

RASTERH	MOVE.W INDEX,D7
	MOVE.L #SCR_COL2,A6
	MOVE.W (A6,D7.W),$FF8240
	MOVE.L #RED_COL,A6
	MOVE.W (A6,D7.W),$FF8244
	ADDQ.W #$2,INDEX
	CMP.W #56*2,INDEX
	BEQ RASTERI
	BCLR #0,$FFFA0F
	RTE

RASTERI	MOVE.L #RASTERJ,$120.W
	BCLR #$0,$FFFA0F
	RTE

RASTERJ	BCLR #$0,$FFFA0F
	RTE

SCROLL1	LEA SCREEN+15*$A0,A0
	MOVE.L #BLOCK1,A1

	MOVE.W #HEIGHT-1,D0

HSCROL1	MOVEQ #3,D1

HSCRO1	LSL.W $8(A1)
	ROXL.W (A1)

	ROXL.W $98(A0)
	ROXL.W $90(A0)
	ROXL.W $88(A0)
	ROXL.W $80(A0)
	ROXL.W $78(A0)
	ROXL.W $70(A0)
	ROXL.W $68(A0)
	ROXL.W $60(A0)
	ROXL.W $58(A0)
	ROXL.W $50(A0)
	ROXL.W $48(A0)
	ROXL.W $40(A0)
	ROXL.W $38(A0)
	ROXL.W $30(A0)
	ROXL.W $28(A0)
	ROXL.W $20(A0)
	ROXL.W $18(A0)
	ROXL.W $10(A0)
	ROXL.W $8(A0)
	ROXL.W (A0)

	ADDQ.L #$2,A0
	ADDQ.L #$2,A1
	DBF D1,HSCRO1
	
	ADD.L #$98,A0
	ADDQ.L #$8,A1

	DBF D0,HSCROL1

	SUBQ.W #$1,HSCRHELP1
	BEQ NEWCHAR1

	RTS

HSCRHELP1	DC.W $20

NEWCHAR1	MOVE.W #$20,HSCRHELP1
	MOVE.L POINTER1,A0
	MOVE.B (A0)+,D0
	BEQ END1

	MOVE.L A0,POINTER1

	CMP.B #$20,D0
	BEQ SPACE1

	MOVE.L #CHARS,A1
	MOVE.L A1,A2
	LEA LETTERS,A0
NEXTCHAR1	MOVE.B (A0)+,D1
	BEQ NEXTLINE1
	CMP.B #$FF,D1
	BEQ SPACE1
	CMP.B D1,D0
	BEQ COPYCHAR1
	LEA $10(A1),A1
	BRA NEXTCHAR1

NEXTLINE1	LEA $A0*32(A2),A2
	MOVE.L A2,A1
	BRA NEXTCHAR1

COPYCHAR1	LEA BLOCK1,A0
	MOVE.W #HEIGHT-1,D0

HSCRWEER1	MOVE.W (A1),(A0)+
	MOVE.W $2(A1),(A0)+
	MOVE.W $4(A1),(A0)+
	MOVE.W $6(A1),(A0)+
	MOVE.W $8(A1),(A0)+
	MOVE.W $A(A1),(A0)+
	MOVE.W $C(A1),(A0)+
	MOVE.W $E(A1),(A0)+

	ADD.L #$A0,A1

	DBF D0,HSCRWEER1

	RTS

END1	MOVE.L #HSCRTEXT1,POINTER1
	RTS

SPACE1	LEA BLOCK1,A0
	MOVE.W #4*26,D0
CLEAR1	CLR.l (A0)+
	DBF D0,CLEAR1
	RTS

BLOCK1	DS.L 4*26
	DC.L $0

JUMPTAB1	DC.L SCH1,SCH2,SCH3,SCH4,SCH5,SCH6,SCH7,ZETDEEL
	DC.L SCH1,SCH2,SCH3,SCH4,SCH5,SCH6,SCH7,ZETDEEL1

NR1	DC.W $0
TEKEN1	DC.L $0

SCROLL2	MOVE.W NR1,D0
	ADDQ.W #1,NR1
	LSL.W #2,D0
	LEA JUMPTAB1,A0
	MOVE.L (A0,D0.W),A1
	JMP (A1)

ZETDEEL	LEA SCREEN7,A0
	BSR SCHUIF

	MOVE.L POINTER2,A0
	MOVE.B (A0)+,D0
	BEQ END2

	MOVE.L A0,POINTER2

	CMP.B #$20,D0
	BEQ SPACE2

	MOVE.L #CHARS,A1
	MOVE.L A1,A2
	LEA LETTERS,A0
NEXTCHAR2	MOVE.B (A0)+,D1
	BEQ NEXTLINE2
	CMP.B #$FF,D1
	BEQ SPACE2
	CMP.B D1,D0
	BEQ COPYCHAR2
	LEA $10(A1),A1
	BRA NEXTCHAR2

NEXTLINE2	LEA $A0*32(A2),A2
	MOVE.L A2,A1
	BRA NEXTCHAR2

END2	MOVE.L #HSCRTEXT2,POINTER2
	RTS

SPACE2	LEA SPACEBLOK,A1

COPYCHAR2	MOVE.L A1,A0
	MOVE.L A0,TEKEN1
COPYKARS	LEA BUFFER1,A1
	LEA SCREEN7+$98,A2
	MOVEQ #HEIGHT-1,D6
COPYKAR	MOVEM.W (A0),D0-D3
	CLR.W (A1)+
	MOVE.W D0,(A1)+
	CLR.W (A1)+
	MOVE.W D1,(A1)+
	CLR.W (A1)+
	MOVE.W D2,(A1)+
	CLR.W (A1)+
	MOVE.W D3,(A1)+
	MOVEM.W D0-D3,(A2)
	ADD.L #160,A0
	ADD.L #160,A2
	DBF D6,COPYKAR
	RTS

ZETDEEL1	LEA SCREEN7,A0
	BSR.S SCHUIF
	CLR.W NR1
	MOVE.L TEKEN1,A0
	ADDQ.W #8,A0
	BRA.S COPYKARS

SCH1	LEA SCREEN0,A0
	BRA.S SCHUIF
SCH2	LEA SCREEN1,A0
	BRA.S SCHUIF
SCH3	LEA SCREEN2,A0
	BRA.S SCHUIF
SCH4	LEA SCREEN3,A0
	BRA.S SCHUIF
SCH5	LEA SCREEN4,A0
	BRA.S SCHUIF
SCH6	LEA SCREEN5,A0
	BRA.S SCHUIF
SCH7	LEA SCREEN6,A0
SCHUIF	LEA BUFFER1,A1
	MOVE.L A0,A3

	MOVEQ #HEIGHT-1,D6
ROLLEN	MOVEM.L (A1)+,D0-D3
	LSL.L #2,D0
	LSL.L #2,D1
	LSL.L #2,D2
	LSL.L #2,D3
	MOVEM.L D0-D3,-16(A1)
	SWAP D0
	SWAP D1
	SWAP D2
	SWAP D3
	OR.W D0,$98(A0)
	OR.W D1,$9A(A0)
	OR.W D2,$9C(A0)
	OR.W D3,$9E(A0)
	ADD.L #160,A0
	DBF D6,ROLLEN

	LEA BUFFER1,A1
	LEA REALSCREEN1,A2
	MOVEQ #HEIGHT-1,D6
SCHUIFZET	MOVEM.L (A3),D0-D3
	MOVEM.L D0-D3,(A2)
	MOVEM.L D2-D3,(A3)
	MOVEM.L $10(A3),D0-D3
	MOVEM.L D0-D3,$10(A2)
	MOVEM.L D0-D3,$8(A3)
	MOVEM.L $20(A3),D0-D3
	MOVEM.L D0-D3,$20(A2)
	MOVEM.L D0-D3,$18(A3)
	MOVEM.L $30(A3),D0-D3
	MOVEM.L D0-D3,$30(A2)
	MOVEM.L D0-D3,$28(A3)
	MOVEM.L $40(A3),D0-D3
	MOVEM.L D0-D3,$40(A2)
	MOVEM.L D0-D3,$38(A3)
	MOVEM.L $50(A3),D0-D3
	MOVEM.L D0-D3,$50(A2)
	MOVEM.L D0-D3,$48(A3)
	MOVEM.L $60(A3),D0-D3
	MOVEM.L D0-D3,$60(A2)
	MOVEM.L D0-D3,$58(A3)
	MOVEM.L $70(A3),D0-D3
	MOVEM.L D0-D3,$70(A2)
	MOVEM.L D0-D3,$68(A3)
	MOVEM.L $80(A3),D0-D3
	MOVEM.L D0-D3,$80(A2)
	MOVEM.L D0-D3,$78(A3)
	MOVEM.L $90(A3),D0-D3
	MOVEM.L D0-D3,$90(A2)
	MOVEM.L D0-D3,$88(A3)
	MOVE.W 2(A1),$98(A3)
	MOVE.W 6(A1),$9A(A3)
	MOVE.W 10(A1),$9C(A3)
	MOVE.W 14(A1),$9E(A3)
	ADD.L #16,A1
	ADD.L #160,A3
	ADD.L #160,A2
	DBF D6,SCHUIFZET
	RTS

POINTER1	DC.L HSCRTEXT1
POINTER2	DC.L HSCRTEXT2

HSCRTEXT1	DC.B "     "			; DON'T REMOVE OR CHANGE
	DC.B "-GAMENAME-"
	DC.B $00				; $0 TO END

HSCRTEXT2	DC.B "                "		; DON'T REMOVE OR CHANGE
	DC.B "HOTLINE PRESENTS ... "
	DC.B "  PRESS F1-F3 TO CHANGE MUSIC   "
	DC.B "  GREETINGS TO ...  "
	DC.B "   INTRO BY LETHAL!     "	; DON'T REMOVE OR CHANGE
	DC.B $00				; $0 TO END

LETTERS	DC.B "ABCDEFGHIJ",$00
	DC.B "KLMNOPQRST",$00
	DC.B "UVWXYZ1234",$00
	DC.B "567890!.-+",$00
	DC.B $FF

	EVEN

HEIGHT = 26

SCR_COL1	DS.W 57
SCR_COL2	DS.W 57

LOGO_COL1	DS.W 101
LOGO_COL2	DS.W 101

BAR_COL1	DC.W $0100,$0000,$0100,$0100,$0200,$0100,$0200,$0200 ; RED
	DC.W $0300,$0200,$0300,$0300,$0400,$0300,$0400,$0400 ; L.RED
	DC.W $0500,$0400,$0500,$0500,$0600,$0500,$0600,$0600 ; RED
	DC.W $0700,$0600,$0700,$0700,$0600,$0700	   ; + INTERVAL
	DC.W $0600,$0600,$0500,$0600,$0500,$0500,$0400,$0500 ; #56
	DC.W $0400,$0400,$0300,$0400,$0300,$0300,$0200,$0300
	DC.W $0200,$0200,$0100,$0200,$0100,$0100,$0000,$0100
	DC.W $0000,$0000,$0000,$0000,$0000

BAR_COL2	DC.W $0001,$0000,$0001,$0001,$0002,$0001,$0002,$0002 ; BLUE
	DC.W $0003,$0002,$0003,$0003,$0004,$0003,$0004,$0004 ; L.BLUE
	DC.W $0005,$0004,$0005,$0005,$0006,$0005,$0006,$0006 ; BLUE
	DC.W $0007,$0006,$0007,$0007,$0006,$0007	   ; + INTERVAL4
	DC.W $0006,$0006,$0005,$0006,$0005,$0005,$0004,$0005 ; #56
	DC.W $0004,$0004,$0003,$0004,$0003,$0003,$0002,$0003
	DC.W $0002,$0002,$0001,$0002,$0001,$0001,$0000,$0001
	DC.W $0000,$0000,$0000,$0000,$0000

PIC_COL1	DC.W $0100,$0000,$0100,$0100,$0210,$0100,$0210,$0210 ; GOLD
	DC.W $0320,$0210,$0320,$0320,$0430,$0320,$0430,$0430 ; YELLOW
	DC.W $0540,$0430,$0540,$0540,$0650,$0540,$0650,$0650 ; GOLD
	DC.W $0760,$0650,$0760,$0760,$0650,$0760	   ; + INTERVAL
	DC.W $0650,$0650,$0540,$0650,$0540,$0540,$0430,$0540 ; #100
	DC.W $0430,$0430,$0320,$0430,$0320,$0320,$0210,$0320
	DC.W $0210,$0210,$0100,$0210,$0100,$0100,$0000,$0100
	DC.W $0000,$0000,$0000,$0000,$0000,$0000
	DS.W 41

PIC_COL2	DC.W $0111,$0000,$0111,$0111,$0222,$0111,$0222,$0222 ; GRAY
	DC.W $0333,$0222,$0333,$0333,$0444,$0333,$0444,$0444 ; WHITE
	DC.W $0555,$0444,$0555,$0555,$0666,$0555,$0666,$0666 ; GRAY
	DC.W $0777,$0666,$0777,$0777,$0666,$0777	   ; + INTERVAL
	DC.W $0666,$0666,$0555,$0666,$0555,$0555,$0444,$0555 ; #100
	DC.W $0444,$0444,$0333,$0444,$0333,$0333,$0222,$0333
	DC.W $0222,$0222,$0111,$0222,$0111,$0111,$0000,$0111
	DC.W $0000,$0000,$0000,$0000,$0000,$0000
	DS.W 41

RED_COL	DC.W $0100,$0000,$0100,$0100,$0200,$0100,$0200,$0200 ; RED
	DC.W $0300,$0200,$0300,$0300,$0400,$0300,$0400,$0400 ; L.RED
	DC.W $0500,$0400,$0500,$0500,$0600,$0500,$0600,$0600 ; RED
	DC.W $0700,$0600,$0700,$0700,$0600,$0700	   ; + INTERVAL
	DC.W $0600,$0600,$0500,$0600,$0500,$0500,$0400,$0500 ; #56
	DC.W $0400,$0400,$0300,$0400,$0300,$0300,$0200,$0300
	DC.W $0200,$0200,$0100,$0200,$0100,$0100,$0000,$0100
	DC.W $0000,$0000,$0000,$0000,$0000

BLUE_COL	DC.W $0001,$0000,$0001,$0001,$0002,$0001,$0002,$0002 ; BLUE
	DC.W $0003,$0002,$0003,$0003,$0004,$0003,$0004,$0004 ; L.BLUE
	DC.W $0005,$0004,$0005,$0005,$0006,$0005,$0006,$0006 ; BLUE
	DC.W $0007,$0006,$0007,$0007,$0006,$0007	   ; + INTERVAL4
	DC.W $0006,$0006,$0005,$0006,$0005,$0005,$0004,$0005 ; #56
	DC.W $0004,$0004,$0003,$0004,$0003,$0003,$0002,$0003
	DC.W $0002,$0002,$0001,$0002,$0001,$0001,$0000,$0001
	DC.W $0000,$0000,$0000,$0000,$0000

LOGO	INCBIN EXTRAS\HOTLINE1.LOG

GAME	INCBIN FILE.PRG

EOF

	EVEN

CHARS	INCBIN EXTRAS\LAIR.CHR
