' ***********************************************************
' *                                                         *
' *     SOLITAIRE                      AUTEUR : CAET JC     *
' *                                                         *
' ***********************************************************
'
'
'
HIDEM
soli
'                                  PAGE DE PRESENTATION
PROCEDURE soli
  CLEAR
  CLS
  son
  @debut
  @col
  DIM t(9,9)
  FOR x=1 TO 9
    FOR y=1 TO 9
      t(x,y)=0
    NEXT y
  NEXT x
  FOR x=1 TO 7
    FOR y=1 TO 7
      t(x,y)=1
    NEXT y
  NEXT x
  FOR x=1 TO 2
    FOR y=1 TO 2
      t(x,y)=0
      t(x+5,y)=0
      t(x,y+5)=0
      t(x+5,y+5)=0
    NEXT y
  NEXT x
  t(5,7)=2
  t(3,7)=3
  @aff
  x=3
  y=7
  e=3
  r=7
  w=1
  @bouc
RETURN
'                                  BOUCLE / DEPLACEMENT
PROCEDURE bouc
  DO
    @dep
    w=3
    o=x
    p=y
    @dep1
    l=x
    m=y
    q=(o+l)/2
    s=(p+m)/2
    u=ABS(o-l)
    i=ABS(p-m)
    IF w=2
      IF u+i=2 AND u*i=0 AND t(q,s)=1
        t(q,s)=2
        t(o,p)=2
        t(l,m)=3
        w=1
        @aff
      ENDIF
    ENDIF
    IF t(o,p)<>2
      t(o,p)=1
      IF o=l AND p=m
        w=1
      ENDIF
      hum
      @aff
    ENDIF
    @verif
  LOOP
RETURN
'                                  SELECTION CASES DEPART / ARRIVEE
PROCEDURE dep1
  DO
    @getkey
    EXIT IF a$=" "
    IF a$="4"
      x=x-1
      IF x<1
        x=1
      ENDIF
    ENDIF
    IF a$="6"
      x=x+1
      IF x>7
        x=7
      ENDIF
    ENDIF
    IF a$="8"
      y=y-1
      IF y<1
        y=1
      ENDIF
    ENDIF
    IF a$="2"
      y=y+1
      IF y>7
        y=7
      ENDIF
    ENDIF
    IF a$<>" "
      IF t(x,y)=0
        x=e
        y=r
        @aff
      ELSE
        t(e,r)=w
        w=t(x,y)
        t(x,y)=3
        e=x
        r=y
        @aff
      ENDIF
    ENDIF
  LOOP
RETURN
PROCEDURE dep
  DO
    @getkey
    EXIT IF a$=" " AND w<>2
    IF a$="4"
      x=x-1
      IF x<1
        x=1
      ENDIF
    ENDIF
    IF a$="6"
      x=x+1
      IF x>7
        x=7
      ENDIF
    ENDIF
    IF a$="8"
      y=y-1
      IF y<1
        y=1
      ENDIF
    ENDIF
    IF a$="2"
      y=y+1
      IF y>7
        y=7
      ENDIF
    ENDIF
    IF a$<>" "
      IF t(x,y)=0
        x=e
        y=r
        @aff
      ELSE
        t(e,r)=w
        w=t(x,y)
        t(x,y)=3
        e=x
        r=y
        @aff
      ENDIF
    ENDIF
  LOOP
RETURN
'                                  DEPLACEMENTS ?
PROCEDURE getkey
  ' LE PROGRAMME FONCTIONNAIT AU CLAVIER
  ' DANS SA PREMIERE VERSION !
  a$=""
  REPEAT
    st=STICK(1)
  UNTIL st<>0 OR STRIG(1)<>0
  IF st=2
    a$="2"
  ENDIF
  IF st=1
    a$="8"
  ENDIF
  IF st=8
    a$="6"
  ENDIF
  IF st=4
    a$="4"
  ENDIF
  IF STRIG(1)<>0
    a$=" "
    REPEAT
    UNTIL STRIG(1)=0
  ENDIF
RETURN
'                                  AFFICHE LES PIONS
PROCEDURE aff
  FOR a=1 TO 7
    FOR b=1 TO 7
      IF t(a,b)<>0
        IF t(a,b)=1
          PUT (a+2)*25,((b-1)*25)+15,r$
        ENDIF
        IF t(a,b)=2
          PUT (a+2)*25,((b-1)*25)+15,t$
        ENDIF
        IF t(a,b)=3
          PUT (a+2)*25,((b-1)*25)+15,b$
        ENDIF
      ENDIF
    NEXT b
  NEXT a
RETURN
'                                  LES COULEURS
PROCEDURE col
  COLOR 9
  CIRCLE 160,100,109,0,3600
  DEFFILL 11,1,5
  FILL 10,60
RETURN
'                                  CHARGEMENT IMAGES / CAPTURE PIONS
PROCEDURE debut
  a$=SPACE$(32066)
  a=VARPTR(a$)
  BLOAD "SOLI.pi1",a
  FOR t=0 TO 15
    SETCOLOR t,0
  NEXT t
  BMOVE a,XBIOS(2)-34,32000
  GET 12,16,31,35,b$
  GET 12,37,31,56,r$
  GET 12,61,31,80,t$
  DEFFILL 0,1,2
  PBOX 10,10,50,150
  SGET a$
  CLS
  q=XBIOS(6,L:XBIOS(2)-32)
  rideau
  SPUT a$
  REPEAT
  UNTIL STRIG(1)<>0
  rideau
RETURN
'                                  EFFET "RIDEAU"
PROCEDURE rideau
  c=8
  FOR t=199 TO 0 STEP -1
    c=c+0.2
    IF c>12
      c=8
    ENDIF
    COLOR c
    LINE 0,t,319,t
  NEXT t
  FOR t=0 TO 199
    COLOR 0
    LINE 0,t,319,t
  NEXT t
RETURN
'                                  VERIFICATION DES COUPS ENCORE POSSIBLES
PROCEDURE verif
  t(x,y)=1
  @aff
  ind=0
  FOR ys=1 TO 7
    FOR xs=1 TO 7
      IF t(xs,ys)<>0
        IF t(xs,ys)=1
          IF t(xs+1,ys)=1 AND t(xs+2,ys)=2
            ind=ind+1
          ENDIF
          IF xs>1
            IF t(xs-1,ys)=1 AND t(xs-2,ys)=2
              ind=ind+1
            ENDIF
          ENDIF
          IF t(xs,ys+1)=1 AND t(xs,ys+2)=2
            ind=ind+1
          ENDIF
          IF ys>1
            IF t(xs,ys-1)=1 AND t(xs,ys-2)=2
              ind=ind+1
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    NEXT xs
  NEXT ys
  IF ind=0
    @fin
  ENDIF
  t(x,y)=3
  @aff
RETURN
'                                  C'EST LA FIN (PLUS DE COUPS)
PROCEDURE fin
  FOR ys=1 TO 7
    FOR xs=1 TO 7
      IF t(xs,ys)=1
        to=to+1
        @clig
      ENDIF
    NEXT xs
  NEXT ys
  PAUSE 50
  @rideau
  IF to>1
    PRINT AT(10,10);"IL RESTAIT ";to;" PIONS !"
  ENDIF
  IF to=1
    PRINT AT(10,10);"PLUS QU'UN PION ? BRAVO !!!"
  ENDIF
  PAUSE 150
  @soli
RETURN
'                                  DERNIERS PIONS CLIGNOTENT
PROCEDURE clig
  hum
  FOR ut=1 TO 3
    t(xs,ys)=1
    @aff
    t(xs,ys)=2
    @aff
  NEXT ut
RETURN
'                                  SON SI MOUVEMENT IMPOSSIBLE
PROCEDURE son
  DO
    READ com
    EXIT IF com=-1
    s1$=s1$+CHR$(com)
  LOOP
RETURN
'                                  MOUVEMENT IMPOSSIBLE -> HUM!
PROCEDURE hum
  VOID XBIOS(32,L:VARPTR(s1$))
RETURN
'                                  DATAS SON
DATA 0,0,1,0,2,0,3,0,4,0,5,0
DATA 8,16,9,16,10,0
DATA 13,1,11,64,12,31
DATA 7,254
DATA 0,122,1,8,255,16
DATA 0,0,1,0,2,0,3,0,4,0,5,0,7,255,8,15,9,15,10,15
DATA 255,0,-1,0
'
' ********************FIN DE PROGRAMME******************************
