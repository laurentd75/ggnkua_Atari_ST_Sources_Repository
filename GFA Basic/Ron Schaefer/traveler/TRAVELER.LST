' ########################################################################
' ########################################################################
' #####################                           ########################
' #####################  TRIP PLANNER ST VERSION  ########################
' #####################    BY RON SCHAEFER MD     ########################
' #####################   SCHAEFER SUPERGRAPHIC   ########################
' ##################### (C) 1988 ANTIC PUBLISHING ########################
' #####################                           ########################
' ########################################################################
' ########################################################################
Dim Place$(41),Table(40,40),Visit(40),Ftrip(20),V(20),Colr$(2)
Dim Lat_deg(40),Lat_sec(40),Lon_deg(40),Lon_sec(40),Lon(40),Lat(40)
Dim Mstrip$(25),Ck!(40),Spalette%(16,3)
Dim Ls%(12),Cl$(12)
Rez%=Xbios(4)
If Rez%=0 Then
  @Sound_1(5,5)
  Alert 3,"Sorry Traveler the Travel|Planner works does not|work in low resolution.|Switch to Med or Hi rez.",1," OK ",Dummy
  End
Endif
@Save_pal
Switch!=True
If Rez%=2 Then
  Type$="PI3"
  Setcolor 1,0,0,0
  Setcolor 0,7,7,7
  Deftext 1,0,0,13
Else
  Setcolor 0,0,0,0
  Setcolor 3,7,7,7
  Type$="PI2"
Endif
@Degas("TRAVELER."+Type$,1)
Max_size=40
Hstart=0                       ! initial offset for table display
Vstart=0
Hsize=8
Vsize=17
Resip!=True                    ! initially have table do auto reciprical data
' ------------------------------ READ MENU STRIP DATA --------------------------
Restore Strip_data
For I%=0 To 22
  Read Mstrip$(I%)
Next I%
' ----------------------------- PROGRAM CONTROL LOOP ---------------------------
Menu Mstrip$()
Sget Title$
On Menu  Gosub Menu_handler
Do
  On Menu
  Exit If All_done!
Loop
' -------------------------------- MENU BAR HANDLER --------------------------
Procedure Menu_handler
  If Mstrip$(Menu(0))="  Load Lat/Lon  "
    @Mgrow
    Fileselect "\*.LAT","",Fn$
    @Mshrink
    If Fn$<>"" Then
      @Clear_matrix
      Hstart=0                       ! initial offset for table display
      Vstart=0
      Switch!=False
      If Exist(Fn$) Then
        Defmouse 2
        Open "I",#1,Fn$
        Input #1,Size
        For N%=1 To Size
          Input #1,Place$(N%),Lat_deg(N%),Lat_sec(N%),Lon_deg(N%),Lon_sec(N%)
          Lon(N%)=Lon_deg(N%)+Lon_sec(N%)/60
          Lat(N%)=Lat_deg(N%)+Lat_sec(N%)/60
        Next N%
        Close #1
        @Calculate_table
        Defmouse 0
      Else
        @Sound_1(5,5)
        Alert 3,"Sorry I am unable to|find the file|"+Fn$,1," Bummer ",Dummy
      Endif
    Endif
  Endif
  If Mstrip$(Menu(0))="  Calculate  "
    @Mgrow
    Hidem
    Cls
    Openw 0
    Clearw 0
    Print At(1,1);
    Print "Calculate the distance between two points using their latitude & longitude."
    Print "Enter eastern longitude and southern latitude as negative numbers."
    Print
    Print "The first place:"
    @Get_coords(6)
    Lon(1)=Lon_deg+Lon_sec/60
    Lat(1)=Lat_deg+Lat_sec/60
    Print
    Print "The second place:"
    @Get_coords(11)
    Lon(2)=Lon_deg+Lon_sec/60
    Lat(2)=Lat_deg+Lat_sec/60
    @Calculate_distance(Lon(1),Lon(2),Lat(1),Lat(2))
    Print
    Print "The calculated distance is ";Distance;" miles."
    Print
    Input "Press return to continue. ",Dummy$
    @Mshrink
    Showm
    Menu Off
    Sput Title$
  Endif
  If Mstrip$(Menu(0))="  Load Table  "
    @Mgrow
    Fileselect "\*.MAP","",Fn$
    @Mshrink
    If Fn$<>"" Then
      Hstart=0                       ! initial offset for table display
      Vstart=0
      @Clear_matrix
      Switch!=False
      If Exist(Fn$) Then
        Defmouse 2
        Open "I",#1,Fn$
        Input #1,Size
        For N%=1 To Size
          Input #1,Place$(N%),Lat_deg(N%),Lat_sec(N%),Lon_deg(N%),Lon_sec(N%)
          Lon(N%)=Lon_deg(N%)+Lon_sec(N%)/60
          Lat(N%)=Lat_deg(N%)+Lat_sec(N%)/60
        Next N%
        For X%=1 To Size
          For Y%=1 To Size
            Input #1,Table(X%,Y%)
          Next Y%
        Next X%
        Close #1
        Defmouse 0
      Else
        @Sound_1(5,5)
        Alert 3,"Sorry I am unable to|find the file|"+Fn$,1," Bummer ",Dummy
      Endif
    Endif
  Endif
  ' ------------------------------ Save a table of data ------------
  If Mstrip$(Menu(0))="  Save Table  "
    @Mgrow
    If Place$(1)="" Then
      @Sound_1(5,5)
      Alert 3,"There is no data table in|memory at this time.  Please|create or load one.",1," OK ",Dummy
      @Mshrink
    Else
      Getfile:
      Fileselect "\*.MAP","",Fn$
      @Mshrink
      If Fn$<>"" Then
        If Exist(Fn$) Then
          @Sound_1(5,5)
          Alert 3,"|That file exists.  Do you|want to overwrite it?",0,"Yes|No",Ow
          If Ow=2 Then
            Goto Getfile
          Endif
        Endif
        Defmouse 2
        Size=0
        For N%=1 To Max_size
          If Place$(N%)<>"" Then
            Size=Size+1
          Endif
        Next N%
        Open "O",#1,Fn$
        Print #1,Size
        For N%=1 To Size
          Print #1,Place$(N%);",";Lat_deg(N%);",";Lat_sec(N%);",";Lon_deg(N%);",";Lon_sec(N%)
        Next N%
        For X%=1 To Size
          For Y%=1 To Size
            Print #1,Table(X%,Y%)
          Next Y%
        Next X%
        Close #1
        Defmouse 0
      Endif
    Endif
  Endif
  ' --------------------------------- EDIT TABLE ----------------------
  If Mstrip$(Menu(0))="  Edit Table  " Or Mstrip$(Menu(0))="  Create Table  "
    @Mgrow
    If Place$(1)="" And Mstrip$(Menu(0))="  Edit Table  " Then
      @Sound_1(5,5)
      Alert 3,"There is no data table in|memory at this time.  Please|create or load one.",1," OK ",Dummy
      @Mshrink
    Else
      If Mstrip$(Menu(0))="  Create Table  " Then
        Hstart=0                       ! initial offset for table display
        Vstart=0
        @Clear_matrix
      Endif
      Cls
      Openw 0
      Fullw 0
      Clearw 0
      If Mstrip$(Menu(0))="  Create Table  " Then
        Tit$="     CREATE A NEW DATA TABLE"
      Else
        Tit$="EDIT AN EXISTING DATA TABLE"
      Endif
      @Draw_table
      @Draw_grid
      Mouse_type=5
      Defmouse 5
      Do
        Mouse Mx,My,K
        Mt=5
        If My>152*Rez% Or My<8*Rez% Then
          Mt=3
        Endif
        If Mt<>Omt Then
          Defmouse Mt
          Omt=Mt
        Endif
        If K=1 Then
          Xpos=Mx Div 72
          Ypos=(My Div 8*Rez%)-1
          ' --------------------- test to see if in data range if so change
          If Xpos>0 And Ypos<18 And Ypos>0 And Xpos<9 Then
            @Sound_2(6,6)
            Print At(1,20);
            Input "Enter the new number:";New
            If Resip! Then
              Table(Xpos+Hstart,Ypos+Vstart)=New
              Table(Ypos+Vstart,Xpos+Hstart)=New
            Else
              Table(Xpos+Hstart,Ypos+Vstart)=New
            Endif
            Cls
            @Draw_table
            @Draw_grid
          Endif
          ' -------------- test to see if in place name range if so change
          If Xpos=0 Or Ypos=0 Then
            @Sound_2(6,6)
            Print At(1,20);
            Print "Enter the new place name."
            Print "(Max of 8 Characters long):";
            Form Input 8,New_name$
            Nm=Max(Xpos+Hstart,Ypos+Vstart)
            Place$(Nm)=New_name$
            Print At(1,20);"                         ";
            Print At(1,21);"                                   ";
            @Get_coords(20)
            Lon_deg(Nm)=Lon_deg
            Lon_sec(Nm)=Lon_sec
            Lat_deg(Nm)=Lat_deg
            Lat_sec(Nm)=Lat_sec
            Lon(Nm)=Lon_deg+Lon_sec/60
            Lat(Nm)=Lat_deg+Lat_sec/60
            @Draw_it
          Endif
          ' --------------------- test to see if changing inverse
          If Mx>557 And My>178*Rez% And Mx<580 And My<187*Rez% Then
            @Sound_2(6,6)
            Resip!=True
            @Ch_resip
          Endif
          If Mx>583 And My>178*Rez% And Mx<611 And My<187*Rez% Then
            @Sound_2(6,6)
            Resip!=False
            @Ch_resip
          Endif
          ' ----------------------------- test to see if in shifting boxes
          If Mx>480 And My>167*Rez% And Mx<511 And My<177*Rez% Then    !HOME
            @Sound_2(5,5)
            If Switch! Then
              Switch!=False
              Vstart=0
              Hstart=0
            Else
              Switch!=True
              Vstart=Size-17
              Hstart=Size-8
              If Vstart<0 Then
                Vstart=0
              Endif
              If Hstart<0 Then
                Hstart=0
              Endif
            Endif
            @Draw_it
          Endif
          If Mx>480 And My>157*Rez% And Mx<511 And My<167*Rez% Then    !UP
            Vstart=Vstart-1
            If Vstart<0 Then
              @Mgrow
              @Sound_1(5,5)
              Alert 3,"|You have reached the minimum|size for the data table.",1," OK ",Dummy
              @Mshrink
              Defmouse 5
              Vstart=0
            Else
              @Sound_2(6,6)
              @Draw_it
            Endif
          Endif
          If Mx>480 And My>177*Rez% And Mx<511 And My<187*Rez% Then    !DOWN
            Vstart=Vstart+1
            If Vstart+Vsize>Max_size Then
              @Mgrow
              @Sound_1(5,5)
              Alert 3,"|You have reached the maximum|size for the data table.",1," OK ",Dummy
              @Mshrink
              Defmouse 5
              Vstart=Vstart-1
            Else
              @Sound_2(6,6)
              @Draw_it
            Endif
          Endif
          If Mx>450 And My>167*Rez% And Mx<480 And My<177*Rez% Then    !Left
            Hstart=Hstart-1
            If Hstart<0 Then
              @Mgrow
              @Sound_1(5,5)
              Alert 3,"|You have reached the minimum|size for the data table.",1," OK ",Dummy
              @Mshrink
              Defmouse 5
              Hstart=0
            Else
              @Sound_2(6,6)
              @Draw_it
            Endif
          Endif
          If Mx>510 And My>167*Rez% And Mx<540 And My<177*Rez% Then    !Right
            Hstart=Hstart+1
            If Hstart+Hsize>Max_size Then
              @Mgrow
              @Sound_1(5,5)
              Alert 3,"|You have reached the maximum|size for the data table.",1," OK ",Dummy
              @Mshrink
              Defmouse 5
              Hstart=Hstart-1
            Else
              @Sound_2(6,6)
              @Draw_it
            Endif
          Endif
        Endif
        Exit If K=2
      Loop
      '      @Findxy
      Closew 0
      @Mshrink
      Defmouse 0
      Menu Off
      Sput Title$
    Endif
  Endif
  ' ---------------------------------- EXIT ---------------------
  If Mstrip$(Menu(0))="  EXIT  "
    @Mgrow
    Alert 2,"|Do you really want to quit?",1,"Yes|No",Dummy
    @Mshrink
    If Dummy=1 Then
      Graphmode 1
      Cls
      @Restorepal
      Edit
      End
    Endif
  Endif
  If Mstrip$(Menu(0))="  Plan Trip  "
    @Calc
  Endif
  If Mstrip$(Menu(0))="  About Traveler "
    @Mgrow
    Graphmode 1
    Openw 0
    Cls
    Color 1
    Deftext 1,0,0,6
    Lx=100
    Rx=539
    Ty=20
    By=189
    If Rez%=2
      By=By+By-Ty
    Endif
    Ls%(0)=1
    Ls%(1)=4
    For N%=2 To 12
      Ls%(N%)=0
    Next N%
    Cl$(1)="     Trip Planner Saves Your Money."
    Cl$(2)=""
    Cl$(3)="      Written by Ron Schaefer M.D."
    Cl$(4)="       (C) 1988 Antic Publishing"
    Cl$(5)=" "
    Cl$(6)="   Written and compiled with GFA BASIC"
    Cl$(7)="           (C) 1987, MichTron"
    Cl$(8)="  Atari 8 bit version 'Your Best Route'"
    Cl$(9)="         by Jeffery Summers M.D."
    Cl$(10)="           Antic November 1987"
    Cl$(11)="    Atari 8 bit consultant Bill Buck."
    Free$=Str$(Fre(0))
    Cl$(12)="          Free memory:"+Free$
    Deffill 0,2,8
    Defline 1,2
    Pbox Lx,Ty,Rx,By
    Box Lx,Ty,Rx,By
    Defline 1,1
    Box Lx+5,Ty+2,Rx-5,By-2
    Deftext 3,16,0,12
    Shift=10
    Text 235,Ty+25,"TRAVELER"
    Deftext 1,Ls%(0),0,6
    For X%=1 To 12
      Deftext 1,Ls%(X%)
      Text Lx+55,(Ty+25+(X%)*Shift),Cl$(X%)
    Next X%
    Deftext 1,1,0,6
    Text Lx+182,By-9,"Continue"
    Deftext 1,0
    Defline 1,2
    Box Lx+175,By-19,Rx-186,By-4
    Repeat
      K=Mousek
      X=Mousex
      Y=Mousey
    Until (K>0 And X>Lx+175 And X<Rx-186 And Y>By-19 And Y<By-4) Or Inkey$<>""
    Graphmode 3
    Pbox Lx+175,By-19,Rx-186,By-4
    If Rez%=2 Then
      Deftext 1,0,0,13
    Else
      Deftext 1,0,0,6
    Endif
    Pause 5
    Graphmode 1
    @Mshrink
    Menu Off
    Sput Title$
  Endif
  Menu Off
Return
' ------------------------------------------------------------------
Procedure Clear_matrix
  Arrayfill Table(),0
  For N%=1 To Max_size
    Place$(N%)=""
  Next N%
Return
' --------------------------------------------------------------------
Procedure Draw_it
  Cls
  @Draw_table
  @Draw_grid
Return
' ------------------------------- DRAW GRID FOR TABLE --------------
Procedure Draw_grid
  Color 3
  For N%=1 To 19
    Line 0,8*N%*Rez%,639,8*N%*Rez%
  Next N%
  For N%=1 To 8
    Line 72*N%,8*Rez%,72*N%,152*Rez%
  Next N%
  Color 1
  Box 480,157*Rez%,510,187*Rez%
  Box 450,167*Rez%,540,177*Rez%
  Text 490,165*Rez%,Chr$(1)
  Text 490,185*Rez%,Chr$(2)
  Text 460,175*Rez%,Chr$(4)
  Text 520,175*Rez%,Chr$(3)
  Text 490,175*Rez%,Chr$(7)
  Text 130,187*Rez%,"Click-Left to Select, Click-Right to Exit."
  @Ch_resip
Return
' ----------------------- CHANGE INVERSE TEXT LIGTH TO DARK -----------------
Procedure Ch_resip
  Text 550,175*Rez%," Inverse"
  If Resip! Then
    Text 560,185*Rez%,"ON"
    Deftext 1,2
    Text 585,185*Rez%,"OFF"
    Deftext 1,0
  Else
    Deftext 1,2
    Text 560,185*Rez%,"ON"
    Deftext 1,0
    Text 585,185*Rez%,"OFF"
    Deftext 1,0
  Endif
  Deftext 1,0
Return
' ----------------------------- DRAW DATA TABLE ---------------------
Procedure Draw_table
  Print At(25,1);Tit$
  For X%=1 To Min(Hsize,Max_size)
    For Y%=1 To Min(Vsize,Max_size)
      Print At(X%*9+1,2);Place$(X%+Hstart)
      Print At(1,Y%+2);Place$(Y%+Vstart)
      If Place$(X%+Hstart)<>"" And Place$(Y%+Vstart)<>"" Then
        Print At(X%*9+3,Y%+2);Table(X%+Hstart,Y%+Vstart)
      Endif
    Next Y%
    Print
  Next X%
Return
' ----------------------------------------------------------
Procedure Calc
  @Mgrow
  If Place$(1)="" Then
    @Sound_1(5,5)
    Alert 3,"There is no data table in|memory at this time.  Please|create or load one.",1," OK ",Dummy
    @Mshrink
  Else
    Size=0
    For N%=1 To Max_size
      If Place$(N%)<>"" Then
        Size=Size+1
      Endif
    Next N%
    For Lp=1 To Size
      Ck!(Lp)=False
    Next Lp
    Cls
    Openw 0
    Fullw 0
    Clearw 0
    Print At(1,1);
    For N%=1 To 20
      If Place$(N%)<>"" Then
        Print At(5,N%);N%;"  ";Place$(N%);
      Endif
      If Place$(N%+20)<>"" Then
        Print At(37,N%);N%+20;"  ";Place$(N%+20)
      Endif
    Next N%
    Ssloc:
    Print At(5,21);
    Input "Enter the starting point (0 if it does not matter):";Sloc
    If Sloc<0 Or Sloc>Size Then
      @Sound_1(5,5)
      Alert 3,"Your Start was out of|the available range|of 1 to "+Str$(Size)+".",1," OK ",Dummy
      Goto Ssloc
    Endif
    Ck!(Sloc)=True
    Deftext 3
    If Sloc<21 And Sloc>0 Then
      Print At(18,Sloc);"Start"
    Endif
    If Sloc>20 And Sloc<41 Then
      Print At(50,Sloc-20);"Start"
    Endif
    Deftext 1
    Seloc:
    Print At(5,21);
    Input "  Enter the ending point (0 if it does not matter):";Eloc
    If Eloc<0 Or Eloc>Size Then
      @Sound_1(5,5)
      Alert 3,"Your ending point was out of|the available range|of 1 to "+Str$(Size)+".",1," OK ",Dummy
      Goto Seloc
    Endif
    Ck!(Eloc)=True
    Deftext 3
    If Eloc<21 And Eloc>0 Then
      Print At(25,Eloc);"Finish"
    Endif
    If Eloc>20 And Eloc<41 Then
      Print At(57,Eloc-20);"Finish"
    Endif
    Deftext 1
    I=1
    Do
      Svloc:
      Print At(5,21);"                                                          "
      Print At(5,21);
      Input "Enter the points you want to visit, 0=exit:";Vloc
      If Vloc<0 Or Vloc>Size Then
        @Sound_1(5,5)
        Alert 3,"Your visit was out of|the available range|of 1 to "+Str$(Size)+".",1," OK ",Dummy
        Goto Svloc
      Endif
      If Ck!(Vloc) And Vloc<>0 Then
        @Sound_1(5,5)
        Alert 3,"|You have selected that |place once aleady.",1," OK ",Dummy
        Goto Svloc
      Endif
      Ck!(Vloc)=True
      Deftext 2
      If Vloc<21 And Vloc>0 Then
        Print At(18,Vloc);"Visit"
      Endif
      If Vloc>20 And Vloc<41 Then
        Print At(50,Vloc-20);"Visit"
      Endif
      Deftext 1
      Visit(I)=Vloc
      Inc I
      Exit If Vloc=0
    Loop
    Cls
    If Sloc>0 Then
      Print Space$(30);"Start at ";Place$(Sloc)
    Endif
    If I<=2 Then
      Curtot=Table(Sloc,Eloc)
    Else
      Arrayfill V(),0
      I=I-2
      Curtot=99999999
      For J%=1 To I
        V(J%)=I-J%+1
      Next J%
      4219:
      Tot=0
      For J%=1 To I-1
        Tot=Tot+Table(Visit(V(J%)),Visit(V(J%+1)))
      Next J%
      Tot=Tot+Table(Sloc,Visit(V(1)))+Table(Visit(V(I)),Eloc)
      If Tot<Curtot Then
        @Sound_2(2,2)
        Print At(1,1);"Running best total=";Tot
        @Final_trip
      Endif
      4230:
      Index=1
      4235:
      V(Index)=V(Index)+1
      If V(Index)>I Then
        V(Index)=1
        Index=Index+1
        Goto 4235
      Endif
      If Index>I Then
        Goto 4400
      Endif
      For J%=1 To I
        For K%=1 To I
          If (K%<>J%) And V(K%)=V(J%) Then
            Jrem=J%
            Krem=K%
            Out!=True
          Endif
        Next K%
      Next J%
      If Out! Then
        Out!=False
        J%=Jrem
        K%=Krem
        Goto 4230
      Endif
      Goto 4219
      4400:
      For J%=1 To I
        If J%=1 And Sloc=0 Then
          Print Space$(30);"Start at ";Place$(Visit(Ftrip(J%)))
        Else
          If J%=I And Eloc=0 Then
            Print Space$(30);"End at ";Place$(Visit(Ftrip(J%)))
          Else
            Print Space$(30);"Then Goto ";Place$(Visit(Ftrip(J%)))
          Endif
        Endif
      Next J%
    Endif
    If Eloc>0 Then
      Print Space$(30);"End at ";Place$(Eloc)
    Endif
    Print Space$(30);"Total mileage ";Curtot
    Print At(1,1);"                          "
    Print At(25,20);"Click or press Return to continue."
    Do
      Exit If Mousek>0 Or Inkey$<>""
    Loop
    Map!=False
    If Eloc>0 And Lat(Eloc)>20 And Lat(Eloc)<50 And Lon(Eloc)>60 And Lon(Eloc)<135 Then
      Map!=True
    Endif
    If Sloc>0 And Lat(Sloc)>20 And Lat(Sloc)<50 And Lon(Sloc)>60 And Lon(Sloc)<135 Then
      Map!=True
    Endif
    For J%=1 To I
      If Lat(Visit(Ftrip(J%)))>20 And Lat(Visit(Ftrip(J%)))<50 And Lon(Visit(Ftrip(J%)))>60 And Lon(Visit(Ftrip(J%)))<135 Then
        Map!=True
      Endif
    Next J%
    If Map! Then
      If Exist("USA."+Type$)
        Alert 2,"Would you like to see this|data ploted out on a map|of the USA?",0," Yes | No ",Yn
        If Yn=1 Then
          @Plot
        Endif
      Endif
    Endif
    Alert 2,"Would you like to create an|overlay file for the program|Maps and Legends?",0," Yes | No ",Yn
    If Yn=2 Then
      @Mgrow
    Endif
    If Yn=1 Then
      @Mgrow
      Getafile:
      Fileselect "\*.TXT","",Fn$
      @Mshrink
      If Fn$<>"" Then
        If Exist(Fn$) Then
          @Sound_1(5,5)
          Alert 3,"|That file exists.  Do you|want to overwrite it?",0,"Yes|No",Ow
          If Ow=2 Then
            Goto Getafile
          Endif
        Endif
        Defmouse 2
        Open "O",#1,Fn$
        If Sloc=0 Then
          Print #1;0,Lat(Visit(Ftrip(1))),360-Lon(Visit(Ftrip(1)))
        Else
          Print #1;0,Lat(Sloc),360-Lon(Sloc)
        Endif
        For J=1 To I
          Print #1;1,Lat(Visit(Ftrip(J))),360-Lon(Visit(Ftrip(J)))
        Next J
        If Eloc<>0 Then
          Print #1;1,Lat(Eloc),360-Lon(Eloc)
        Endif
        Close #1
        Defmouse 0
      Endif
    Endif
    Closew 0
    Menu Off
    Sput Title$
  Endif
Return
'
Procedure Calculate_distance(Lon1,Lon2,Lat1,Lat2)
  ' Calculates distance between two points.
  '
  Lon_diff=Abs(Lon1-Lon2)
  If Lon_diff>180
    Lon_diff=360-Lon_diff
  Endif
  If Lon_diff=0 And Lat1-Lat2=0
    Distance=0
  Else
    Latb_diff=90-Lat1
    Latc_diff=90-Lat2
    Cos_a=Cos(Lon_diff*Pi/180)
    Cos_c=Cos(Latc_diff*Pi/180)
    Sin_c=Sin(Latc_diff*Pi/180)
    Cos_b=Cos(Latb_diff*Pi/180)
    Sin_b=Sin(Latb_diff*Pi/180)
    A_cos=Cos_b*Cos_c+Sin_b*Sin_c*Cos_a
    Arccos=90-Atn(A_cos/Sqr(-A_cos*A_cos+1))*180/Pi
    Distance=Int(Arccos/360*25000)
  Endif
Return
'
Procedure Final_trip
  For J=0 To 9
    Ftrip(J)=V(J)
  Next J
  Curtot=Tot
Return
' -------------------------------- SOUND ROUTINES ------------------------------
Procedure Sound_1(Snd,Snd1)
  Sound 1,15,Snd,Snd1
  Wave 1,1,9,6000
Return
Procedure Sound_2(Snd,Snd1)
  Sound 1,15,Snd,Snd1
  Wave 1,1,8,512,5
  Wave 0,0
Return
Procedure Rect1
  Graphmode 1
  Color 1
  Target=Timer+120
  Repeat
    Box I Mod 640,I Mod 200,639-I Mod 640,199-I Mod 200
    Add I,8
  Until Timer>Target
Return
' ----------------------------- MENU GROW BOX ROUTINE ------------------------
Procedure Mgrow
  @Sound_2(5,5)
  Mx=300
  My=90*Rez%
  Sw=40
  Sh=20*Rez%
  Ex=0
  Ey=0
  Ew=639
  Eh=199*Rez%
  @Grow_box
Return
' ---------------------------------- GROW BOX ----------------------------------
Procedure Grow_box
  Dpoke Gintin,Mx              ! X Coordinate of Box in it's Initial Size
  Dpoke Gintin+2,My            ! Y Coordinate of Box in it's Initial Size
  Dpoke Gintin+4,Sw            ! Initial WIDTH of Box
  Dpoke Gintin+6,Sh            ! Initial HEIGHT of Box
  Dpoke Gintin+8,Ex            ! X Coordinate of Box in it's Final Size
  Dpoke Gintin+10,Ey           ! Y Coordinate of Box in it's Final Size
  Dpoke Gintin+12,Ew           ! WIDTH of Box in it's Final Size
  Dpoke Gintin+14,Eh           ! HEIGHT of Box in it's Final Size
  Gemsys (73)                  ! A.E.S. Graphics Library Op Code
Return
' ------------------------------------------------------------------
Procedure Mshrink
  @Sound_2(4,4)
  Mx=300
  My=90*Rez%
  Ew=40
  Eh=20*Rez%
  Sx=0
  Sy=0
  Sw=639
  Sh=199*Rez%
  @Shrink_box
Return
' -------------------------------- SHRINK BOX ----------------------------------
Procedure Shrink_box
  Dpoke Gintin,Mx              ! X Coordinate of Box in it's Final Size
  Dpoke Gintin+2,My            ! Y Coordinate of Box in it's Final Size
  Dpoke Gintin+4,Ew            ! WIDTH of Box in it's Final Size
  Dpoke Gintin+6,Eh            ! Height of Box in it's Final Size
  Dpoke Gintin+8,Sx            ! X Coordinate of Box in it's Initial Size
  Dpoke Gintin+10,Sy           ! Y Coordinate of Box in it's Initial Size
  Dpoke Gintin+12,Sw           ! Initial WIDTH of Box
  Dpoke Gintin+14,Sh           ! Initial HEIGHT of Box
  Gemsys (74)                  ! A.E.S. Graphics Library Op Code
Return
' --------------------------- LOAD DEGAS TYPE PIC ---------------------
Procedure Degas(Filename$,Col)
  If Exist(Filename$) Then
    Cls
    Open "I",#2,Filename$
    Temp$=Input$(36,#2)
    Colr$(Col)=Mid$(Temp$,3,36)
    Close #2
    Void Xbios(6,L:Varptr(Colr$(Col)))
    Physbase=Xbios(2)
    Bload Filename$,Physbase-34
    Clr Temp$
  Else
    @Sound_1(5,5)
    Alert 3,"Picture file not found",1,"OK",Dummy
  Endif
Return
' ==========================
Procedure Calculate_table
  Vinc=0
  For H%=1 To Size
    Vinc=Vinc+1
    For V%=Vinc To Size
      If H%<>V% Then
        @Calculate_distance(Lon(H%),Lon(V%),Lat(H%),Lat(V%))
        Table(H%,V%)=Distance
        Table(V%,H%)=Distance
      Endif
    Next V%
  Next H%
Return
Procedure Get_coords(Row)
  Local L$
  Print At(1,Row);"Enter the longitude degrees: ";
  L$=""
  Form Input 4 As L$
  Lon_deg=Val(L$)
  L$=""
  Print At(36,Row);"minutes: ";
  Form Input 4 As L$
  Lon_sec=Val(L$)
  Print At(1,Row+1);"Enter the latitude degrees: ";
  L$=""
  Form Input 4 As L$
  Lat_deg=Val(L$)
  Print At(36,Row+1);"minutes: ";
  L$=""
  Form Input 4 As L$
  Lat_sec=Val(L$)
Return
Procedure Plot
  Openw 0
  Fullw 0
  @Mgrow
  If Not Usa! Then
    @Degas("USA."+Type$,2)
    Usa!=True
    Sget Usa$
  Else
    Void Xbios(6,L:Varptr(Colr$(2)))
    Sput Usa$
  Endif
  If Rez%=2 Then
    Defline 3,2,0,0
    Deffill 0
    Color=0
  Else
    Defline 1,1,0,0
    Deffill 2
    Color 2
  Endif
  If Sloc=0 Then
    Draw ((127-Lon(Visit(Ftrip(1))))/63)*615+9,((50-Lat(Visit(Ftrip(1))))/26)*166+19
  Else
    Circle ((127-Lon(Sloc))/63)*615+9,(((50-Lat(Sloc))/26)*166+19)*Rez%,5
    Draw ((127-Lon(Sloc))/63)*615+9,(((50-Lat(Sloc))/26)*166+19)*Rez%
  Endif
  For J=1 To I
    Circle ((127-Lon(Visit(Ftrip(J))))/63)*615+9,(((50-Lat(Visit(Ftrip(J))))/26)*166+19)*Rez%,5
    Draw  To ((127-Lon(Visit(Ftrip(J))))/63)*615+9,(((50-Lat(Visit(Ftrip(J))))/26)*166+19)*Rez%
  Next J
  If Eloc<>0 Then
    Circle ((127-Lon(Eloc))/63)*615+9,(((50-Lat(Eloc))/26)*166+19)*Rez%,5
    Draw  To ((127-Lon(Eloc))/63)*615+9,(((50-Lat(Eloc))/26)*166+19)*Rez%
  Endif
  Color 1
  '  @Findxy
  Do
    Exit If Mousek>0 Or Inkey$<>""
  Loop
  Closew 0
  @Mshrink
  Void Xbios(6,L:Varptr(Colr$(1)))
  Defline 1,1,0,0
  Sput Title$
Return
Dim Spalette%(16,3)
@Save_pal
' ------------- SAVE ORIGINAL COLOR PALETTE -----------------------
Procedure Save_pal
  For Z%=0 To 15
    Dpoke Contrl,26
    Dpoke Contrl+2,0
    Dpoke Contrl+6,2
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Vdisys
    Spalette%(Z%,0)=Dpeek(Intout+2)
    Spalette%(Z%,1)=Dpeek(Intout+4)
    Spalette%(Z%,2)=Dpeek(Intout+6)
  Next Z%
Return
Procedure Blackout
  ' ------------------ BLACK OUT -----------------------
  For Z%=0 To 15
    Dpoke Contrl,14
    Dpoke Contrl+2,0
    Dpoke Contrl+6,4
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Dpoke Intin+4,0
    Dpoke Intin+6,0
    Vdisys
  Next Z%
Return
Procedure Restorepal
  ' --------------------- RESTORES PALLET -------------------
  For Z%=0 To 15
    Dpoke Contrl,14
    Dpoke Contrl+2,0
    Dpoke Contrl+6,4
    Dpoke Intin,Z%
    Dpoke Intin+2,Spalette%(Z%,0)
    Dpoke Intin+4,Spalette%(Z%,1)
    Dpoke Intin+6,Spalette%(Z%,2)
    Vdisys
  Next Z%
Return
' ............................... MENU STRIP DATA .............................
Strip_data:
Data TRAVELER  ,  About Traveler ,------------------,-,-,-,-,-,-,""
Data DATA  ,  Create Table  ,  Edit Table  ,  Load Table  ,  Load Lat/Lon  ,  Save Table  ,""
Data OPTIONS  ,  Calculate  ,  Plan Trip  ,-------------,  EXIT  ,""
Data "",""
