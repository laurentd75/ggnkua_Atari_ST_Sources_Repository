' #############################################################################
' #############################################################################
' ###########################    ST MICROSCOPE    #############################
' ########################### BY RON SCHAEFER MD  #############################
' ###########################                     #############################
' ###########################    COPYRIGHT 1988   #############################
' ###########################   ANTIC PUBLISHING  #############################
' #############################################################################
' #############################################################################
'
If Xbios(4)<>0 Then
  Alert 3,"|Sorry the computer simulated|microscope only works in|low resolution.",1," OK ",Junk
  Goto Clean_up
Endif
' =============================================================================
Dim S$(5),S%(10),D%(10),P%(10),Magit$(8)
Dim Spalette%(16,3),Cx%(75,43)
Dim Id_data$(30,3)
Deftext 4,0,0,4
@Init_compress
Picpath$=Dir$(0)+"\"
If Exist(Picpath$+"SLIDES\MICRO.PC1")
  Picpath$=Picpath$+"SLIDES\"
Else
  If Not Exist(Picpath$+"MICRO.PC1")
    Print At(1,1);"Load MICRO.PC1 picture into memory."
    Do
      Fileselect "\MICRO.PC1","MICRO.PC1",Fn$
      @Do_sound_2(7,7)
      If Fn$<>"" Then
        Start_micro=Instr(Fn$,"MICRO.PC1")-1
        If Start_micro>0 Then
          Picpath$=Mid$(Fn$,1,Start_micro)
        Else
          Print At(1,1);"That was the wrong file--try again."
        Endif
      Endif
      Exit If Start_micro>0 Or Fn$=""
    Loop
  Endif
Endif
Skpped:
@Makesprite
Mag=0
Sx=90
Sy=60
Osx=90
Osy=60
If Not Exist(Picpath$+"MICRO.PC1") Then
  Alert 3,"|Picture file not found.",1,"Abort",Dummy
  Goto Clean_up
Endif
@Load_pc1(Picpath$+"MICRO.PC1")
Sget M$
Graphmode 3
Color 1
Sprite Frame$,260+Sx/12.5,27+Sy/24
Graphmode 2
Text 15,120,Title$
Text 269,95,"   5x"
Graphmode 1
Deffill 4,2,8
Fill 306,94
Defmouse 3
Showm
Do
  Mouse Mx,My,K
  Exit If Outnow!
  Mt=3
  If Mx>7 And My>14 And Mx<156 And My<99 Then
    Mt=5
  Endif
  If Mt<>Omt Then
    Defmouse Mt
    Omt=Mt
  Endif
  If K>0 Then
    If Mx>272 And My>175 And Mx<308 And My<185 Then
      @Grow_box(4)
      @Do_sound_1(2,3)
      Alert 2,"|Do you really|want to quit?",1," YES | NO ",Yn
      @Do_sound_2(5,4)
      Defmouse 3
      If Yn=1 Then
        Outnow!=True
      Endif
      @Shrink_box
    Endif
    If Mx>272 And My>120 And Mx<308 And My<131 Then     ! HELP
      @Do_sound_2(3,3)
      @Grow_box(1)
      Deffill 3,2,8
      Pbox 2,110,264,197
      Graphmode 2
      Text 10,117,"      COMPUTER SIMULATED MICROSCOPE       "
      Text 10,124,"            BY RON SCHAEFER M.D."
      Text 10,135,"TO MOVE SLIDE USE LARGE ARROW BUTTONS."
      Text 10,146,"THE LARGE SQUARE BUTTON CENTERS SLIDE."
      Text 10,156,"CHANGE THE MAGNIFICATION WITH THE SMALL"
      Text 10,163,"  UP AND DOWN ARROWS (MIN=25Ox MAX=8OOOx)"
      Text 10,173,"GREEN BUTTON = FUNCTIONAL, RED = INACTIVE"
      Graphmode 1
    Endif
    If Mx>272 And My>139 And Mx<308 And My<149 Then       ! INFO
      @Cleartxt
      @Do_sound_2(4,4)
      @Grow_box(2)
      Text 15,135,"PROGRAMMING DATA AND SCREEN INFORMATION"
      Text 15,145,"SX = "+Str$(Sx)+" SY = "+Str$(Sy)+" MAG="+Str$(Mag)
      Text 15,170,"PICTURE FILE NAME:"
      Text 15,178,Fn$
      Text 15,186,"CLICK-RIGHT TO RETURN TO NORMAL MODE."
      @Findxy
      Defmouse 3
      @Do_sound_2(4,4)
      @Shrink_box
    Endif
    If Point(Mx,My)=0 And Point(Mx,My+1)=0 And Point(Mx,My+2)=0 Then
      Point!=False
    Else
      Point!=True
    Endif
    If Mx>7 And My>14 And Mx<156 And My<99 And Point!<>0 Then  ! WHAT
      @Do_sound_2(6,6)
      @Grow_box(5)
      @Cleartxt
      Defmouse 5
      Graphmode 2
      If Mag=0 Then
        Text 15,170,"WHAT MODE ONLY WORKS WITH GREATER THEN"
        Text 15,177,"5 TIMES MAGNIFICATION."
        @Do_sound_2(4,4)
      Else
        Graphmode 1
        @Do_sound_2(3,3)
        Pbox 8,123,258,135
        Graphmode 2
        While Point(Mx,My)=0
          My=My+1
        Wend
        If Mag>0.5 And Point(Mx,My)<>0 Then
          Text 15,140,Id_data$(Point(Mx,My)+15,1)
          Text 15,148,Id_data$(Point(Mx,My)+15,2)
          Text 15,156,Id_data$(Point(Mx,My)+15,3)
        Else
          Text 15,140,Id_data$(Point(Mx,My),1)
          Text 15,148,Id_data$(Point(Mx,My),2)
          Text 15,156,Id_data$(Point(Mx,My),3)
        Endif
        Graphmode 1
      Endif
    Endif
    If Mx>272 And My>157 And Mx<308 And My<167 Then      ! LOAD FILE
      @Do_sound_2(6,6)
      @Grow_box(3)
      Fileselect Picpath$+"*.LST",Unk$,Fn$
      @Do_sound_2(7,7)
      If Fn$<>"" Then
        @Init_move
        Unk$=Fn$
        @Shrink_box
        @Grow_box(3)
        Title$=""
        @Cleartxt
        Graphmode 2
        Text 15,140,"PLEASE WAIT WHILE I LOAD THE SLIDE DATA"
        Text 15,148,"FILES INTO MEMORY."
        Graphmode 1
        Defmouse 2
        Sprite Frame$
        @Do_sound_2(5,6)
        Mag=0
        Mpic$=Left$(Fn$,(Len(Fn$)-4))+"_3.pi1"
        Spic$=Left$(Fn$,(Len(Fn$)-4))+"_2.pi1"
        Bpic$=Left$(Fn$,(Len(Fn$)-4))+"_1.pi1"
        Apic$=Left$(Fn$,(Len(Fn$)-4))+".pi1"
        @Mdegas(Mpic$,2)
        If Not_loaded!=False!
          @Mdegas(Spic$,1)
          If Not_loaded!=False!
            @Mdegas(Bpic$,4)
            If Not_loaded!=False!
              @Mdegas(Apic$,3)
            Endif
          Endif
        Endif
        Sput M$
        Graphmode 2
        Text 269,95,"   5x"
        If Not_loaded!=False!
          @Idata
          Void Xbios(6,L:Varptr(Colr$))
          @Bitbit
          Graphmode 3
          Color 1
          Sprite Frame$,260+Sx/12.5,27+Sy/24
          Graphmode 1
          Deffill 4,2,8
          Fill 306,94
        Endif
      Endif
      @Shrink_box
      @Cleartxt
      Defmouse 3
    Endif
    If Mag>0 And Mag<2 Then
      If Mx>195 And My>36 And Mx<217 And My<54 Then        ! UP
        @Init_move
        Dec Sy
        If Sy<0 Then
          Sy=0
          @Do_sound_1(5,5)
        Else
          @Bitblit
        Endif
      Endif
      If Mx>195 And My>83 And Mx<217 And My<99 Then         ! DOWN
        @Init_move
        Inc Sy
        If Sy>113 Then
          @Do_sound_1(5,5)
          Sy=113
        Else
          @Bitblit
        Endif
      Endif
      If Mx>168 And My>59 And Mx<190 And My<77 Then    ! LEFT
        @Init_move
        Dec Sx
        If Sx<0 Then
          Sx=0
          @Do_sound_1(5,5)
        Else
          @Bitblit
        Endif
      Endif
      If Mx>223 And My>60 And Mx<244 And My<77 Then     ! RIGHT
        @Init_move
        Inc Sx
        If Sx>168 Then
          Sx=168
          @Do_sound_1(5,5)
        Else
          @Bitblit
        Endif
      Endif
      If Mx>195 And My>59 And Mx<217 And My<76 Then  !CENTER
        @Init_move
        Sx=90
        Sy=60
        @Bitblit
        @Do_sound_2(5,6)
      Endif
    Endif
    If Mx>254 And My>89 And Mx<265 And My<98 Then   ! SHRINK
      Bx=75
      By=43
      Sw=5
      Sh=5
      Ex=7
      Ey=14
      Ew=150
      Eh=85
      @Do_sound_2(4,4)
      @Shrink_box
      @Shrink_box
      Pause 5
      If Mag=0 Then
        @Do_sound_1(6,6)
        Alert 3,"|This is the minimum|magnification now.",1," OK ",G
        @Do_sound_2(4,4)
        Defmouse 3
      Else
        If Mag=0.25 Then
          Deffill 2,2,8
          Mag=0
          @Bitbit
          Fill 206,91
          Fill 234,68
          Fill 205,43
          Fill 176,68
          Fill 206,67
          Fill 261,94
          Deffill 4,2,8
          Fill 306,94
          Graphmode 3
          Sprite Frame$,260+Sx/12.5,27+Sy/24
          Graphmode 1
          Text 269,95,"   5x"
          @Cleartxt
        Else
          ' ===========
          Graphmode 1
          If Mag=0.5 Then
            Text 269,95," 25O"
            Slide_num=4
            Mag=0.25
            @Bitblit
          Else
            ' ============
            If Mag=1 Then
              Mag=0.5
              Text 269,95," 5OOx"
              Slide_num=1
              @Bitblit
            Else
              If Mag=2 Then
                Deffill 4,2,8
                Fill 206,91
                Fill 234,68
                Fill 205,43
                Fill 176,68
                Fill 206,67
                Mag=1
                Text 269,95,Str$(Mag)
                Slide_num=2
                @Bitblit
              Else
                Mag=Mag/2
                Text 269,95,Str$(Mag)
                Deffill 4,2,8
                Fill 306,94
                Put 7,14,Magit$(Mag)
              Endif
            Endif
          Endif
        Endif
      Endif
    Endif
    If Mx>301 And My>89 And Mx<312 And My<98 Then     ! UP MAG
      Graphmode 1
      @Do_sound_2(4,4)
      @Grow_box(0)
      @Grow_box(0)
      If Mag<8 Then
        If Mag=0 Then
          Deffill 4,2,8
          Fill 206,91
          Fill 234,68
          Fill 205,43
          Fill 176,68
          Fill 206,67
          Fill 261,94
          Text 269,95," 25O"
          Slide_num=4
          @Bitblit
          Mag=0.25
        Else
          ' =============
          If Mag=0.25 Then
            Text 269,95," 5O"
            Slide_num=1
            @Bitblit
            Mag=0.5
          Else
            ' ==============
            If Mag=0.5 Then
              Text 269,95,"1O"
              Slide_num=2
              @Bitblit
              Mag=1
            Else
              Mag=Mag*2
              Deffill 2,2,8
              Fill 206,91
              Fill 234,68
              Fill 205,43
              Fill 176,68
              Fill 206,67
              If Magit$(Mag)<>"" Then
                Text 269,95,Str$(Mag)
                Put 7,14,Magit$(Mag)
              Else
                Hidem
                Text 269,95,Str$(Mag)
                @Cleartxt
                Graphmode 2
                Text 15,140,"PLEASE WAIT WHILE I INCREASE THE"
                Text 15,147,"MAGNIFICATION."
                Graphmode 1
                Mh%=1
                Repeat
                  Nh%=1
                  Repeat
                    Cx%(Mh%,Nh%)=Point(39+Mh%,34+Nh%)
                    Inc Nh%
                  Until Nh%>43
                  Inc Mh%
                Until Mh%>75
                Mg%=1
                Repeat
                  Ng%=1
                  Repeat
                    Deffill Cx%(Mg%,Ng%),2,8
                    Pbox 6+Mg%+Xf%,13+Ng%+Yf%,7+Mg%+Xf%,14+Ng%+Yf%
                    Inc Yf%
                    Inc Ng%
                  Until Ng%>43
                  Yf%=0
                  Inc Xf%
                  Inc Mg%
                Until Mg%>75
                @Cleartxt
                @Do_sound_1(3,6)
                Xf%=0
                Get 7,14,157,99,Magit$(Mag)
                Showm
              Endif
            Endif
          Endif
        Endif
      Else
        @Do_sound_1(6,6)
        Alert 3,"|Sorry this is the maximum|magnification available.",1," OK ",G
        @Do_sound_2(4,4)
        Defmouse 3
      Endif
      If Mag=8 Then
        Deffill 2,2,8
        Fill 306,94
      Endif
    Endif
  Endif
Loop
@Do_sound_3(1,15,5,5)
' ---------------------------------- CLEAN-UP END ------------------------------
Clean_up:
Cls
Defmouse 0
Setcolor 0,7,7,7
Setcolor 1,7,0,0
Setcolor 2,0,7,0
Setcolor 3,0,0,0
Setcolor 15,0,0,0
Showm
End
' ==================================
' ---------------------------------- CLEAN-UP END ------------------------------
Procedure Mdegas(Filename$,Dest)
  If Exist(Filename$) Then
    Open "I",#1,Filename$
    Temp$=Input$(36,#1)
    Colr$=Mid$(Temp$,3,36)
    Close #1
    T$=Space$(32066)
    S$(Dest)=Space$(32066)
    Bload Filename$,Varptr(T$)
    Bmove Varptr(T$),Varptr(S$(Dest))-34,32066
    Not_loaded!=False!
  Else
    Mid$(Filename$,Len(Filename$)-1,1)="C"
    If Exist(Filename$)
      Temp$=Space$(32760)
      P%=Varptr(Temp$)
      Bload Filename$,P%
      S$(Dest)=Space$(32066)
      Screen%=Varptr(S$(Dest))
      Temp2$=Space$(40)
      B%=Varptr(Temp2$)
      Picture.res=Dpeek(P%)
      P%=P%+34
      Colr$=Mid$(Temp$,3,36)
      For K%=1 To 200 !400 hi res
        Scr%=Screen%
        For C%=1 To 4 !1 to 2 medium res; 1 to 1 hi res
          P%=C:Unpac(L:P%,L:B%,40) !80 medium res; 80 hi res
          Q%=C:Unrav(L:B%,L:Scr%,40,8) !80,4 medium res; 80;2 hi res
          Scr%=Scr%+2
        Next C%
        Screen%=Screen%+160
      Next K%
      Not_loaded!=False!
    Else
      Mid$(Filename$,Len(Filename$)-1,1)="I"
      Alert 1,"Picture files|must be in the|same directory as MICRO.PC1.",1," Too Bad ",G
      Not_loaded!=True!
    Endif
  Endif
Return
' ========================
Procedure Findxy
  Defmouse 7
  Do
    Mouse Mx,My,K
    If K=1 Then
      Text 15,152,"THE MOUSE IS AT "+"mx="+Str$(Mx)+" my="+Str$(My)+" "
      Text 15,159,"THE COLOR INDEX = "+Str$(Point(Mx,My))+" "
    Endif
    Exit If K=2
  Loop
  @Cleartxt
Return
' ------------- SAVE ORIGINAL COLOR PALETTE -----------------------
Procedure Save_pal
  For Z%=0 To 15
    Dpoke Contrl,26
    Dpoke Contrl+2,0
    Dpoke Contrl+6,2
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Vdisys
    Spalette%(Z%,0)=Dpeek(Intout+2)
    Spalette%(Z%,1)=Dpeek(Intout+4)
    Spalette%(Z%,2)=Dpeek(Intout+6)
  Next Z%
Return
' ===================================
' --------------------------- BITBLIT  ---------------------
Procedure Bitblit
  Sprite Frame$,260+Sx/12.5,27+Sy/24
  S%(0)=Varptr(S$(Slide_num))
  S%(1)=319
  S%(2)=199
  S%(3)=Int((S%(1)+15)/16)
  S%(4)=0
  S%(5)=4
  D%(0)=Xbios(2)
  D%(1)=319
  D%(2)=199
  D%(3)=Int((D%(1)+15)/16)
  D%(4)=0
  D%(5)=4
  ' -------------------------------
  Dx=7
  Dy=14
  P%(4)=Dx       ! dest Xo
  P%(5)=Dy         ! dest Yo
  P%(6)=Dx+149       ! dest Xi
  P%(7)=Dy+85       ! dest Yi
  P%(8)=3
  ' -------------------------------
  P%(0)=Sx          ! origin Xo
  P%(1)=Sy          ! origin Yo
  P%(2)=Sx+149     ! origin Xi
  P%(3)=Sy+85       ! origin Yi
  Vsync
  Bitblt S%(),D%(),P%()
Return
' ==============================================
Procedure Bitbit
  @Updatepos
  S%(0)=Varptr(S$(3))
  S%(1)=319
  S%(2)=199
  S%(3)=Int((S%(1)+15)/16)
  S%(4)=0
  S%(5)=4
  D%(0)=Xbios(2)
  D%(1)=319
  D%(2)=199
  D%(3)=Int((D%(1)+15)/16)
  D%(4)=0
  D%(5)=4
  ' ----------
  Dx=7
  Dy=14
  P%(4)=Dx       ! dest Xo
  P%(5)=Dy         ! dest Yo
  P%(6)=Dx+149       ! dest Xi
  P%(7)=Dy+85       ! dest Yi
  P%(8)=3
  ' -------------------------------
  P%(0)=0          ! origin Xo
  P%(1)=0          ! origin Yo
  P%(2)=149     ! origin Xi
  P%(3)=85       ! origin Yi
  Vsync
  Bitblt S%(),D%(),P%()
  ' -------------------------------
  Dx=261
  Dy=29
  P%(4)=Dx       ! dest Xo
  P%(5)=Dy         ! dest Yo
  P%(6)=Dx+26       ! dest Xi
  P%(7)=Dy+14       ! dest Yi
  P%(8)=3
  ' -------------------------------
  P%(0)=0          ! origin Xo
  P%(1)=89          ! origin Yo
  P%(2)=26     ! origin Xi
  P%(3)=103      ! origin Yi
  Vsync
  Bitblt S%(),D%(),P%()
Return
' ======================================================================
Procedure Do_sound_1(Snd,Snd1)
  Sound 1,12,Snd,Snd1
  Wave 1,1,9,6000
Return
Procedure Do_sound_2(Snd,Snd1)
  Sound 1,12,Snd,Snd1
  Wave 1,1,8,512,5
  Wave 0,0
Return
Procedure Do_sound_3(Snd,Snd1,Per,Dur)
  Sound 1,2,Snd,Snd1
  Wave 1,1,9,Per,Dur
Return
Procedure Updatepos
  Osx=Sx
  Osy=Sy
Return
Procedure Pixel(Px,Py)
  Dpoke Contrl,105
  Dpoke Contrl+2,1
  Dpoke Contrl+6,0
  Dpoke Ptsin,Px
  Dpoke Ptsin+2,Py
  Vdisys
  Pixval=Dpeek(Intout)
  Cind=Dpeek(Intout+2)
  Vdisys
Return
Procedure Cleartxt
  Deffill 3,2,8
  Pbox 2,110,264,197
  Graphmode 2
  Text 15,120,Title$
  Graphmode 1
Return
' ---------------------------------- GROW BOX ----------------------------------
Procedure Grow_box(Type)
  If Type=0 Then
    Bx=75
    By=43
    Sw=5
    Sh=5
    Ex=7
    Ey=14
    Ew=150
    Eh=85
  Else
    Bx=272
    By=120
    Sw=36
    Sh=11
    Ex=2
    Ey=110
    Ew=262
    Eh=87
  Endif
  If Type=2 Then
    By=139
  Endif
  If Type=3 Then
    By=157
    Ex=0
    Ey=0
    Ew=319
    Eh=198
  Endif
  If Type=4 Then
    Ex=0
    Ey=0
    Ew=319
    Eh=198
    By=175
  Endif
  If Type=5 Then
    Bx=Mx-5
    By=My-5
    Sw=10
    Sh=10
  Endif
  Dpoke Gintin,Bx              ! X Coordinate of Box in it's Initial Size
  Dpoke Gintin+2,By            ! Y Coordinate of Box in it's Initial Size
  Dpoke Gintin+4,Sw            ! Initial WIDTH of Box
  Dpoke Gintin+6,Sh            ! Initial HEIGHT of Box
  Dpoke Gintin+8,Ex            ! X Coordinate of Box in it's Final Size
  Dpoke Gintin+10,Ey           ! Y Coordinate of Box in it's Final Size
  Dpoke Gintin+12,Ew           ! WIDTH of Box in it's Final Size
  Dpoke Gintin+14,Eh           ! HEIGHT of Box in it's Final Size
  Gemsys (73)                  ! A.E.S. Graphics Library Op Code
Return
' -------------------------------- SHRINK BOX ----------------------------------
Procedure Shrink_box
  Dpoke Gintin,Bx              ! X Coordinate of Box in it's Final Size
  Dpoke Gintin+2,By       ! Y Coordinate of Box in it's Final Size
  Dpoke Gintin+4,Sw            ! WIDTH of Box in it's Final Size
  Dpoke Gintin+6,Sh            ! Height of Box in it's Final Size
  Dpoke Gintin+8,Ex            ! X Coordinate of Box in it's Initial Size
  Dpoke Gintin+10,Ey      ! Y Coordinate of Box in it's Initial Size
  Dpoke Gintin+12,Ew           ! Initial WIDTH of Box
  Dpoke Gintin+14,Eh           ! Initial HEIGHT of Box
  Gemsys (74)                  ! A.E.S. Graphics Library Op Code
Return
' ---------------------------------- DRAG BOX ----------------------------------
Procedure Drag_box
  Dpoke Gintin,Dw              ! WIDTH of Box being Dragged
  Dpoke Gintin+2,Dh            ! HEIGHT of Box being Dragged
  Dpoke Gintin+4,Sx            ! Starting X Coordinate of Box
  Dpoke Gintin+6,Sy+Woff       ! Starting Y Coordinate of Box
  Dpoke Gintin+8,Bdx           ! X Coordinate of Boundary Rectangle
  Dpoke Gintin+10,Bdy+Woff     ! Y Coordinate of Boundary Rectangle
  Dpoke Gintin+12,Bdw          ! WIDTH of Boundary Rectangle
  Dpoke Gintin+14,Bdh          ! Height of Boundary Rectangle
  Gemsys (71)                  ! A.E.S. Graphics Library Op Code
  Ex=Dpeek(Gintout+2)          ! X Coordinate when Mouse Button was Released
  Ey=Dpeek(Gintout+4)-Woff     ! Y Coordinate when Mouse Button was Released
Return
' --------------------------------- MOVE BOX -----------------------------------
Procedure Move_box
  Dpoke Gintin,Bw              ! WIDTH of Box being Moved
  Dpoke Gintin+2,Bh            ! HEIGHT of Box Being Moved
  Dpoke Gintin+4,Sx            ! X Coordinate of Box in it's Initial Position
  Dpoke Gintin+6,Sy+Woff       ! Y Coordinate of Box in it's Initial Position
  Dpoke Gintin+8,Ex            ! X Coordinate of Box in it's Final Position
  Dpoke Gintin+10,Ey+Woff      ! Y Coordinate of Box in it's Final Position
  Gemsys (72)                  ! A.E.S. Graphics Library Op Code
Return
Procedure Idata
  Id_path$=Fn$
  If Exist(Id_path$) Then
    Open "I",#1,Id_path$
    Input #1,Junk$,Title$
    For J%=1 To 30
      For N%=1 To 3
        Input #1,Junk$,Junk,Id_data$(J%,N%)
        If Id_data$(J%,N%)="." Then
          Id_data$(J%,N%)=" "
        Endif
      Next N%
    Next J%
    Close #1
  Else
    Print "File not found."
  Endif
Return
Procedure Init_move
  For N%=1 To 8
    Magit$(N%)=""
  Next N%
Return
Procedure Makesprite
  '
  ' Sprite-Convert data in string
  Let Frame$=Mki$(0)+Mki$(0)
  Let Frame$=Frame$+Mki$(1)
  Let Frame$=Frame$+Mki$(15)
  Let Frame$=Frame$+Mki$(0)
  Restore Sprite_data
  For I%=1 To 16
    Read Foregrnd,Backgrnd
    Let Frame$=Frame$+Mki$(Backgrnd)+Mki$(Foregrnd)
  Next I%
  Sprite_data:
  Data 0,0,65535,0,32769,32766,49149,16386,40965,16386,40965,16386,40965,16386,40965,16386
  Data 40965,16386,40965,16386,40965,16386,49149,16386,32769,32766,65535,0,0,0,0,0
Return
Procedure Load_pc1(Degas.file$)
  '
  ' Thanks to Jim Kent for this routine to read a compressed picture
  '
  If Degas.file$<>""
    If Exist(Degas.file$)
      Temp$=Space$(32760)
      P%=Varptr(Temp$)
      Bload Degas.file$,P%
      Screen%=Xbios(2)
      Temp2$=Space$(40)
      B%=Varptr(Temp2$)
      Picture.res=Dpeek(P%)
      P%=P%+2
      For I%=0 To 15
        Setcolor I%,Dpeek(P%)
        P%=P%+2
      Next I%
      '
      For K%=1 To 200 !400 hi res
        Scr%=Screen%
        For C%=1 To 4 !1 to 2 medium res; 1 to 1 hi res
          P%=C:Unpac(L:P%,L:B%,40) !80 medium res; 80 hi res
          Q%=C:Unrav(L:B%,L:Scr%,40,8) !80,4 medium res; 80;2 hi res
          Scr%=Scr%+2
        Next C%
        Screen%=Screen%+160
      Next K%
    Else
      Alert 1,"Can't find file.",1,"Cancel",Button
    Endif
  Endif
Return
'
Procedure Init_compress
  '
  ' Stash into memory object code routines to uncompress picture
  '
  Unpack$=Space$(15*4)
  Unpac=Varptr(Unpack$)
  Restore Unpack
  For I=1 To 15
    Read X
    Lpoke Unpac,X
    Unpac=Unpac+4
  Next I
  Unpac=Varptr(Unpack$)
  Unrave$=Space$(10*4)
  Unrav=Varptr(Unrave$)
  Restore Unravel
  For I=1 To 10
    Read X
    Lpoke Unrav,X
    Unrav=Unrav+4
  Next I
  Unrav=Varptr(Unrave$)
  Unpack:
  Data &h48e740e0,&h206f0014,&h226f0018,&h2449d4ef
  Data &h001c4241,&h12186b0c,&h12d851c9,&hfffcb3ca
  Data &h6df0600e,&h44011018,&h12c051c9,&hfffcb3ca
  Data &h6de02008,&h4cdf0702,&h4e750000
  Unravel:
  Data &h48e740c0,&h206f0010,&h226f0014,&h302f0018
  Data &h322f001a,&he2486004,&h3298d2c1,&h51c8fffa
  Data &h4cdf0302,&h4e750000
Return
