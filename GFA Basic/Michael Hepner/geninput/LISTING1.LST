' =============================================================================
'         LISTING 1          GENERALIZED INPUT ROUTINES            03/25/1988
'                                 BY MICHAEL HEPNER
'                         COPYRIGHT 1988 ANTIC PUBLISHING INC.
' =============================================================================
'
Fullw 1
Titlew 1," Generalized Input Routines "
Cls
'
' -----------------------------------------------------------------------------
'       CHECK SCREEN RESOLUTION:  Must run in MEDIUM or HIGH resolution.
' -----------------------------------------------------------------------------
Rez%=Xbios(4)
If Rez%=0 Then
  Alert 3," Please switch to | | MEDIUM RESOLUTION ",1," OK ",B
  Quit
Endif
'
' -----------------------------------------------------------------------------
'     INITIALIZE: Call the Generalized Initialization Routines
' -----------------------------------------------------------------------------
Gosub Menu_setup
Gosub Fld_dimen
'
' -----------------------------------------------------------------------------
'                      SHOW LIST OF OPTIONS AVAILABLE:
'              When selected option is done, show the list again.
' -----------------------------------------------------------------------------
Do
  Gosub Show_main_option_list
Loop
'
' ----------------------------  END OF MAIN PROGRAM  --------------------------
'
'
' =============================================================================
' SHOW_MAIN_OPTION_LIST:  This procedure shows how to initialize the DATA
'        values and call the Generalized Option List Procedure.
' -----------------------------------------------------------------------------
Procedure Show_main_option_list
  Titlew 1," Generalized Input Routines "
  '
  Restore Main_list_menu_data                   ! Build the customized
  Gosub Build_menu_bar                          ! drop down menus.
  '
  Repeat
    Restore Main_option_list_data               ! Build the customized
    Gosub Show_option_list                      ! option list.
    '
    Gosub Check_which_option                    ! Wait for MOUSE or Func-key.
  Until Function%>0
  '
  On Function% Gosub Option1,Option2,Option3    !  ... OptionN
Return
'
'
' =============================================================================
' ==========              OPTION 1  -  __________________            ==========
' =============================================================================
Procedure Option1
  Titlew 1," Option One "
  Cls
  '
  Restore Opt1_menu_data                        ! Different
  Gosub Build_menu_bar                          ! drop down menus.
  '
  Restore Opt1_fld_data                         ! Set up the
  Gosub Fld_setup                               ! data entry screen.
  '
  Opt_done%=0
  While Opt_done%=0
    Gosub Process_option1
  Wend
Return
'
' =============================================================================
' PROCESS_OPTION1:  ______________
' -----------------------------------------------------------------------------
Procedure Process_option1
  For I%=1 To Num_flds%
    Fld_val$(I%)=Space$(Fld_leng%(I%))
  Next I%
  Redraw%=1
  '
  Rec_done%=0
  Repeat
    If Redraw%=1 Then                           ! First time or after desk
      Gosub Show_headings                       ! accessory, display screen.
    Endif
    '
    Gosub Ask_for_opt1_field
    Fld_num%=Nxt_fld%
  Until Rec_done%>0
Return
'
' =============================================================================
' ASK_FOR_OPT1_FIELD:  Print the field prompt, and input the field.
'                _________________________________________________
'               |   One common procedure is best when there are   |
'               |   no further edits needed on the input fields.  |
'                -------------------------------------------------
' -----------------------------------------------------------------------------
Procedure Ask_for_opt1_field
  Temp$=Fld_val$(Fld_num%)
  Gosub Check_field_input
  Fld_val$(Fld_num%)=Temp$
  '
  Gosub Clear_box
Return
'
'
' =============================================================================
' ==========         OPTION 2  -  ________________                   ==========
' =============================================================================
Procedure Option2
  Titlew 1," Option Two "
  Cls
  '
  Restore Opt2_menu_data                        ! Different
  Gosub Build_menu_bar                          ! drop down menus.
  '
  Restore Opt2_fld_data                         ! Different
  Gosub Fld_setup                               ! field definitions.
  '
  Opt_done%=0
  Repeat
    Gosub Process_option2
  Until Opt_done%>0
Return
'
' =============================================================================
' PROCESS_OPTION2:  ______________
' -----------------------------------------------------------------------------
Procedure Process_option2
  For I%=1 To Num_flds%
    Fld_val$(I%)=Space$(Fld_leng%(I%))
  Next I%
  Redraw%=1
  '
  Rec_done%=0
  Repeat
    If Redraw%=1 Then                           ! First time or after desk
      Gosub Show_headings                       ! accessory, display screen.
    Endif
    '
    On Fld_num% Gosub Ask_field1,Ask_field2,Ask_field3
    Fld_num%=Nxt_fld%
    '
    ' ------------ Edit all the fields when Rec_done%=1
  Until Rec_done%>0
Return
'
' =============================================================================
' ASK_FIELD1:  Ask for the first field.
' -----------------------------------------------------------------------------
Procedure Ask_field1
  Temp$=Fld_val$(Fld_num%)
  Gosub Check_field_input
  Fld_val$(Fld_num%)=Temp$
  '
  Gosub Clear_box
  '
  '                    Additional edits can go here.
Return
'
' =============================================================================
' ASK_FIELD2:  Ask for the second field.
' -----------------------------------------------------------------------------
Procedure Ask_field2
  Temp$=Fld_val$(Fld_num%)
  Gosub Check_field_input
  Fld_val$(Fld_num%)=Temp$
  '
  Gosub Clear_box
  '
  '                    Additional edits can go here.
Return
'
' =============================================================================
' ASK_FIELD3:  Ask for the third field.
' -----------------------------------------------------------------------------
Procedure Ask_field3
  Temp$=Fld_val$(Fld_num%)
  Gosub Check_field_input
  Fld_val$(Fld_num%)=Temp$
  '
  Gosub Clear_box
  '
  '                    Additional edits can go here.
Return
'
'
' =============================================================================
' ==========                   OPTION 3  -  EXIT                     ==========
' =============================================================================
Procedure Option3
  Alert 2,"    Do you really    | |    want to QUIT?    ",1,"YES|NO ",B
  If B=1 Then
    @Restorepal
    Edit
  Endif
Return
'
'
' =============================================================================
'               GENERALIZED INPUT ROUTINES TO PROCESS THE MENU BAR
' =============================================================================
'
' -----------------------------------------------------------------------------
' MENU_SETUP:  Dimension the Menu Bar Array, initialize variables,
'              and set the screen colors and text size.
' -----------------------------------------------------------------------------
Procedure Menu_setup
  Max_menu%=150                           ! Make this as big as you need.
  Dim Menu_bar$(Max_menu%)
  Dim Spalette%(16,3)
  '
  Insert_mode%=0
  First_redraw%=0
  Redraw%=0
  '
  @Save_pal
  If Rez%=1 Then
    Setcolor 0,7,7,7
    Setcolor 1,7,0,0
    Setcolor 2,0,0,4
    Setcolor 3,0,0,0
    Txt_size%=6
  Else
    Setcolor 0,7,7,7
    Setcolor 1,0,0,0
    Txt_size%=13
  Endif
Return
'
'
' ==============================================================================
' BUILD_MENU_BAR:  Builds the drop down menus and activates them.
' -----------------------------------------------------------------------------
Procedure Build_menu_bar
  For I%=0 To Max_menu%
    Read Menu_bar$(I%)
    Exit If Menu_bar$(I%)="***"
  Next I%
  '
  Menu_bar$(I%)=""
  Menu Menu_bar$()
  On Menu  Gosub Menu_handler
  On Menu Message Gosub Menu_message
Return
'
' -----------------------------------------------------------------------------
' DATA for MENU BAR:  First line is needed to activate the desk accessories.
'        On other lines, the first value will appear on the menu bar and the
'        following values will appear on the drop down menu.
' -----------------------------------------------------------------------------
Main_list_menu_data:
Data DESK, About Gen. Input ,------------------,1,2,3,4,5,6,""
Data QUIT, End Program,""
Data ***
'
Opt1_menu_data:
Data DESK, About Gen. Input ,------------------,1,2,3,4,5,6,""
Data QUIT, End Program,""
Data DONE, Return to Menu,""
Data OTHER, Put Optional, Features in the, Drop Down Menu,""
Data ***
'
Opt2_menu_data:
Data DESK, About Gen. Input ,------------------,1,2,3,4,5,6,""
Data QUIT, End Program,""
Data DONE, Return to Menu,""
Data OTHER, More Optional, Features, Can Go Here,""
Data ***
'
'
' =============================================================================
' MENU_HANDLER:  Determines which drop down menu option was selected.
' -----------------------------------------------------------------------------
Procedure Menu_handler
  Menu Off
  Menu_option$=Menu_bar$(Menu(0))
  '
  If Menu_option$=" Return to Menu" Then
    Fld_done%=99
    Rec_done%=99
    Opt_done%=99
  Endif
  '
  If Menu_option$=" End Program" Then
    Gosub Option3
  Endif
  '
  If Menu_option$=" About Gen. Input " Then
    A$="Generalized Input Routines|"
    A1$="    Sample--Listing 1|"
    A2$="    by Michael Hepner|"
    A3$=" "+Chr$(189)+" 1988 Antic Publishing"
    Alert 1,A$+A1$+A2$+A3$,1,"OK",A
  Endif
  '
  '                ____________________________________________
  '               |  Code an IF statement for each selection   |
  '               |  defined on the drop down menus.           |
  '                --------------------------------------------
Return
'
'
' =============================================================================
' MENU_MESSAGE:  Determine if the screen needs to be redrawn.
'        (Ignore first call which comes at the start of the program.)
' -----------------------------------------------------------------------------
Procedure Menu_message
  If Menu(1)=20 Then
    If First_redraw%=0 Then
      First_redraw%=1
    Else
      Redraw%=1
    Endif
    '
    If Rez%=1 Then
      Setcolor 0,7,7,7
      Setcolor 1,7,0,0
      Setcolor 2,0,0,4
      Setcolor 3,0,0,0
    Else
      Setcolor 0,7,7,7
      Setcolor 1,0,0,0
    Endif
  Endif
Return
'
'
' =============================================================================
'              GENERALIZED ROUTINES TO PROCESS THE OPTION LIST
' =============================================================================
'
' -----------------------------------------------------------------------------
' SHOW_OPTION_LIST:  Using DATA statements, build the list of options.
'        Before calling this procedure, use a RESTORE command to point to
'        the DATA statements for the option list.
' -----------------------------------------------------------------------------
Procedure Show_option_list
  Cls
  Color 2
  Defline 1,1,0,0
  '
  Read Offset%,Spacing%
  Offset%=Offset%*Rez%
  Spacing%=Spacing%*Rez%
  '
  Read Num_select%
  For I%=1 To Num_select%
    Read Select$
    Y%=Spacing%*(I%-1)+Offset%
    Deftext 2,0,0,Txt_size%
    Text 192,Y%-Rez%,"F"
    Text 200,Y%-Rez%,I%
    Rbox 180,Y%-9*Rez%,221,Y%+Rez%
    Deftext 1,0,0,Txt_size%
    Text 248,Y%-Rez%,Select$
  Next I%
  '
  Gosub Build_box
  Text 160,152*Rez%,"Press function key of desired option,"
  Text 304,160*Rez%,"or"
  Text 160,168*Rez%,"click the MOUSE on the desired option."
Return
'
' -----------------------------------------------------------------------------
' DATA for OPTION LIST:  Options are listed using the TEXT command.
'        First data value gives the Y coordinate for the first option.
'        Second value gives the text spacing between options.
'        Third value is the number of options followed by their text values.
'        For even spacing, use one of the following sets:
'            56, 28,  2      (Minimum number of options)
'            48, 22,  3
'            34, 24,  4
'            28, 22,  5
'            22, 20,  6
'            18, 18,  7
'            16, 16,  8
'            16, 14,  9
'            14, 13, 10      (Maximum number of options)
'        You may also adjust the spacing if you wish to print extra lines
'        on the Option List Screen.
' -----------------------------------------------------------------------------
Main_option_list_data:
Data 48,22
Data 3
Data Option One
Data Option Two
Data Quit
'
'
' =============================================================================
' CHECK_WHICH_OPTION:  Processes user inputs from the Option Screen.
' -----------------------------------------------------------------------------
Procedure Check_which_option
  On Menu Key Gosub Check_function_key
  On Menu Button 1,1,1 Gosub Compute_mouse_option
  '
  Function%=0
  Redraw%=0
  Repeat
    On Menu
  Until (Function%>0 And Function%<=Num_select%) Or Redraw%=1
Return
'
'
' =============================================================================
' CHECK_FUNCTION_KEY:  Only responds if function key matches an option.
' -----------------------------------------------------------------------------
Procedure Check_function_key
  If (Menu(14) And 255)=0 Then
    Key%=Menu(14)/256
    If Key%>58 And Key%<=58+Num_select% Then
      Function%=Key%-58
    Endif
  Endif
Return
'
'
' =============================================================================
' COMPUTE_MOUSE_OPTION:  Only responds if MOUSE was clicked on an option.
' -----------------------------------------------------------------------------
Procedure Compute_mouse_option
  Y%=Menu(11)-22*Rez%
  Y1%=Y%-Offset%+9*Rez%
  Y2%=Int(Y1%/Spacing%)
  Y3%=Y1%-Y2%*Spacing%
  If Y3%>=0 And Y3%<=10*Rez% Then
    Function%=Y2%+1
  Endif
Return
'
'
' =============================================================================
'     GENERALIZED ROUTINES FOR DEFINING THE INPUT FIELDS AND SCREEN LAYOUT
' =============================================================================
'
' -----------------------------------------------------------------------------
' FLD_DIMEN:  Dimension the Field Arrays (large enough for the largest set).
' -----------------------------------------------------------------------------
Procedure Fld_dimen
  Max_flds%=3                            ! Make this as large as you need.
  Dim Fld_hstart%(Max_flds%),Fld_yline%(Max_flds%),Fld_xstart%(Max_flds%)
  Dim Fld_leng%(Max_flds%),Fld_type$(Max_flds%),Fld_heading$(Max_flds%)
  Dim Fld_prompt$(Max_flds%),Fld_help$(Max_flds%)
  Dim Fld_val$(Max_flds%)
Return
'
'
' =============================================================================
' FLD_SETUP:  For each different screen, read the DATA statements that define
'        each field on the screen, and build the screen definition arrays.
' -----------------------------------------------------------------------------
Procedure Fld_setup
  Read Num_flds%
  For I%=1 To Num_flds%
    Read Fld_hstart%(I%),Fld_yline%(I%),Fld_xstart%(I%)
    Read Fld_leng%(I%),Fld_type$(I%),Fld_heading$(I%)
    Read Fld_prompt$(I%),Fld_help$(I%)
  Next I%
Return
'
' -----------------------------------------------------------------------------
' DATA for SCREEN SETUP:  First data value tells how many sets of data follow.
'        Each set contains four numeric values and four text values:
'           X coordinate of header, Y coordinate, X coordinate of field,
'           length, type, heading, prompt, and help message.
' -----------------------------------------------------------------------------
Opt1_fld_data:
Data 2
'
Data 120,40,184,25,A-Z,Field1
Data "Enter value for Field #1."
Data "Field #1 must be alphabetic.  Space, comma, or dash is also allowed."
'
Data 160,60,224,20,ANY,Field2
Data "Enter value for Field #2."
Data "Field #2 may contain any characters."
'
Opt2_fld_data:
Data 3
'
Data 184,30,248,5,NUM,Number
Data "Enter a number."
Data "Only numeric characters are valid."
'
Data 184,50,256,12,DEC,Decimal
Data "Enter a decimal value."
Data "Only numbers and a decimal point are allowed."
'
Data 240,90,328,1,Y/N,Yes-or-No
Data "Answer question (Y/N)."
Data "Enter Y for Yes   or   N for No."
'
'                ___________________________________________
'               |   You can define a field as any "type".   |
'               |   You code how each "type" is edited in   |
'               |   the HAVE_DATA Procedure.                |
'                -------------------------------------------
'
'
' =============================================================================
' SHOW_HEADINGS:  Using the screen definition arrays, build the screen and
'        build the instruction box at the bottom of the screen.
' -----------------------------------------------------------------------------
Procedure Show_headings
  Cls
  Defline 1,1,0,0
  Color 2
  For I%=1 To Num_flds%
    Y%=Fld_yline%(I%)*Rez%
    Text Fld_hstart%(I%),Y%,Fld_heading$(I%)
    Text Fld_hstart%(I%)+8*Len(Fld_heading$(I%)),Y%,":"
    X%=Fld_xstart%(I%)
    Text X%,Y%,Fld_val$(I%)
    Line X%,Y%+2,X%-1+8*Fld_leng%(I%),Y%+2
  Next I%
  '
  Gosub Build_box
  Box 4,125*Rez%,634,139*Rez%
  Box 9,127*Rez%,629,137*Rez%
  Deffill 2,1
  Fill 7,126*Rez%
  '
  Deftext 3,0,0,Txt_size%
  Text 24,135*Rez%,"Press F10 when all fields are correct."
  '
  If Insert_mode%=0 Then
    Deftext 2,0,0,Txt_size%
    Text 488,135*Rez%,"Insert mode: Off"
  Else
    Deftext 3,0,0,Txt_size%
    Text 488,135*Rez%,"Insert mode: ON "
  Endif
  Deftext 1,0,0,Txt_size%
  '
  Fld_num%=1
  Xsub%=1
Return
'
'
' =============================================================================
' BUILD_BOX:  Draws a box with thick border.
' -----------------------------------------------------------------------------
Procedure Build_box
  Color 2
  Defline 1,1,0,0
  Box 4,137*Rez%,634,176*Rez%
  Box 9,139*Rez%,629,174*Rez%
  Deffill 2,1
  Fill 7,138*Rez%
Return
'
'
' =============================================================================
' CLEAR_BOX:  Erases the inside of the box.
' -----------------------------------------------------------------------------
Procedure Clear_box
  Deffill 0,1
  Pbox 24,144*Rez%,604,168*Rez%
Return
'
'
' =============================================================================
'                        ROUTINES TO READ AN ENTIRE FIELD
' =============================================================================
'
' -----------------------------------------------------------------------------
' CHECK_FIELD_INPUT:  Processes user inputs from a data entry screen.
' -----------------------------------------------------------------------------
Procedure Check_field_input
  Hold$=Temp$
  '
  X%=Int((80-Len(Fld_prompt$(Fld_num%)))/2)
  Print At(X%,20);Fld_prompt$(Fld_num%)
  '
  Xstart%=Fld_xstart%(Fld_num%)
  Yline%=Fld_yline%(Fld_num%)*Rez%
  Fleng%=Fld_leng%(Fld_num%)
  Type_input$=Fld_type$(Fld_num%)
  '
  Gosub Cursor
  '
  On Menu Key Gosub Check_field_key
  On Menu Button 1,1,1 Gosub Compute_mouse_field
  '
  Fld_done%=0
  Redraw%=0
  Repeat
    On Menu
  Until Fld_done%>0 Or Redraw%=1
Return
'
'
' =============================================================================
' CHECK_FIELD_KEY:  Processes keyboard inputs from a data entry screen.
' -----------------------------------------------------------------------------
Procedure Check_field_key
  Menu Off
  If Menu(13)>=4 Then                    ! Skip Control & Alternate characters
    Gosub Beep
  Else
    If (Menu(14) And 255)=0 Then
      Gosub Check_special_key
    Else
      Gosub Check_regular_key
    Endif
  Endif
Return
'
'
' =============================================================================
' CHECK_REGULAR_KEY:  Processes standard keys.
' -----------------------------------------------------------------------------
Procedure Check_regular_key
  If Menu(14)=7181 Then                  ! Return
    Gosub Finish_field
  Else
    If Menu(14)=3849                     ! Tab
      Gosub Finish_field
    Else
      If Menu(14)=29197 Then             ! Enter
        Gosub Finish_field
      Else
        If Menu(14)=3592 Then            ! Backspace
          Gosub Have_backspace
        Else
          If Menu(14)=21375 Then         ! Delete
            Gosub Have_delete
          Else
            If Menu(14)=283 Then         ! Escape
              Gosub Clear_field
            Else
              Gosub Have_data            ! add character to field
            Endif
          Endif
        Endif
      Endif
    Endif
  Endif
Return
'
'
' =============================================================================
' CHECK_SPECIAL_KEY:  Processes function keys and other special keys.
'     (Only function key F10 is used by the sample data entry screen.)
'             ( Fkey=59 for F1   ---->   Fkey=67 for F9 )
' -----------------------------------------------------------------------------
Procedure Check_special_key
  Fkey=Menu(14)/256
  If Fkey=72 Then                        ! Up Arrow
    Nxt_fld%=Fld_num%-1
    Nxt_xsub%=1
    Gosub Next_field
  Else
    If Fkey=80 Then                      ! Down Arrow
      Nxt_fld%=Fld_num%+1
      Nxt_xsub%=1
      Gosub Next_field
    Else
      If Fkey=75 Then                    ! Left Arrow
        Gosub Have_left_arrow
      Else
        If Fkey=77 Then                  ! Right Arrow
          Gosub Have_right_arrow
        Else
          If Fkey=82 Then                ! Insert
            Gosub Have_insert
          Else
            If Fkey=71 Then              ! Clr Home
              Gosub Clear_field
            Else
              If Fkey=97 Then            ! Undo
                Gosub Have_undo_key
              Else
                If Fkey=98 Then          ! Help
                  Gosub Have_help_key
                Else
                  If Fkey=68 Then        ! F10
                    Gosub Record_is_done
                  Endif
                Endif
              Endif
            Endif
          Endif
        Endif
      Endif
    Endif
  Endif
Return
'
'
' =============================================================================
' COMPUTE_MOUSE_FIELD:  Computes which field the MOUSE was clicked on.
' -----------------------------------------------------------------------------
Procedure Compute_mouse_field
  X%=Menu(10)
  Y%=Menu(11)-22*Rez%
  '
  Fld%=0
  For I%=1 To Num_flds%
    If X%>=Fld_hstart%(I%) And X%<Fld_xstart%(I%)+8*Fld_leng%(I%) Then
      If Y%>=Fld_yline%(I%)-7*Rez% And Y%<Fld_yline%(I%)*Rez% Then
        Fld%=I%
      Endif
    Endif
    Exit If Fld%>0
  Next I%
  '
  If X%<=Fld_xstart%(Fld%) Then
    Nxt_xsub%=1
  Else
    Nxt_xsub%=Int((X%-Fld_xstart%(Fld%))/8)+1
  Endif
  '
  If Fld%>0 Then
    If Fld%=Fld_num% Then
      Gosub Cursor
      Xsub%=Nxt_xsub%
      Gosub Cursor
    Else
      Nxt_fld%=Fld%
      Gosub Next_field
    Endif
  Endif
Return
'
'
' =============================================================================
' CURSOR:  Draws or erases the cursor block.
' -----------------------------------------------------------------------------
Procedure Cursor
  Graphmode 3
  Deffill 1,1
  Xchar%=Xstart%+(Xsub%-1)*8
  Pbox Xchar%-1,Yline%+2*Rez%,Xchar%+8,Yline%-8*Rez%
  Graphmode 1
Return
'
'
' =============================================================================
' HAVE_DATA:  Check if key is valid for this field type.
' -----------------------------------------------------------------------------
Procedure Have_data
  C$=Chr$(Menu(14))
  If Type_input$="ANY" Then
    Gosub Keep_data
  Else
    If Type_input$="A-Z" Then
      If Instr(" .,-ABCDEFGHIJKLMNOPQRSTUVWXYZ",Upper$(C$)) Then
        Gosub Keep_data
      Else
        Gosub Beep
      Endif
    Else
      If Type_input$="Y/N" Then
        C$=Upper$(C$)
        If Instr("YN",C$) Then
          Gosub Keep_data
        Else
          Gosub Beep
        Endif
      Else
        If Type_input$="NUM" Then
          If Instr("0123456789",C$) Then
            Gosub Keep_data
          Else
            Gosub Beep
          Endif
        Else
          If Type_input$="DEC" Then
            If Instr("-.0123456789",C$) Then
              Gosub Keep_data
            Else
              Gosub Beep
            Endif
          Endif
        Endif
      Endif
    Endif
  Endif
Return
'
'
' =============================================================================
' KEEP_DATA:  Key is valid, so add it to the field.
' -----------------------------------------------------------------------------
Procedure Keep_data
  Gosub Cursor
  '
  If Insert_mode%=1 Then
    L%=Fleng%-Xsub%
    If L%>0 Then
      Mid$(Temp$,Xsub%+1,L%)=Mid$(Temp$,Xsub%,L%)
      Mid$(Temp$,Xsub%,1)=" "
      Text Xstart%,Yline%,Temp$
    Endif
  Endif
  '
  Text Xchar%,Yline%,C$
  Mid$(Temp$,Xsub%,1)=C$
  If Xsub%<Fleng% Then
    Inc Xsub%
    Add Xchar%,8
  Endif
  Gosub Cursor
Return
'
'
' =============================================================================
' BEEP:  Key is not valid, so make a beeping noise.
' -----------------------------------------------------------------------------
Procedure Beep
  Sound 1,12,1,8,1
  Sound 1,0,0,0
Return
'
'
' =============================================================================
' FINISH_FIELD:  Set flag for field done, determine which field is next.
' -----------------------------------------------------------------------------
Procedure Finish_field
  Gosub Cursor
  '
  Fld_done%=1
  Nxt_fld%=Fld_num%+1
  If Nxt_fld%>Num_flds% Then
    Nxt_fld%=1
  Endif
  Xsub%=1
Return
'
'
' =============================================================================
' NEXT_FIELD:  Field may not be done, determine which field is next.
' -----------------------------------------------------------------------------
Procedure Next_field
  Gosub Cursor
  '
  Fld_done%=99
  If Nxt_fld%<1 Then
    Nxt_fld%=Num_flds%
  Else
    If Nxt_fld%>Num_flds% Then
      Nxt_fld%=1
    Endif
  Endif
  Xsub%=Nxt_xsub%
Return
'
'
' =============================================================================
' HAVE_LEFT_ARROW:  Move cursor left but leave data as is.
' -----------------------------------------------------------------------------
Procedure Have_left_arrow
  If Xsub%>1 Then
    Gosub Cursor
    Dec Xsub%
    Gosub Cursor
  Endif
Return
'
'
' =============================================================================
' HAVE_RIGHT_ARROW:  Move cursor right but leave data as is.
' -----------------------------------------------------------------------------
Procedure Have_right_arrow
  If Xsub%<Fleng% Then
    Gosub Cursor
    Inc Xsub%
    Gosub Cursor
  Endif
Return
'
'
' =============================================================================
' HAVE_BACKSPACE:  Move cursor left, pulling data with it.
'      You can make the BACKSPACE key act like the LEFT ARROW key by
'      deleting this procedure and changing the related GOSUB statement
'      to GOSUB HAVE_LEFT_ARROW.
' -----------------------------------------------------------------------------
Procedure Have_backspace
  If Xsub%>1 Then
    Gosub Cursor
    Dec Xsub%
    Gosub Cursor
    Gosub Have_delete
  Endif
Return
'
'
' =============================================================================
' HAVE_DELETE:  Pull data from the right into this position.
' -----------------------------------------------------------------------------
Procedure Have_delete
  Gosub Cursor
  L%=Fleng%-Xsub%
  If L%=0 Then
    Mid$(Temp$,Fleng%,1)=" "
    Text Xchar%,Yline%," "
  Else
    Mid$(Temp$,Xsub%,L%)=Mid$(Temp$,Xsub%+1,L%)
    Mid$(Temp$,Fleng%,1)=" "
    Text Xstart%,Yline%,Temp$
  Endif
  Gosub Cursor
Return
'
'
' =============================================================================
' HAVE_INSERT:  Toggle INSERT mode off and on.
' -----------------------------------------------------------------------------
Procedure Have_insert
  If Insert_mode%=0 Then
    Insert_mode%=1
    Deftext 3,0,0,Txt_size%
    Text 488,135*Rez%,"Insert mode: ON "
  Else
    Insert_mode%=0
    Deftext 2,0,0,Txt_size%
    Text 488,135*Rez%,"Insert mode: Off"
  Endif
  Deftext 1,0,0,Txt_size%
Return
'
'
' =============================================================================
' CLEAR_FIELD:  Set the current field to spaces.
'    Both the CLR HOME key and the ESCAPE key call this procedure.
'    You can define a separate procedure for the ESCAPE key if you
'    want it to perform some other function.
' -----------------------------------------------------------------------------
Procedure Clear_field
  Gosub Cursor
  Temp$=Space$(Fleng%)
  Xsub%=1
  Text Xstart%,Yline%,Temp$
  Gosub Cursor
Return
'
'
' =============================================================================
' HAVE_UNDO_KEY:  Restore the original value of the current field.
' -----------------------------------------------------------------------------
Procedure Have_undo_key
  Gosub Cursor
  Temp$=Hold$
  Xsub%=1
  Text Xstart%,Yline%,Temp$
  Gosub Cursor
Return
'
'
' =============================================================================
' HAVE_HELP_KEY:  Display the HELP message.
' -----------------------------------------------------------------------------
Procedure Have_help_key
  X%=Int((80-Len(Fld_help$(Fld_num%)))/2)
  Print At(X%,21);Fld_help$(Fld_num%)
Return
'
'
' =============================================================================
' RECORD_IS_DONE:  Set flags to end the input process.
' -----------------------------------------------------------------------------
Procedure Record_is_done
  Gosub Cursor
  Fld_done%=1
  Rec_done%=1
Return
'
' ------------- SAVE ORIGINAL COLOR PALETTE -----------------------
Procedure Save_pal
  '
  ' Requires Dim Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,26
    Dpoke Contrl+2,0
    Dpoke Contrl+6,2
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Vdisys
    Spalette%(Z%,0)=Dpeek(Intout+2)
    Spalette%(Z%,1)=Dpeek(Intout+4)
    Spalette%(Z%,2)=Dpeek(Intout+6)
  Next Z%
Return
'
Procedure Restorepal
  ' --------------------- RESTORES PALLET -------------------
  ' Dimensions: Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,14
    Dpoke Contrl+2,0
    Dpoke Contrl+6,4
    Dpoke Intin,Z%
    Dpoke Intin+2,Spalette%(Z%,0)
    Dpoke Intin+4,Spalette%(Z%,1)
    Dpoke Intin+6,Spalette%(Z%,2)
    Vdisys
  Next Z%
Return
