'
' ---------------------------------------------
' S L I D E R
' Copyright 1988 Antic Publishing
' WRITTEN BY  STEPHEN EVERMAN & PAUL PRATT
' ----------------------------------------------
'
@Get_resolution
Dim Kolors(16),In_colors(16),Scr%(6,6)
Dim Num$(26),Un_num$(26)
@Get_in_colors
@Menu_drw
@Grid
Print At(3,5);Chr$(7)
Defmouse 3
Playing=True
Mixed=False
@Main
End
' ------------------------------------------------------------------
Procedure Main
  While Playing=True
    Mouse Mx,My,Btn
    If My<2*Ry
      @Menu
    Endif
    If Btn=1
      @Slide
    Endif
  Wend
Return
' --------------------------------------------------------------------
Procedure Menu
  Hidem
  Sget Main_screen$
  Showm
  Put 0,0,Menu$
  Looking=True
  While My<12*Ry And Looking=True
    Mouse Mx,My,Btn
    If Btn=1
      Which=(Mx Div (64*Rx))+1
      On Which Gosub Import,Scramble,View,Number,Fini
      @Release
    Endif
  Wend
  Sput Main_screen$
  @Release
Return
' --------------------------------------------------------------------
Procedure Get_resolution
  Rz=Xbios(4)
  If Xbios(4)=0
    Kind_file$="\*.PI1"
    Ofx=0
    Ofy=0
    Rx=1
    Ry=1
    Tx=1
  Endif
  If Xbios(4)=1
    Kind_file$="\*.PI2"
    Ofx=1
    Ofy=0
    Rx=2
    Ry=1
    Tx=1
  Endif
  If Xbios(4)=2
    Kind_file$="\*.PI3"
    Ofx=1
    Ofy=1
    Rx=2
    Ry=2
    Tx=0
  Endif
Return
' --------------------------------------------------------------------
Procedure Number
  Sput Main_screen$
  For Y=0 To 4
    For X=0 To 4
      If Number=True
        Put X*(64*Rx),Y*(40*Ry),Un_num$(Scr%(X+1,Y+1))
      Else
        Put X*(64*Rx),Y*(40*Ry),Num$(Scr%(X+1,Y+1))
      Endif
    Next X
  Next Y
  Sget Main_screen$
  If Number=True
    Number=False
  Else
    Number=True
  Endif
  Looking=False
Return
' -------------------------------------------------------------------
Procedure Fini
  Alert 2,"Really quit Slider?",1,"Ok|Cancel",Button
  If Button=1
    Cls
    Sget Main_screen$
    @Set_in_colors
    Playing=False
  Endif
Return
' ------------------------------------------------------------------
Procedure Slide
  Cx=(Mx Div (64*Rx))
  Cy=(My Div (40*Ry))
  Cubx=Cx*(64*Rx)
  Cuby=Cy*(40*Ry)
  @Find_dir
  If Move=True
    Get Cx*(64*Rx),Cy*(40*Ry),Cx*(64*Rx)+(63*Rx)+Ofx,Cy*(40*Ry)+(39*Ry),Obj$
    Scr%(Shitx+1,Shity+1)=Scr%(Cx+1,Cy+1)
    Scr%(Cx+1,Cy+1)=0
    '
    If Dx<>0
      Qq=0
      While Abs(Qq)<(64*Rx)
        Add Qq,Dx*(8*Rx)
        Put Cx*(64*Rx),Cy*(40*Ry),Empty$
        Put Cx*(64*Rx)+Qq,Cy*(40*Ry),Obj$
      Wend
      '      Print At(3,4);Chr$(7)
    Else
      Qq=0
      While Abs(Qq)<(40*Ry)
        Add Qq,Dy*(5*Ry)
        Put Cx*(64*Rx),Cy*(40*Ry),Empty$
        Put Cx*(64*Rx),Cy*(40*Ry)+Qq,Obj$
      Wend
    Endif
    Put Cx*(64*Rx),Cy*(40*Ry)-Ofy,Empty$
    Sget Main_screen$
  Endif
  @Release
  @Check_finished
Return
' --------------------------------------------------------------------
Procedure Check_finished
  If Mixed=True
    Good_pic=True
    For Y=0 To 4
      For X=0 To 4
        If Scr%(X+1,Y+1)<>5*Y+X
          Good_pic=False
        Endif
      Next X
    Next Y
    If Good_pic=True
      @Reward
    Endif
  Endif
Return
' -------------------------------------------------------------------
Procedure Reward
  For X=0 To 15
    Setcolor X,1904
  Next X
  For X=0 To 15
    Xx=(X/1.3)
    Sput Goal$
    Sound 1,13,Xx,5+Xx Div 3,3
    Sput Main_screen$
    Sound 2,13,Xx-1,4+Xx Div 3,5
    Setcolor X,Kolors(X)
  Next X
  Mixed=False
  Pause 10
  Wave 0
Return
' -------------------------------------------------------------------
Procedure Scramble
  @Grid
  Cx=1
  Cy=1
  '
  Alert 2," |   DO YOU WANT THE PUZZLE   | |        SCRAMBLED         |",1,"SOFT|MEDIUM|HARD",Answ
  Defmouse 3
  For R=0 To 20*2.2^Answ
    '
    Rrx=Random(3)
    If Rrx=2
      Rrx=-1
    Endif
    Rry=Random(3)
    If Rry=2
      Rry=-1
    Endif
    '
    If Scr%(Cx+Rrx,Cy+Rry)<>99 And Scr%(Cx+Rrx,Cy+Rry)<>0 And Abs(Rry)<>Abs(Rrx)
      Get (Cx+Rrx-1)*(64*Rx),(Cy+Rry-1)*(40*Ry),(Cx+Rrx-1)*(64*Rx)+(63*Rx)+Ofx,(Cy+Rry-1)*(40*Ry)+(39*Ry),Obj$
      Put (Cx+Rrx-1)*(64*Rx),(Cy+Rry-1)*(40*Ry),Empty$
      Scr%(Cx,Cy)=Scr%(Cx+Rrx,Cy+Rry)
      Scr%(Cx+Rrx,Cy+Rry)=0
      Put (Cx-1)*(64*Rx),(Cy-1)*(40*Ry),Obj$
      Sget Main_screen$
      Add Cx,Rrx
      Add Cy,Rry
      Sound 1,10+Random(5),Cx+Rrx,Cy+Rry
    Endif
  Next R
  Sound 1,0,0,0
  Looking=False
  Mixed=True
Return
' -----------------------------------------------------------------
Procedure Find_dir
  Move=False
  Restore Check
  For Chk=0 To 3
    Read Xx,Yy
    If Scr%(Cx+Xx+1,Cy+Yy+1)=0
      Shitx=Cx+Xx
      Shity=Cy+Yy
      Dx=Xx
      Dy=Yy
      Move=True
    Endif
  Next Chk
Return
Check:
Data 0,-1,1,0,0,1,-1,0
' -----------------------------------------------------------------
Procedure View
  Sput Goal$
  Hidem
  While Btn<>0
    Mouse Mx,My,Btn
  Wend
  Sput Main_screen$
  Showm
  Looking=False
Return
' ------------------------------------------------------------------
Procedure Import
  If Rz=0
    Alert 2,"Select file type",1,"Degas|Neo",Neo
    Neo=-(Neo-1)
    If Neo
      Kind_file$=Left$(Kind_file$,Len(Kind_file$)-3)+"NEO"
    Else
      Kind_file$=Left$(Kind_file$,Len(Kind_file$)-3)+"PI1"
    Endif
  Endif
  Fileselect Kind_file$,"",Fil$
  If Fil$<>""
    A=0
    While Instr(A+1,Fil$,"\")<>0 And A<Len(Fil$)
      A=Instr(A+1,Fil$,"\")
    Wend
    Kind_file$=Left$(Fil$,A)+"*"+Right$(Kind_file$,4)
    If Exist(Fil$)
      If Neo
        @Neo_load(Fil$)
      Else
        Open "I",#1,Fil$
        A=Inp(#1)
        B=Inp(#1)
        If A*256+B=Rz              !check if legal file type
          For X=0 To 15
            A=Inp(#1)
            B=Inp(#1)
            Setcolor X,A*256+B
            Kolors(X)=A*256+B
          Next X
          Bget #1,Xbios(3),32000
          Close #1
          File_loaded=True
        Else
          Alert 3,"Invalid file type",1,"Cancel",Button
          Close #1
          File_loaded=False
        Endif
      Endif
      If File_loaded
        Sget Main_screen$
        Sget Goal$
        @Grid
        Mixed=False
      Endif
    Else
      Alert 1,"File not found",1,"Cancel",Button
    Endif
  Endif
  Looking=False
Return
' ------------------------------------------------------------------
Procedure Release
  While Btn<>0
    Mouse Mx,My,Btn
  Wend
  Pause 4
Return
' ---------------------------------------------------------------------
Procedure Get_in_colors
  For X=0 To 15
    In_colors(X)=Xbios(7,X,-1)-16515072
  Next X
Return
' ------------------------------------------------------------
Procedure Set_game_colors
  Restore Kolors
  For X=0 To 15
    Read Kol
    Setcolor X,Kol
    Kolors(X)=Kol
  Next X
Return
' -----------------------------------------
Procedure Set_in_colors
  For X=0 To 15
    Setcolor X,In_colors(X)
  Next X
Return
' -------------------------------------------------------------------------
Procedure Neo_load(Filename$)
  '
  Open "I",#1,Filename$
  '
  A=Inp(#1)
  B=Inp(#1)
  File_loaded=A*256+B=0
  A=Inp(#1)
  B=Inp(#1)
  File_loaded=File_loaded And A*256+B=0
  If File_loaded
    For X=0 To 15
      A=Inp(#1)
      B=Inp(#1)
      Setcolor X,A*256+B
      Kolors(X)=A*256+B
    Next X
    Close #1
    '
    Physbase=Xbios(2)
    Bload Filename$,Physbase-128
  Else
    Alert 1,"Invalid file type",1,"Cancel",Button
    Close #1
  Endif
  '
Return
' -----------------------------------------------------------------
Procedure Grid
  Deffill 1,1,1
  Cls
  Sput Goal$
  Color 1
  For X=0 To 6
    For Y=0 To 4
      Color 1
      Box X*(64*Rx),Y*(40*Ry),X*(64*Rx)+(63*Rx)+Ofx,Y*(40*Ry)+(39*Ry)+Ofy
      Color 0
      Box X*(64*Rx)+1,Y*(40*Ry)+1,X*(64*Rx)+(62*Rx)+(2*Ofx),Y*(40*Ry)+(38*Ry)+Ofy*2
    Next Y
  Next X
  Color 1
  Pbox 0,0,63*Rx+1,39*Ry    ! blank space
  Color 0
  Box 2*Rx,2*Ry,62*Rx,38*Ry
  '
  Color 1
  Box 0,0,319*Rx+Ofx,199*Ry+Ofy    ! box whole area
  Arrayfill Scr%(),99
  Deftext 0,0,0,6
  Deffill 1,1,1
  For Y=0 To 4
    For X=0 To 4
      Get X*(64*Rx),Y*(40*Ry),X*(64*Rx)+(63*Rx),Y*(40*Ry)+(39*Ry),Un_num$(5*Y+X)
      Scr%(X+1,Y+1)=5*Y+X
      If X>0 Or Y>0
        Pbox X*(64*Rx)+2,Y*(40*Ry)+2,X*(64*Rx)+20,Y*(40*Ry)+12
        Text X*(64*Rx)+4,Y*(40*Ry)+10,5*Y+X   !NUMBER WINDOWS
      Endif
      Get X*(64*Rx),Y*(40*Ry),X*(64*Rx)+(63*Rx),Y*(40*Ry)+(39*Ry),Num$(5*Y+X)
      Sound 1,11,X+2,Y+3
    Next X
  Next Y
  Sound 1,0,0,0
  Sget Main_screen$
  Get 0,0,(64*Rx),(39*Ry)+Ofy*1,Empty$
  Number=True
  @Number
Return
' -----------------------------------------------------------------------
Procedure Menu_drw
  For X=0 To 15
    Setcolor X,In_colors(0)
  Next X
  Number=True
  Graphmode 2
  Cls
  Deftext 1,0,0,6
  Text 0,(10*Ry),319*Rx," LOAD  MIX  SHOW  NUM#  QUIT "
  Box 0,0,319*Rx,(13*Ry)
  For X=1 To 4
    Line X*(64*Rx),0,X*(64*Rx),(12*Ry)
  Next X
  Get 0,0,319*Rx,(13*Ry),Menu$
  '
  Cls
  @Set_game_colors
  Deffill 3,2,14
  Pbox -1,-1,320*Rx,200*Ry
  If Rz<2
    Deffill 2,1,2
  Else
    Deffill 1,1,2
  Endif
  Prbox 20*Rx,26*Ry,300*Rx,96*Ry
  Color Tx
  Rbox 20*Rx,26*Ry,300*Rx,96*Ry
  Rbox 23*Rx,29*Ry,297*Rx,93*Ry
  Deffill 3,1,1
  Fill (24*Rx)-1,28*Ry
  Get 20*Rx,26*Ry,300*Rx,96*Ry,Temp$
  Deffill 0,3,9
  Fill 43*Rx,58*Ry
  Deftext Tx,4,0,32
  Text 37*Rx,71*Ry,250*Rx,"SLIDER"
  Deftext Tx,0,0,6
  Text 160*Rx,89*Ry,130*Rx,Chr$(189)+"1988 Antic Publishing"
  Put 20*Rx,106*Ry,Temp$
  Deftext Tx,0,0,4
  Text 94*Rx,127*Ry,130*Rx,"WRITTEN BY"
  Deftext Tx,0,0,4
  Text 44*Rx,168*Ry,230*Rx,"MOVE MOUSE TO TOP OF SCREEN FOR MENU"
  '
  Deftext Tx,0,0,10
  Text 43*Rx,146*Ry,230*Rx,"PRATT & EVERMAN"
  '
  If Rz<2
    Deftext 3,0,0,10
    Text 45*Rx,148*Ry,230*Rx,"PRATT & EVERMAN"
    Deftext 3,4,0,32
    Text 40*Rx,74*Ry,250*Rx,"SLIDER"
  Endif
  '
  Sget Main_screen$
  Sget Goal$
  Deftext 1,1,0,6
  Cls
Return
Kolors:
Data 1556,205,1904,0,1312,7,80,5,1365,546,119,85,1360,1799,1904,0
