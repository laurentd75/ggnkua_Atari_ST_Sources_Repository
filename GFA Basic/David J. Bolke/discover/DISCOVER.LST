'
' World Discovery
'
' By David J. Bolke
' Copyright 1988 Antic Publishing Inc.
'
'
If Xbios(4)<>0
  Alert 1,"Discovery requires|  low resolution.",1,"OK",X
  End
Endif
@Initial
@New_map
Dim Spalette%(16,3)
@Save_pal
Do !   main game loop ...............................
  @Menu
  @Initgame
  On Gm Gosub Jig,Jig,Find,Find,New_map,Quit
  If Cg=States%
    @Updatescore
  Endif
Loop
Procedure Find !......................................
  @Statesoutline
  Repeat
    @Getsn
    Redo2:
    If Gm=3
      @Prints
    Else
      @Box
    Endif
    Repeat
      If (Timer-Ti)/200>T+1
        @Time
      Endif
      Mouse X,Y,K
    Until K=1 Or K=2
    If K=2
      Alert 2," | | Exit to Menu ",1,"Yes|No",K
      Inc K
      If K<>2
        Goto Redo2
      Endif
    Endif
    Exit If K=2
    M=0
    For I=X-1 To X+1
      For J=Y-1 To Y+1
        If Point(I,J)=Sc%(Sn)
          M=1
        Endif
      Next J
    Next I
    If M=0 Or Abs(Cx%(Sn)-X)>30 Or Abs(Cy%(Sn)-Y)>30
      Inc Wg
      If Gm=3
        Deftext 11,0,0,6
        Text 110+Offx,10,C$
        Text 110+Offx,20,B$
        Graphmode 1
      Else
        Deffill 4,2,8
        Prbox 108,0,300,24
        Graphmode 3
        Text 132,16,Sn$(Sn)
        Graphmode 1
      Endif
      Sound 1,10,1,2
      Wave 8,1,3,30000,70
    Else
      Inc Cg
      Rs%(Sn)=1
      Sound 1,10,1,4
      Wave 1,1,3,6000,10
      For I=1 To Np%(Sn)
        X%(I-1)=Sx%(Sn,I)
        Y%(I-1)=Sy%(Sn,I)
      Next I
      Deffill 4,2,13
      Polyfill Np%(Sn),X%(),Y%()
      Polyline Np%(Sn),X%(),Y%()
    Endif
    Exit If Cg>49
  Until Cg>49
Return
Procedure Box !...........................
  Deffill 2,2,8
  Prbox 108,0,300,24
  Deftext 1,0,0,4
  Graphmode 3
  Arrayfill X%(),0
  For X=1 To 3
    Repeat
      A=Random(Cats%)+1
    Until X%(A)=0
    X%(A)=1
    B$=D$(Sn,A)
    A$=Cl$(A)+": "+B$
    Text 112,X*8-1,A$
  Next X
  Graphmode 1
Return
Procedure Getsn !.................
  Repeat
  Until Mousek=0
  Repeat
    Sn=Random(States%)+1
  Until Rs%(Sn)=0
Return
Procedure Quit !.............................
  Alert 2," | | Exit to Desktop ",1,"No|Yes",X
  If X=2
    @Restore_pal
    Edit
  Endif
Return
Procedure Jig !.........................................
  Hidem
  If Gm=1
    @Statesoutline
  Else
    @Usaoutline
  Endif
  Repeat
    @Getsn
    Redo:
    @Prints
    Repeat
      Mouse X,Y,K
      Put X,Y,S$(Sn),6
      @Time
      Put X,Y,S$(Sn),6
    Until K=1 Or K=2
    If K=2
      Alert 2," | | Exit to Menu ",1,"Yes|No",K
      Inc K
      If K<>2
        Goto Redo
      Endif
    Endif
    Exit If K=2
    If Abs(Sx%(Sn,0)-X)>4 Or Abs(Sy%(Sn,0)-Y)>4
      Inc Wg
      Sound 1,10,1,2
      Wave 8,1,3,20000,10
    Else
      Inc Cg
      Rs%(Sn)=1
      Sound 1,10,1,5
      Wave 1,1,3,6000,10
      For I=1 To Np%(Sn)
        X%(I-1)=Sx%(Sn,I)
        Y%(I-1)=Sy%(Sn,I)
      Next I
      Deffill Sc%(Sn),2,8
      Polyfill Np%(Sn),X%(),Y%()
      Polyline Np%(Sn),X%(),Y%()
    Endif
  Until Cg>49
Return
Procedure Time !.........................
  T=Int((Timer-Ti)/200)
  Deftext 2,0,0,6
  A$="T="+Str$(T)+" "
  Text 170,198,A$
  P=0
  If Cg+Wg>0
    P=Int((Cg/(Cg+Wg))*1000)/10
  Endif
  A$="% "+Str$(P)+"   "
  Text 170,188,A$
  A$="ToGo "+Str$(States%-Cg)+" "
  Text 170,178,A$
Return
Procedure Prints !....................
  Deftext 8,0,0,4
  Text 110,10,State$
  Text 110,20,Capital$
  Deftext 7,0,0,6
  C$=String$(15,32)
  Text 110+Offx,10,C$
  Mid$(C$,1)=Sn$(Sn)
  B$=String$(15,32)
  Text 110+Offx,20,B$
  Mid$(B$,1)=Cn$(Sn)
  If Rnd<0.5
    Text 110+Offx,10,C$
  Else
    Text 110+Offx,20,B$
  Endif
  If Gm<3
    Text 110+Offx,10,C$
    Text 110+Offx,20,B$
  Endif
Return
Procedure Initgame !..............................
  Sc=0
  Cg=0
  Wg=0
  Arrayfill Rs%(),0
  T=0
  Ti=Timer
Return
Procedure Updatescore !............................
  I=1
  Repeat
    If Pt(Gm,I)=0
      Pt(Gm,I)=T
      Pc(Gm,I)=Int((Cg/(Cg+Wg))*1000)/10
      I=8
    Endif
    Inc I
  Until I>5
  If I<7
    For I=1 To 4
      Pt(Gm,I)=Pt(Gm,I+1)
      Pc(Gm,I)=Pc(Gm,I+1)
    Next I
    Pt(Gm,5)=T
    Pc(Gm,5)=Int((Cg/(Cg+Wg))*1000)/10
  Endif
Return
Procedure Menu !...................................
  Cls
  Showm
  Deftext 11,1,0,20
  Text 0,22,Game_title$
  Deftext 11,0,0,4
  Text 27,30,"by David J. Bohlke"
  Text 15,37,Chr$(189)+"1988 Antic Publishing"
  @Smallusa
  For I=1 To 4
    Deftext 12,0,0,6
    Text 5,20*I+100,Gm$(I)
    Deffill 13,0,0
    For J=1 To 5
      Pbox J*30+120,I*20+90,J*30+150,I*20+110
      Deftext 2,0,0,4
      S$=Str$(Pt(I,J))
      Text J*30+122,I*20+98,S$
      Deftext 11,0,0,4
      S$=Str$(Pc(I,J))
      Text J*30+122,I*20+106,S$
    Next J
  Next I
  Deftext 1,0,0,4
  Text 0,197,"Left click to select, Right click to execute..."
  Text 150,107,"Last five times, percents"
  Repeat
    For I=1 To 6
      Deftext 14,2,0,6
      Deffill 5,2,15
      If Gm=I
        Deftext 14,0,0,6
        Deffill 8,2,8
      Endif
      Prbox 0,I*10+32,30,I*10+40
      Text 35,I*10+40,Gm$(I)
    Next I
    Repeat
      X=Mousex
      Y=Mousey
    Until (Mousek=1 And X<30 And Y>40 And Y<100) Or Mousek=2
    If Mousek=1
      Gm=Int((Y-10)/10)-2
    Endif
  Until Mousek=2
Return
Procedure Statesoutline !..............................
  Cls
  Color 1
  For I=1 To States%
    Sound 1,8,#Random(40)*20+200,0
    For C=0 To Np%(I)-1
      X%(C)=Sx%(I,C+1)
      Y%(C)=Sy%(I,C+1)
    Next C
    If Gm=3 Or Gm=4
      Deffill Sc%(I),2,8
    Else
      Deffill 4,2,8
    Endif
    Polyfill Np%(I),X%(),Y%()
    Polyline Np%(I),X%(),Y%()
  Next I
  Sound 1,0,0,0
Return
Procedure Usaoutline !...................................
  Cls
  For I=1 To States%
    Sound 1,8,#Random(40)*30+200,0
    For C=0 To Np%(I)-1
      X%(C)=Sx%(I,C+1)
      Y%(C)=Sy%(I,C+1)
    Next C
    Deffill 4,2,8
    Polyfill Np%(I),X%(),Y%()
  Next I
  Sound 1,0,0,0
Return
'
Procedure Initial !.........................................
  Max_states%=100
  Max_points%=30
  Max_cats%=15
  Dim Sc%(Max_states%),X%(99),Y%(99),Sx%(Max_states%,Max_points%),Cl$(Max_cats%)
  Dim Sy%(Max_states%,Max_points%),Np%(Max_states%),Rs%(Max_states%)
  Dim Cx%(Max_states%),Cy%(Max_states%),Sn$(Max_states%),Cn$(Max_states%)
  Dim S$(Max_states%),Gm$(6),Pt(4,5),Pc(4,5),D$(Max_states%,Max_cats%)
  First_game!=True
Return
'
Procedure New_map
  '
  Fileselect "\*.DSC","",State_file$
  If State_file$<>""
    If Exist(State_file$)
      '
      Arrayfill Sx%(),0
      Arrayfill Sy%(),0
      Arrayfill Pt(),0
      Arrayfill Pc(),0
      Gm=0
      '
      Open "I",#1,State_file$
      '
      Line Input #1,Game_title$ ! e.g. USA DISCOVERY
      If Len(Game_title$)>15
        Game_title$=Left$(Game_title$,15)
      Endif
      '
      Line Input #1,State$
      Line Input #1,Capital$
      If Len(State$)>10
        State$=Left$(State$,10)
      Endif
      If Len(Capital$)>10
        Capital$=Left$(Capital$,10)
      Endif
      '
      If Len(State$)>Len(Capital$) ! Clean up display here
        Offx=Int(6.5*Len(State$))
      Else
        Offx=Int(6.5*Len(Capital$))
      Endif
      '
      I=0
      Do
        Line Input #1,Cl$ ! Categories until "LAST CAT"
        Exit If Cl$="LAST CAT"
        If Len(Cl$)>15
          Cl$=Left$(Cl$,15)
        Endif
        I=I+1
        Cl$(I)=Cl$
      Loop
      Cats%=I
      For I=0 To 15
        Input #1,A
        Input #1,B
        Input #1,C
        Setcolor I,A,B,C
      Next I
      '
      Cls
      Deftext 8,17,0,32
      Text 10,140,"Discovery"
      Print At(25,23);"Reading DATA ..."
      Deftext 1,0,0,4
      Text 192,197,"Written in GFA Basic."
      '
      I=1
      While Not Eof(#1)
        Deffill 0,2,8
        Pbox 0,0,90,90
        Color 1
        Lx=2000
        Hx=0
        Ly=2000
        Hy=0
        C=0
        Line Input #1,Sn$(I) ! State name
        If Len(Sn$(I))>15
          Sn$(I)=Left$(Sn$(I),15)
        Endif
        Line Input #1,Cn$(I) ! Capital name
        If Len(Cn$(I))>15
          Cn$(I)=Left$(Cn$(I),15)
        Endif
        Input #1,Sc%(I) ! State color
        '
        Do
          Input #1,X
          Input #1,Y
          Exit If X=0
          Inc C
          Sx%(I,C)=X ! X polygon point of state, point within state
          Sy%(I,C)=Y ! Y polygon point of state, point within state
          If Y>Hy
            Hy=Y
          Endif
          If Y<Ly
            Ly=Y
          Endif
          If X>Hx
            Hx=X
          Endif
          If X<Lx
            Lx=X
          Endif
        Loop
        Np%(I)=C ! Number of points
        Cx%(I)=(Hx+Lx)/2 ! Center x point
        Cy%(I)=(Hy+Ly)/2 ! Center y point
        For C=1 To Np%(I) ! Push into one dimension array to draw
          X%(C-1)=Sx%(I,C)-Lx
          Y%(C-1)=Sy%(I,C)-Ly
        Next C
        Deffill 2,2,7
        Polyfill Np%(I),X%(),Y%()
        Get 0,0,Hx-Lx+1,Hy-Ly+1,S$(I) ! Get state to Put later as mouse pointer
        Sx%(I,0)=Lx
        Sy%(I,0)=Ly
        For J=1 To Cats%
          Line Input #1,D$(I,J) ! Read category data
          If Len(D$(I,J))>17
            D$(I,J)=Left$(D$(I,J),17)
          Endif
        Next J
        Inc I
      Wend
      States%=I-1 ! Number of states
      '
      Close #1
      First_game!=False
      '
      Restore Game_options
      For I=1 To 6
        Read Gm$(I) ! Menu options
      Next I
      Gm$(3)="Find "+State$+"/"+Capital$
      Game_options:
      Data Jigsaw (outline),Jigsaw (freehand),Find state/capital,Quiz
      Data Load new file,Quit
      Gm=1
    Else
      Alert 1,"Can't find discovery file.",1,"Cancel",Dummy
      If First_game!
        Edit
      Else
        Gm=1
      Endif
    Endif
  Else
    If First_game!
      Edit ! Must select at least a file
    Else
      Gm=1
    Endif
  Endif
Return
Procedure Show_points(P)
  For I=1 To Np%(P)
    Print Sx%(P,I),Sy%(P,I)
  Next I
  Print
Return
Procedure Smallusa !..............................
  Deffill 4,2,8
  Prbox 185,0,315,80
  Rbox 185,0,315,80
  Rbox 184,0,316,80
  Rbox 183,0,317,80
  For I=1 To States%
    Sound 1,8,#Random(50)*10+200,0
    For J=1 To Np%(I)
      X%(J-1)=Sx%(I,J)*0.4+187
      Y%(J-1)=Sy%(I,J)*0.4+1
    Next J
    Deffill Random(10)+5
    Polyfill Np%(I),X%(),Y%()
    Polyline Np%(I),X%(),Y%()
  Next I
  Sound 1,0,0,0
Return
' ------------- SAVE ORIGINAL COLOR PALETTE -----------------------
Procedure Save_pal
  '
  ' Requires Dim Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,26
    Dpoke Contrl+2,0
    Dpoke Contrl+6,2
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Vdisys
    Spalette%(Z%,0)=Dpeek(Intout+2)
    Spalette%(Z%,1)=Dpeek(Intout+4)
    Spalette%(Z%,2)=Dpeek(Intout+6)
  Next Z%
Return
'
Procedure Restore_pal
  ' --------------------- RESTORES PALLET -------------------
  ' Dimensions: Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,14
    Dpoke Contrl+2,0
    Dpoke Contrl+6,4
    Dpoke Intin,Z%
    Dpoke Intin+2,Spalette%(Z%,0)
    Dpoke Intin+4,Spalette%(Z%,1)
    Dpoke Intin+6,Spalette%(Z%,2)
    Vdisys
  Next Z%
Return
