' ******************************************************************
' *                                                                *
' *                          BOXMOUSE.LST                          *
' *                                                                *
' *                Written for ST FORMAT MAGAZINE                  *
' *                                                                *
' *          This program will run in GFA Basic V2 or V3           *
' *                                                                *
' *                                                                *
' ******************************************************************
'
' This program demonstrates how to make a Mouse stay inside an area
' so, if you have a Box you want a user to click on, you can make the mouse
' go to it, and not come out until he has clicked inside that box.
'
'
Print "Demo of SETMOUSE command"
Print
Print "Try to get the mouse out of the Big Box"
Print
For N=0 To 10
  Print
Next N
Print "(Ps. press a mouse button)"
'
'
Box 100,100,50,50  ! Draw a box
'
@Setmouse(72,72)   ! put the mouse inside the box
'
Do
  If Mousex>100          ! if the mouse goes too far right
    @Setmouse(100,Mousey)! make it come back inside the box
  Endif
  '
  If Mousex<50           ! too far left
    @Setmouse(50,Mousey) ! back you come
  Endif
  '
  If Mousey>100          ! too far down
    @Setmouse(Mousex,100)
  Endif
  '
  If Mousey<50           ! too far up
    @Setmouse(Mousex,50)
  Endif
  '
  If Mousek
    @Movem              ! OK so you pressed the mouse button, now we can
  Endif                 ! carry on with the program
  '
Loop
'
'
'
Procedure Setmouse(Mx%,My%) ! this proc puts the mouse pointer at Mx%,My%
  Dpoke Contrl+2,2
  Dpoke Contrl+6,2
  Lpoke Intin,&H10002
  Vdisys 33
  Dpoke Ptsin,Mx%
  Dpoke Ptsin+2,My%
  Vdisys 28
  Showm
Return
'
Procedure Movem    ! yep, you pressed the mouse, so print a message
  If M1=0
    Print "Press again to re-enter the box"
  Endif
  @Setmouse(190,90)! set the mouse outside the box
  Pause 10
  Repeat
  Until Mousek     !Uh Oh, you pressed the mouse button again
  @Setmouse(72,72) ! Back you go inside the box
  Pause 10
  M1=1             !But don't print the message again
Return
