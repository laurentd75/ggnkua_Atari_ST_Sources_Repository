' ******************************************************************
' *                                                                *
' *                             DIR.LST                            *
' *                                                                *
' *                Written for ST FORMAT MAGAZINE                  *
' *                                                                *
' *          This program will run in GFA Basic V2 or V3           *
' *                                                                *
' *                                                                *
' ******************************************************************
'
' This program gets a directory of a disk, and prints the contents
' to the screen. If you want, you can redirect to the printer
' or a file by changing the print commands.
'
'
' set up some work space
Dim Fs$(4000),Path$(512),Sub_directory$(255),Tm%(4000),Dt%(4000)
Fpth$=Space$(8000)
'
Input "Enter Drive Letter to get Directory of ",Drive$
Drive$=Upper$(Drive$)+":"
' Drive$="A:"
'
' set up default variables
Sud%=0
Sub_directory$(1)="\"
Path$="\"
Sdd%=0
Number_of_files%=0
P%=1
'
While P%>0
  Buf$=Space$(45)
  Path$=Sub_directory$(P%)
  Fs$=Drive$+Sub_directory$(P%)+"*.*"+Chr$(0)
  Dec P%
  Attr%=&H10 ! search for directories
  @Get_first
  While A%=0
    @Get_next
  Wend
Wend
Inc Pass%
Print
Print Number_of_files%;" Files and ";Sdd%;" Folders Found on drive ";Drive$
Print "Bytes Free = ";Dfree(Instr("ABCDEFGHIJKLMNOP",Left$(Drive$,1)))
'
Procedure Get_first
  Void Gemdos(&H1A,L:Varptr(Buf$)) !  SET_DTA (buffer for drive data)
  A%=Gemdos(&H4E,L:Varptr(Fs$),Attr%) ! SFIRST
  If A%=0
    Gosub Rd_buff
  Endif
Return
'
Procedure Get_next
  Void Gemdos(&H1A,L:Varptr(Buf$))
  A%=Gemdos(&H4F)                    ! SNEXT
  If A%=0
    Gosub Rd_buff
  Endif
Return
'
Procedure Rd_buff
  F$=Mid$(Buf$,31,Instr(31,Buf$,Chr$(0))-31)
  S%=Lpeek(Varptr(Buf$)+26)
  Atr%=Asc(Mid$(Buf$,22,1))
  Dt%=Dpeek(Varptr(Buf$)+24)
  Tm%=Dpeek(Varptr(Buf$)+22)
  If (Atr% And &H10)<>0 And Left$(F$,1)<>"."
    Inc P%
    Sub_directory$(P%)=Path$+F$+"\"
    Inc Sd%
    Inc Sdd%
    Path$(Sd%)=Path$
    Sud%=Sd%
  Endif
  If (Atr% And &H10)=0
    Inc I%
    Inc Number_of_files%
    Fs$(I%)=F$
    Dt%(I%)=Dt%
    Tm%(I%)=Tm%
    Mid$(Fpth$,I%+I%-1,2)=Mki$(Sud%)
    If Path$<>Lpth$ Then
      Print
      Print "Path=";Path$
      Lpth$=Path$
    Endif
    Print Using "\............\   ###,###,###",F$,S%
  Endif
Return
