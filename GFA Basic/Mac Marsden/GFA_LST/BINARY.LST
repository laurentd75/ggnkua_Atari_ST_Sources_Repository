CLS
@getnum                                         ! CALL PROCEDURE getnum
@find_num                                       ! CALL PROCEDURE findnum
END                                             ! END PROGRAM
PROCEDURE getnum
  DIM a(20)                                     ! ALLOW MEMORY FOR DATA
  f=1                                           ! GET DATA
  DO
    a(f)=f
    INC f
  LOOP UNTIL f>20
  PRINT AT(1,21);"File     Data"                ! DISPLAY DATA
  f=1
  DO
    PRINT AT(2,f);CHR$(f+64);" :     ";a(f)
    INC f
  LOOP UNTIL f>20
RETURN
PROCEDURE find_num
  DO
    finish!=FALSE
    found!=FALSE                                ! SET LOGICAL FLAG
    PRINT AT(1,23);"[ ENTER '9999' TO END ] NUMBER to find (1-20) ";
    INPUT b                                     ! GET NUMBER TO SEARCH FOR
    PRINT AT(1,21);SPC(17)
    PRINT AT(1,23);SPC(60)
    FOR f=1 TO 20                               ! CLEAR OLD SCREEN POINTER
      PRINT AT(20,f);SPC(32)
    NEXT f
    PRINT AT(1,21);"File     Data"
    IF b=9999                                   ! IF B=9999 THEN END
      END
    ENDIF
    low%=0                                      ! INITIALISE VARIABLES
    high%=21
    pass=0
    DO                                          ! BEGIN SEARCH LOOP
      pointer%=(low%+high%)/2                   ! GET POSITION OF DATA
      IF pass=0                                 ! PRINT CURRENT POSITION ON SCREEN
        PRINT AT(20,pointer%);"<--- Start position"
      ELSE
        PRINT AT(20,pointer%);"<--- Pass ";pass
      ENDIF
      IF a(pointer%)=b                          ! DO IF NUMBER IS FOUND
        PRINT AT(20,pointer%);"<--- NUMBER Found after ";pass;" Passes "
        found!=TRUE
      ENDIF
      IF a(pointer%)>b                          ! IF GREATER THAN, THEN
        high%=pointer%                          ! CHANGE POINTER.
      ENDIF
      IF a(pointer%)<b                          ! IF LESS THAN, THEN
        low%=pointer%                           ! CHANGE POINTER.
      ENDIF
      INC pass                                  ! INCREASE COUNTER
    LOOP UNTIL pass>5 OR found!     ! LOOP UNTIL DATA FOUND OR COUNTER GREATER
    IF pass>5                       ! THAN MAXIMUM SEARCH COUNT
      PRINT AT(1,21);"number not found"
    ENDIF
  LOOP
RETURN
