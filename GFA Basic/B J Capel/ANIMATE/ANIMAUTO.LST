'          *******************************************************
'          **                  Hot Air Balloon                  **
'          **            Animating With GFA Basic V2            **
'          **         Program And Graphics By  B J Capel        **
'          **               For ST Format Magazine              **
'          **                     July  1989                    **
'          *******************************************************
'
' This file will also run unmodified in GFA V3 if you have it!
' Press the left mouse button or break to quit demo before the end.
'
On Break Gosub End_prog
Hidem
Dim Screen%(32255/4)
Screen1%=Xbios(3)
Screen2%=(Varptr(Screen%(0))+255) And &HFFFF00
Dim Frame$(20)
Temp$=Space$(32000)
Open "I",#1,"ANIMATE\MOUNT.PIC"
Bget #1,Varptr(Temp$),32000
Close #1
@Load_data("ANIMATE\BALLOON",*Frame$())
@Setpalette
@Swap_screen(Screen2%,Screen2%)
Sput Temp$
@Swap_screen(Screen1%,Screen1%)
X%=-15
Y%=152
Anim_frame_num=1
'
Do
  Vsync
  @Swap_screen(Screen1%,Screen2%)
  Vsync
  Sput Temp$
  If Y%>152
    Y%=152
  Endif
  Put X%,Y%,Frame$(Int(Anim_frame_num)+4),4    ! Balloon Mask
  Put X%,Min(50,Y%)+110,Frame$(10),4           ! Shadow
  Put X%,Y%,Frame$(Int(Anim_frame_num)),6      ! Balloon
  Swap Screen1%,Screen2%
  '  Pause 2
  If X%<>-15 Or Y%<>152
    Add Anim_frame_num,0.5
    If Anim_frame_num=5
      Anim_frame_num=1
    Endif
  Endif
  Z%=Mousek
  Exit If Z%=1 Or Fdata%=9
  Read Fdata%
  If Fdata%=1
    Dec Y%
  Endif
  If Fdata%=2
    Inc X%
    Dec Y%
  Endif
  If Fdata%=3
    Inc X%
    Inc Y%
  Endif
  If Fdata%=4
    Inc X%
  Endif
Loop
Gosub End_prog
'
Procedure Swap_screen(Screen1%,Screen2%)
  Void Xbios(5,L:Screen1%,L:Screen2%,-1)
Return
'
Procedure Setpalette
  For L%=0 To 15
    Nd$=Mid$(Colour_palette$,L%*3+1,3)
    Cv%=256*Val(Mid$(Nd$,1,1))+16*Val(Mid$(Nd$,2,1))+Val(Mid$(Nd$,3))
    Setcolor L%,Cv%
  Next L%
  Nd$=""
Return
'
Procedure Load_data(Cartoon_filename$,P.sarr)
  Local File_length%,X%,A%
  Dim Cartoon_storage_array$(20)
  Open "I",#1,Cartoon_filename$
  File_length%=Lof(#1)
  Buffer$=Space$(File_length%)
  Bget #1,Varptr(Buffer$),File_length%
  Close #1
  Colour_palette$=Mid$(Buffer$,File_length%-47,48)
  X%=1
  For A%=1 To 10
    Cartoon_storage_array$(A%)=Mid$(Buffer$,X%,1062)
    Add X%,1062
  Next A%
  Swap *P.sarr,Cartoon_storage_array$()
  Erase Cartoon_storage_array$()
  Clr Buffer$
Return
'
' Flight Control Data
Data 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
Data 2,2,2,2,2,2,1,2,1,2,2,1,2,1,2,1,2,2,2,2,2,1,2,1,2,1,1
Data 2,2,2,2,2,1,2,2,2,2,1,2,1,2,1,2,2
Data 2,2,2,2,2,1,2,2,2,2,2,2,2,2,1,2,2,1,2,2,2,1,2,2,2,1,2,1,2,1,2,1,2
Data 1,2,2,2,1,2,2,2,2,1,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,2,1
Data 2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2
Data 3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3
Data 3,3,3,3,3,3,3,3,3,3,4,4,4,4,2,2,2,2,2,2,2,4,2,2,2,2,4,2,4,2,2
Data 4,2,2,4,2,2,2,2,2,2,2,4,2,2,4,2,4,2,2,4,2,4,2,4,1
Data 4,2,4,2,2,4,2,4,2,2,4,2,4,2,4,2,4,2,4,2,2,4,2,4,2,2,4,2
Data 4,2,4,2,4,2,3,3,3,3,3,3,3,3,3,3,3,3,3,3,2,3,3,3,3,3,4,3,3,4,3,3
Data 4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,4,2,1
Data 4,2,2,4,2,2,4,2,2,4,2,2,4,2,2,4,2,2,4,2,2,4,2,4,2,2,4,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,9
'
Procedure End_prog
  @Swap_screen(Screen1%,Screen1%)
  Defmouse 0
  Sput Temp$
  Print At(9,18);" Balloon Demo Finished "
  Print At(9,19);"Press Left Mouse Button"
  While Mousek=0
  Wend
  Cls
  Setcolor 0,7,7,7
  Setcolor 1,0,7,0
  Setcolor 15,0,0,0
  Edit
Return
