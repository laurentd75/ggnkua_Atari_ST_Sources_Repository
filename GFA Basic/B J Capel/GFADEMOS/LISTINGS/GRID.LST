On Break Gosub End
'          *******************************************************
'          **                   Plotting Demo                   **
'          **            Graphics With  GFA Basic V2            **
'          **                Program By B J Capel               **
'          **               For ST Format Magazine              **
'          **                     July  1989                    **
'          *******************************************************
'
' This file will also run unmodified in GFA V3 if you have it!
' Press Break (Alt-Shift-Control) to quit demo.
'
Setcolor 0,0,0,0
Setcolor 15,7,7,7
Sget Screen$
Start:
Cls
Offset%=100
R%=85
C=1
@Sbox
@Bbox
@Colours
@Exit_box
Do
  Mouse Across%,Down%,Button%
  If Across%>102 And Across%<300 And Down%>2 And Down%<198 And Button%=1
    @Draw
  Endif
  If Across%>0 And Across%<80 And Down%>85 And Down%<165 And Button%=1
    C=Point(Across%,Down%)
    @Act_colour
  Endif
  If Across%>9 And Across%<71 And Down%>60 And Down%<75 And Button%=1
    Get 20,13,39,32,Screen_cut$
    Get 50,13,69,32,Screen_mask$
    Sput Screen$
    Pause 5
    Hidem
    Do
      Mouse X%,Y%,Z%
      Put X%,Y%,Screen_cut$
      Sput Screen$
      If Z%=1
        Put X%,Y%,Screen_mask$,4
        Put X%,Y%,Screen_cut$,6
        Sget Screen$
      Endif
      Exit If Z%=2
      If Inkey$=" "
        Cls
        Sget Screen$
      Endif
    Loop
    Showm
    Color 1
    Goto Start
  Endif
Loop
'
Procedure Draw
  Deffill C
  Color C
  Px%=(Across%-102)/10
  Py%=(Down%-2)/10
  Plot Px%+20,Py%+13
  If C>0
    Color 1
  Else
    Color 0
  Endif
  Plot Px%+50,Py%+13
  Pbox Px%*10+3+Offset%,Py%*10+2,Px%*10+11+Offset%,Py%*10+10
Return
'
Procedure Sbox
  Deftext 3,0,0,4
  Text 10,40,"Sprite Mask"
  Box 19,12,40,33
  Box 49,12,70,33
Return
'
Procedure Bbox
  For X%=1 To 19
    Line X%*10+2+Offset%,2,X%*10+2+Offset%,199
    Line 103,X%*10+1,301,X%*10+1
  Next X%
Return
'
Procedure Colours
  For Counter=0 To 15
    Deffill 1
    Pbox A*20,R%,A*20+20,R%+20
    Deffill Counter
    Pbox A*20+1,R%+1,A*20+19,R%+19
    Inc A
    If A=4
      A=0
      Add R%,20
    Endif
  Next Counter
  @Act_colour
  Print At(2,25);"Selected";
Return
'
Procedure Act_colour
  Deffill 15
  Box 9,169,71,186
  Deffill C
  Pbox 10,170,70,185
Return
'
Procedure Exit_box
  Deffill 15
  Box 9,60,71,75
  Print At(4,9);"TEST"
Return
'
Procedure End
  Setcolor 0,7,7,7
  Setcolor 15,0,0,0
  Edit
Return
