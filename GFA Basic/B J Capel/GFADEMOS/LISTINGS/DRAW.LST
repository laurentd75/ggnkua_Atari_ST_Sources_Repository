'          *******************************************************
'          **              Kaleidoscope  Draw Demo              **
'          **            Graphics With  GFA Basic V2            **
'          **                Program By B J Capel               **
'          **               For ST Format Magazine              **
'          **                     July  1989                    **
'          *******************************************************
'
' This file will also run unmodified in GFA V3 if you have it!
' Press Break (Alt-Shift-Control) to quit demo.
'
On Break Gosub Break
Cls
@Setup
Do
  While Mousek=1
    Mouse X1%,Y1%,K%
    X2%=319-X1%-5
    Y2%=199-Y1%-5
    X3%=X1%
    Y3%=199-Y1%-5
    X4%=X2%
    Y4%=Y1%
    Hidem
    Color C1%
    Box X1%,Y1%,X1%+5,Y1%+5
    Color C2%
    Box X2%,Y2%,X2%+5,Y2%+5
    Color C3%
    Box X3%,Y3%,X3%+5,Y3%+5
    Color C4%
    Box X4%,Y4%,X4%+5,Y4%+5
  Wend
  Showm
  If Mousek=2
    Sget Screen$
    Cls
    @Screen
    Sput Screen$
    Defmouse 7
  Endif
Loop
'
Procedure Screen
  @Colours
  Defmouse 0
  Do
    Mouse X%,Y%,Z%
    If Z%=1
      @Colours_to_use
    Endif
    '    Pause 10
    Exit If Z%=2
  Loop
  Pause 5
Return
'
Procedure Setup
  Setcolor 15,7,7,7
  Setcolor 0,0,0,0
  C1%=1
  C2%=2
  C3%=3
  C4%=4
Return
'
Procedure Colours
  For A=0 To 15
    Deffill 1
    Pbox A*20,0,A*20+10,20
    Deffill A
    Pbox A*20+1,1,A*20+9,19
  Next A
Return
'
Procedure Colours_to_use
  C1%=Point(X%,Y%)
  C2%=C1%+1
  C3%=C2%+1
  C4%=C3%+1
  Deffill C1%
  Pbox 50,50,70,70
  Deffill C2%
  Pbox 100,50,120,70
  Deffill C3%
  Pbox 150,50,170,70
  Deffill C4%
  Pbox 200,50,220,70
Return
'
Procedure Break
  Setcolor 0,7,7,7
  Setcolor 15,7,7,7
  Edit
Return
