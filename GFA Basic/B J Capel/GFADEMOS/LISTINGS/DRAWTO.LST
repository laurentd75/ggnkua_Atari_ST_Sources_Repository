On Break Gosub End
'          *******************************************************
'          **                   Drawing  Demo                   **
'          **            Graphics With  GFA Basic V2            **
'          **                Program By B J Capel               **
'          **               For ST Format Magazine              **
'          **                     July  1989                    **
'          *******************************************************
'
' This file will also run unmodified in GFA V3 if you have it!
' Press Break (Alt-Shift-Control) to quit demo.
'
Cls
R%=5
Defline ,3
C=1
@Spritemake
Do
  Mouse Across%,Down%,Button%
  If Button%=1
    Plot Across%,Down%
    While Button%=1
      Mouse Across%,Down%,Button%
      Draw  To Across%,Down%
    Wend
  Endif
  If Button%=2
    @Colours
  Endif
Loop
'
Procedure Colours
  Defline ,1
  A=0
  Color 1
  Sget Screen$
  For Counter=0 To 15
    Box A*18,R%,A*18+18,R%+18
    Deffill Counter
    Pbox A*18+1,R%+1,A*18+17,R%+17
    Inc A
  Next Counter
  Pause 5
  Repeat
    Mouse X%,Y%,Z%
    If Z%=1
      C=Point(X%,Y%)
      Color C
      @Spritemake
    Endif
  Until Y%<24 And Z%=1
  Sput Screen$
  Defline ,3
  Pause 10
Return
'
Procedure Spritemake
  Restore Spritedata
  '
  ' **** The following lines were created with SPRITED.LST *****
  '
  A$=Mki$(1)+Mki$(1)+Mki$(0)+Mki$(0)+Mki$(C)
  For N=0 To 15
    Read X,Y
    A$=A$+Mki$(X*256+Y)
  Next N
  For N=0 To 15
    Read X,Y
    A$=A$+Mki$(X*256+Y)
  Next N
  Spritedata:
  Data 248,0,140,0,198,0,66,0,67,0,97,128,56,192,12,96,6,48,3,24,1,140,0,198,0,99,0,49,0,25,0,15,0,0,112,0,56,0,60,0,60,0,30,0,7,0,3,128,1,192,0,224,0,112,0,56,0,28,0,14,0,6,0,0,0,0
  Defmouse A$
  ' ***************************************************************
Return
