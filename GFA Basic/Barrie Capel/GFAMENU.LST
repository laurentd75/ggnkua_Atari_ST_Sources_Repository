Dim Menu_bar$(30),A$(11)
@Setup
'
For N%=0 To 30
  Read Menu_bar$(N%)
  Exit If Menu_bar$(N%)="Los Endos"
Next N%
Menu_bar$(N%)=""
Menu_bar$(N%+1)=""
'
Data Desk, About Me,"------------------",-1,-2,-3,-4,-5,-6,""
Data Menu 1, Item 1    , Item 2, Item 3, Item 4,""
Data Menu 2, Item a    , Item b, Item c,""
Data Menu 3, Graphics , Text, Put, Exit,""
Data "Los Endos"
'
Menu Menu_bar$()
On Menu  Gosub Menu_proc
Do
  On Menu
Loop
'
Procedure Menu_proc
  If Menu(0)=1
    @About
  Endif
  If Menu(0)>1 And Menu(0)<22
    Print At(1,22);"Dummy Item Number ";Menu(0);" selected!";"  "
  Endif
  If Menu(0)=22
    @Graphics
  Endif
  If Menu(0)=23
    @Text
  Endif
  If Menu(0)=24
    @Put
  Endif
  If Menu(0)=25
    End
  Endif
  Menu Off
Return
'
Procedure About
  Get 0,0,Width%,Bar_height%,Bar$
  Cls
  Print
  Print
  Print
  For A%=0 To 11
    If Rez%=0
      Print A$(A%);
    Else
      Gap%=Int(((Width%/8)-Len(A$(A%)))/2)
      Print Tab(Gap%);A$(A%);
    Endif
  Next A%
  Put 0,0,Bar$
Return
'
Procedure Graphics
  Get 0,0,Width%,Bar_height%,Bar$
  Cls
  For A%=1 To 200
    X%=Int(Rnd*(Width%-50)+1)
    Y%=Int(Rnd*(Height%-50)+1)
    Offset%=Int(Rnd*50)+10
    Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
    Pbox X%,Y%,X%+Offset%,Y%+Offset%
  Next A%
  For A%=1 To 50
    X%=Int(Rnd*(Width%-50)+1)
    Y%=Int(Rnd*(Height%-50)+1)
    Offset%=Int(Rnd*50)
    Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
    Pcircle X%,Y%,Offset%
  Next A%
  For A%=1 To 100
    X%=Int(Rnd*(Width%-50)+1)
    Y%=Int(Rnd*(Height%-50)+1)
    Offset%=Int(Rnd*50)
    Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
    Prbox X%,Y%,X%+Offset%,Y%+Offset%
  Next A%
  Put 0,0,Bar$
Return
'
Procedure Text
  Get 0,0,Width%,Bar_height%,Bar$
  Cls
  For A%=1 To 300
    Deftext Cols%
    X%=Int(Rnd*(Width%-50)+1)
    Y%=Int(Rnd*(Height%)+1)
    Text X%,Y%,"ST Format"
  Next A%
  Put 0,0,Bar$
Return
'
Procedure Put
  Get 0,0,Width%,Bar_height%,Bar$
  Cls
  Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
  Pbox 0,0,100,100
  Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
  Pcircle 25,25,15
  Pcircle 75,25,15
  Pcircle 25,75,15
  Pcircle 75,75,15
  Deffill Int(Rnd*Cols%)+1,2,Int(Rnd*24)+1
  Prbox 30,30,70,70
  Get 0,0,100,100,Blit$
  Cls
  For A%=1 To 1000
    X%=Int(Rnd*(Width%-100)+1)
    Y%=Int(Rnd*(Height%-100)+1)
    Put X%,Y%,Blit$
  Next A%
  Put 0,0,Bar$
Return
'
Procedure Setup
  Rez%=Xbios(4)
  If Rez%=0
    Width%=319
    Height%=199
    Bar_height%=10
    Cols%=16
  Endif
  If Rez%=1
    Width%=639
    Height%=199
    Bar_height%=10
    Cols%=4
  Endif
  If Rez%=2
    Width%=639
    Height%=399
    Bar_height%=18
    Cols%=2
  Endif
  A$(0)="This GFA Basic V2 program was written by"
  A$(1)="Barrie Capel for ST Format  Magazine  to"
  A$(2)="to demonstrate the creation of a typical"
  A$(3)="menu driven application."+Chr$(10)+Chr$(13)
  A$(4)=Chr$(10)+Chr$(13)
  A$(5)="The listing can be run in any resolution"
  A$(6)="and a full explanation  found in the GFA"
  A$(7)="Programmers Surgery Column. "+Chr$(10)+Chr$(13)
  A$(8)=Chr$(10)+Chr$(13)
  A$(9)="Note that the items in Menus 1 and 2 are"
  A$(10)="dummies - insert  your own  menu  titles"
  A$(11)="and procedures for your own programs!"
Return
