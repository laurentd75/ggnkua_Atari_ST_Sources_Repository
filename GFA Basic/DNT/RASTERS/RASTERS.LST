.**********************************
.*       VERTICAL RASTERS         *
.*      par DENEB (FANTASY)       *
.*     pour le  DNT-PAPER #3      *
.**********************************
.
fre%=FRE(0)                     ! Save FRE(0)
xb%=XBIOS(2)                    ! Save XBIOS(2)
OUT 4,18                        ! TCHAC !!! cut the mouse
HIDEM                           ! PLOUF !!! hide the mouse
l%=898                          ! curve lenght
RESERVE fre%-(l%*160)-32256     ! Reserve memory
ac%=MALLOC(160*l%)              ! reserve curve buffer
ii1%=MALLOC(32256)              ! syncro screen
i1%=AND(ii1%,&HFFFF00)+256      ! /256
i2%=XBIOS(3)                    ! XBIOS(3) ... yes !!
ON BREAK GOSUB fin
CLS                             ! guess ...
.
FOR i%=0 TO 15                  ! color gradation
  READ col%
  SETCOLOR i%,col%
NEXT i%
DATA 0,$20,$30,$40,$50,$60,$200,$300
DATA $400,$500,$600,$2,$3,$4,$5,$6
.
FOR i%=0 TO 35                  ! paterns drawing
  READ c%
  PSET i%,0,c%
NEXT i%
DATA 6,6,7,7,8,8,9,9,10,10,9,9,8,8,7,7,6,6,11,11,12,12,13,13,14,14,15,15,14,14,13,13,12,12,11,11
.
nb%=15                          ! rasters number
PRINT AT(13,12);"PLEASE WAIT   /18"
.
FOR i%=0 TO l%
  BMOVE xb%+160,ac%+160*i%,160  ! buffer copy
  FOR ii%=0 TO nb%
    ADD q,0.2
    .
    x%=160+(40*SINQ(q))+(100*COSQ(q/8))*COSQ(q+ii%*8)
    RC_COPY xb%,0,0,17,1 TO ac%,x%,i%
    .
    x%=160+(40*COSQ(q/4))+(100*SINQ(q/4))*SINQ(q+ii%*6)
    RC_COPY xb%,18,0,17,1 TO ac%,x%,i%
  NEXT ii%
  IF i%-xi%=50
    PRINT AT(25,12);oo%
    INC oo%
    xi%=i%
  ENDIF
NEXT i%
.
go
fin
END
.
PROCEDURE fin
  ~MFREE(ac%)              ! Free !!!
  ~MFREE(ii1%)
  RESERVE fre%
  VOID XBIOS(5,L:xb%,L:xb%,-1)
  OUT 4,8
  SHOWM
  SETCOLOR 0,&H777
  SETCOLOR 15,0
  SETCOLOR 1,&H7
  SYSTEM
RETURN
.
PROCEDURE go
  REPEAT
    FOR i%=0 TO l%
      BMOVE ac%+MUL(i%,160),i1%,160
      .
      BMOVE i1%,i1%+160,160
      BMOVE i1%,i1%+320,320
      BMOVE i1%,i1%+640,640
      BMOVE i1%,i1%+1280,1280
      BMOVE i1%,i1%+2560,2560
      BMOVE i1%,i1%+5120,5120
      BMOVE i1%,i1%+10240,10240
      BMOVE i1%,i1%+20480,11520
      .
      VSYNC                             ! Syncro VBL
      VOID XBIOS(5,L:i1%,L:i2%,-1)
      SWAP i1%,i2%
      EXIT IF PEEK(&HFFFFC02)=&H39
    NEXT i%
  UNTIL PEEK(&HFFFFC02)=&H39
RETURN
.
.---  COMPIL THIS CODE !!!  ---
