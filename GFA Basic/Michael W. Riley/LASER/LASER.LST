' *****************************************************************************
' *             Atari ST LaserDisc Remote Control Program                     *
' *                      Version 1.00    08/18/89                             *
' *             Designed & Programmed by Michael W. Riley                     *
' *                       ½ 1989 Antic Publishing                             *
' *****************************************************************************
'
@Init_program
@Screen
Spoke &H484,Peek(&H484) And Not 1        ! Turn Off Key Click
Showm
'
On Menu Key Gosub Key_select
Do
  On Menu
  X%=Mousex
  Y%=Mousey
  K%=Mousek
  If K%=1
    If X%>14 And X%<29
      If Y%>50 And Y%<56
        Text 136,59,"  TV/LVP  "
        @Send_code("/0F")               ! TV/LaserDisc Player Signal Toggle
        @Press(15,51,28,55)
      Endif
      If Y%>64 And Y%<70
        Text 136,59,"  STEREO  "
        @Send_code("/4A")               ! Turn both Audio Channels 1 & 2 on
        @Press(15,65,28,69)
      Endif
      If Y%>81 And Y%<87
        Text 136,59,"  CLEAR   "
        @Send_code("/45")               ! Clears previously entered frame #'s
        @Press(15,82,28,86)
      Endif
      If Y%>94 And Y%<100
        Text 136,59,"MULTI-REV "
        @Send_code("/55")               ! Quickly scans backwards w/playback
        @Press(15,95,28,99)
      Endif
      If Y%>107 And Y%<113
        Text 136,59,"SLOW SPEED"
        @Send_code("/46")               ! Incrementally slows playback speed
        @Press(15,108,28,112)
      Endif
      If Y%>120 And Y%<126
        Text 136,59," REPEAT A "
        @Send_code("/48")               ! Sets beginning of repeat frame
        @Press(15,121,28,125)
      Endif
      If Y%>146 And Y%<152
        Text 136,59," CHAPTER  "
        @Send_code("/40")               ! Entered for chapter searches
        @Press(15,147,28,151)
      Endif
      If Y%>159 And Y%<165
        Text 136,59," REMOTE 1 "
        @Send_code("/01")               ! Numeral 1
        @Press(15,160,28,164)
      Endif
      If Y%>172 And Y%<178
        Text 136,59," REMOTE 5 "
        @Send_code("/05")               ! Numeral 5
        @Press(15,173,28,177)
      Endif
      If Y%>185 And Y%<191
        Text 136,59," REMOTE 9 "
        @Send_code("/09")                ! Numeral 9
        @Press(15,186,28,190)
      Endif
    Endif
    If X%>37 And X%<52
      If Y%>50 And Y%<56
        Text 136,59,"    CX    "
        @Send_code("/0E")               ! Toggles CX noise reduction
        @Press(38,51,51,55)
      Endif
      If Y%>81 And Y%<87
        Text 136,59,"  PAUSE   "
        @Send_code("/18")               ! Pauses playback (blanks screen)
        @Press(38,82,51,86)
      Endif
      If Y%>94 And Y%<100
        Text 136,59,"MULTI-FWD "
        @Send_code("/58")               ! Playback at set increased speed
        @Press(38,95,51,99)
      Endif
      If Y%>107 And Y%<113
        Text 136,59,"FAST SPEED"
        @Send_code("/47")               ! Incrementally increases playback
        @Press(38,108,51,112)
      Endif
      If Y%>120 And Y%<126
        Text 136,59," REPEAT B "
        @Send_code("/44")               ! Sets end of repeat frame
        @Press(38,121,51,125)
      Endif
      If Y%>146 And Y%<152
        Text 136,59,"  FRAME   "
        @Send_code("/41")               ! Entered for exact frame searches
        @Press(38,147,51,151)
      Endif
      If Y%>159 And Y%<165
        Text 136,59," REMOTE 2 "
        @Send_code("/02")               ! Numeral 2
        @Press(38,160,51,164)
      Endif
      If Y%>172 And Y%<178
        Text 136,59," REMOTE 6 "
        @Send_code("/06")               ! Numeral 6
        @Press(38,173,51,177)
      Endif
      If Y%>185 And Y%<191
        Text 136,59," REMOTE 0 "
        @Send_code("/00")               ! Numeral 0
        @Press(38,186,51,190)
      Endif
    Endif
    If X%>60 And X%<75
      If Y%>64 And Y%<70
        Text 136,59,"   1/L    "
        @Send_code("/4B")               ! Activate left audio channel only
        @Press(61,65,74,69)
      Endif
      If Y%>146 And Y%<152
        Text 136,59," PROGRAM  "
        @Send_code("/4C")               ! Intended for compact disc usage
        @Press(61,147,74,151)
      Endif
      If Y%>159 And Y%<165
        Text 136,59," REMOTE 3 "
        @Send_code("/03")               ! Numeral 3
        @Press(61,160,74,164)
      Endif
      If Y%>172 And Y%<178
        Text 136,59," REMOTE 7 "
        @Send_code("/07")               ! Numeral 7
        @Press(61,173,74,177)
      Endif
    Endif
    If X%>60 And X%<99
      If Y%>81 And Y%<87
        Play_flag!=-1
        Text 136,59,"   PLAY   "
        @Send_code("/17")               ! Commence playback
        @Press(61,82,98,86)
      Endif
      If Y%>185 And Y%<191
        Play_flag!=-1
        Text 136,59,"  SEARCH  "
        @Send_code("/42")               ! Activates search to desired frame #
        @Press(61,186,98,190)
      Endif
    Endif
    If X%>60 And X%<80
      If Y%>94 And Y%<100
        Text 136,59," SCAN REV "
        @Send_code("/11")               ! Quickly scans in reverse
        @Press(61,95,79,99)
      Endif
      If Y%>107 And Y%<113
        Text 136,59,"STILL REV "
        @Send_code("/50")               ! Freezes pictures (CAV discs only)
        @Press(61,108,79,112)
      Endif
      If Y%>120 And Y%<126
        Text 136,59," CHAP REV "
        @Send_code("/53")               ! Searches to previous chapter stop
        @Press(61,121,79,125)
      Endif
    Endif
    If X%>79 And X%<99
      If Y%>94 And Y%<100
        Text 136,59," SCAN FWD "
        @Send_code("/10")               ! Quickly scans forward
        @Press(80,95,98,99)
      Endif
      If Y%>107 And Y%<113
        Text 136,59,"STILL FWD "
        @Send_code("/54")               ! Increments 1 frame at a time (CAV)
        @Press(80,108,98,112)
      Endif
      If Y%>120 And Y%<126
        Text 136,59," CHAP FWD "
        @Send_code("/52")               ! Searches to next chapter stop
        @Press(80,121,98,125)
      Endif
    Endif
    If X%>84 And X%<99
      If Y%>50 And Y%<56
        If Play_flag!=-1
          Text 136,59,"   STOP   "
          @Send_code("/16")             ! Stops playback and spins down disc
          Play_flag!=0
        Else
          Text 136,59,"  EJECT   "
          @Send_code("/16")             ! Ejects disc from machine
        Endif
        @Press(85,51,98,55)
      Endif
      If Y%>64 And Y%<70
        Text 136,59,"   2/R    "
        @Send_code("/49")               ! Activates right audio channel only
        @Press(85,65,98,69)
      Endif
      If Y%>146 And Y%<152
        Text 136,59," DISPLAY  "
        @Send_code("/43")               ! Display current chapter/frame
        @Press(85,147,98,151)
      Endif
      If Y%>159 And Y%<165
        Text 136,59," REMOTE 4 "
        @Send_code("/04")               ! Numeral 4
        @Press(85,160,98,164)
      Endif
      If Y%>172 And Y%<178
        Text 136,59," REMOTE 8 "
        @Send_code("/08")               ! Numeral 8
        @Press(85,173,98,177)
      Endif
    Endif
    If X%>119 And X%<148                ! Below are repeated commands for
      If Y%>91 And Y%<103               ! clicking on the Function Key graphics
        Play_flag!=-1
        Text 136,59,"   PLAY   "
        @Send_code("/17")
        @Click
      Endif
      If Y%>108 And Y%<120
        Text 136,59,"  FRAME   "
        @Send_code("/41")
        @Click
      Endif
      If Y%>125 And Y%<137
        Play_flag!=-1
        Text 136,59,"  SEARCH  "
        @Send_code("/42")
        @Click
      Endif
      If Y%>142 And Y%<154
        Text 136,59,"  CLEAR   "
        @Send_code("/45")
        @Click
      Endif
      If Y%>159 And Y%<171
        Text 136,59," DISPLAY  "
        @Send_code("/43")
        @Click
      Endif
    Endif
    If X%>212 And X%<241
      If Y%>91 And Y%<103
        Text 136,59,"STILL FWD "
        @Send_code("/54")
        @Click
      Endif
      If Y%>108 And Y%<120
        Text 136,59,"STILL REV "
        @Send_code("/50")
        @Click
      Endif
      If Y%>125 And Y%<137
        Text 136,59," CHAP FWD "
        @Send_code("/52")
        @Click
      Endif
      If Y%>142 And Y%<154
        Text 136,59," CHAP REV "
        @Send_code("/53")
        @Click
      Endif
      If Y%>159 And Y%<171
        Text 136,59,"EJECT DISC"
        @Send_code("/16")
        @Send_code("/16")
        @Click
      Endif
    Endif
    If X%>272 And Y%>48 And X%<297 And Y%<69
      @Click
      Alert 2,"| Are you sure you wish | to exit the program?",1," YES | NO ",An%
      If An%=1
        @Palette_restore
      Endif
      Defmouse 3
    Endif
    Repeat
    Until Mousek<>1
  Endif
Loop
'
Procedure Key_select                   ! Allows keyboard input by scanning
  If Menu(14)=15104      !F1           ! the decimal value from Menu(14).
    Play_flag!=-1
    Command$="   PLAY   "
    @Send_code("/17")
    @Click
  Endif
  If Menu(14)=15360      !F2
    Command$="  FRAME   "
    @Send_code("/41")
    @Click
  Endif
  If Menu(14)=15616      !F3
    Play_flag!=-1
    Command$="  SEARCH  "
    @Send_code("/42")
    @Click
  Endif
  If Menu(14)=15872      !F4
    Command$="  CLEAR   "
    @Send_code("/45")
    @Click
  Endif
  If Menu(14)=16128      !F5
    Command$=" DISPLAY  "
    @Send_code("/43")
    @Click
  Endif
  If Menu(14)=16384      !F6
    Command$="STILL FWD "
    @Send_code("/54")
    @Click
  Endif
  If Menu(14)=16640      !F7
    Command$="STILL REV "
    @Send_code("/50")
    @Click
  Endif
  If Menu(14)=16896      !F8
    Command$=" CHAP FWD "
    @Send_code("/52")
    @Click
  Endif
  If Menu(14)=17152      !F9
    Command$=" CHAP REV "
    @Send_code("/53")
    @Click
  Endif
  If Menu(14)=17408      !F10
    Command$="EJECT DISC"
    @Send_code("/16")
    @Send_code("/16")
    @Click
  Endif
  If Menu(14)=27953
    Command$=" KEYPAD 1 "
    @Send_code("/01")
    @Click
  Endif
  If Menu(14)=28210
    Command$=" KEYPAD 2 "
    @Send_code("/02")
    @Click
  Endif
  If Menu(14)=28467
    Command$=" KEYPAD 3 "
    @Send_code("/03")
    @Click
  Endif
  If Menu(14)=27188
    Command$=" KEYPAD 4 "
    @Send_code("/04")
    @Click
  Endif
  If Menu(14)=27445
    Command$=" KEYPAD 5 "
    @Send_code("/05")
    @Click
  Endif
  If Menu(14)=27702
    Command$=" KEYPAD 6 "
    @Send_code("/06")
    @Click
  Endif
  If Menu(14)=26423
    Command$=" KEYPAD 7 "
    @Send_code("/07")
    @Click
  Endif
  If Menu(14)=26680
    Command$=" KEYPAD 8 "
    @Send_code("/08")
    @Click
  Endif
  If Menu(14)=26937
    Command$=" KEYPAD 9 "
    @Send_code("/09")
    @Click
  Endif
  If Menu(14)=28720
    Command$=" KEYPAD 0 "
    @Send_code("/00")
    @Click
  Endif
  If Menu(14)=283        !Esc
    @Click
    Alert 2,"| Are you sure you wish | to exit the program?",1," YES | NO ",An%
    If An%=1
      @Palette_restore
    Endif
    Defmouse 3
  Endif
  If Command$=""
    Command$="          "
  Endif
  Text 136,59,Command$
  Clr Command$
  Repeat
    Pause 2
  Until Inkey$=""
Return
'
Procedure Init_program
  Hidem
  If Xbios(4)<>0
    Alert 1,"| The Atari ST LaserDisc Remote | Control Program  only runs in |    low resolution mode.",1," SORRY ",Void!
    End
  Endif
  Current_dir$=Dir$(0)+"\"
  If Not Exist(Current_dir$+"LASER.PI1")
    Alert 1,"| Cannot find LASER.PI1 file. | Make sure that file is in the| same directory as LASER.PRG.",1," BYE ",Void!
    End
  Endif
  '
  '   Configure RS-232C Port for 4800 baud, no parity, 1 stop bit, 8 bits
  '
  Void Xbios(15,2,0,&X10001000,-1,-1,-1)
  '
  @Send_code(">A8")                     ! "SR" Code for LaserDisc Player
  @Palette_save
Return
'
Procedure Palette_save
  Dim Palette%(15)
  Num%=0
  Repeat
    Palette%(Num%)=Xbios(7,W:Num%,W:-1)
    Inc Num%
  Until Num%>15
  Clr Num%
Return
'
Procedure Screen
  Local N,X,Y,Z,C
  Filename$=Current_dir$+"LASER.PI1"
  Open "I",#1,Filename$
  Seek #1,2                          ! Move to first DEGAS color byte.
  For N=0 To 15                      ! (First 2 bytes are DEGAS resolution info)
    X=Inp(#1)
    Y=Inp(#1)
    Z=X*256+Y
    C=Xbios(7,N,Z)
  Next N
  Bload Filename$,Xbios(2)-34
  Close #1
Return
'
Procedure Press(X1%,Y1%,X2%,Y2%)        ! Graphic bar for remote w/beep
  Get X1%,Y1%,X2%,Y2%,Temp$
  Pbox X1%,Y1%,X2%,Y2%
  Sound 1,13,1,6,5
  Sound 1,0,0,0,0
  Put X1%,Y1%,Temp$
  Clr Temp$
Return
'
Procedure Click                         ! Sound caused during a keypress
  Sound 0,0,2500,0,0
  Wave 8,1,9,2100
Return
'
Procedure Send_code(Code$)              ! Sends the code via the RS-232 port
  Open "",#1,"AUX:"
  Print #1,Code$
  Close #1
  Clr Code$
Return
'
Procedure Palette_restore
  Cls
  Num%=0
  Repeat
    Setcolor Num%,Palette%(Num%)
    Inc Num%
  Until Num%>15
  Spoke &H484,Peek(&H484) Or 1         ! Turn key click back on
  End
Return
