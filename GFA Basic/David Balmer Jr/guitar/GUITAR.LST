'
'
'                            Guitar Solo
'                        Written in GFA Basic
'                         By David Balmer Jr.
'                  Copyright 1988 Antic Publishing
'
'
'
Dim Tune(6),Freq(100),Lx(12),Ly(12),Mx(12),My(12),Help$(12),Alt$(12),Spalette%(16,3)
Instrument=1
Mlen=2000
Dim Rx(Mlen),Ry(Mlen),Rb(Mlen)
'
Let Resolution%=Xbios(4)
Let Hi%=2
Let Med%=1
'
If Resolution%=Hi%
  Guitar.pic$="GUITAR.PI3"
Else
  If Resolution%=Med%
    Guitar.pic$="GUITAR.PI2"
  Else
    Alert 1,"Guitar solo must run in medium or high res.",1,"Okay",Dummy
    End
  Endif
Endif
'
@Save_pal
'
Let Fill$=String$(16,Mki$(0))
Pbox 0,0,639,399
Feedback=0
If Resolution%=Hi%
  Get 0,0,4,4,Spot$
  Get 0,0,54,6,Light$
Else
  Get 0,0,4,4/2,Spot$
  Get 0,0,54,6/2,Light$
Endif
Deftext 1,0,0,13
Text 200,100,"Loading Guitar Solo Data ..."
'
Gosub Loadnotes
'
Dim N$(12)
Restore Notes_string
For T=1 To 12
  Read N$(T)
Next T
Flanger=0
F=5
Vibrato=0
'
Gosub Read_help
'
Notes_string:
Data "C ","C#","D ","D#","E ","F ","F#","G ","G#","A ","A#","B "
Notes_numbers:
Data 4,5,3,12,3,8,3,3,2,10,2,5
'
Volume=15
V=Volume
S=0.05
'
Gosub Degas(Guitar.pic$)
'
If Resolution%=Hi%
  Get 104,22,176,38,Level$
  Get 130,168,482,264,Neck$
Else
  Get 104,22/2,176,38/2,Level$
  Get 130,168/2,482,264/2,Neck$
Endif
Dim O(6),N(6),So(6),Sn(6)
Restore Notes_numbers
For T=1 To 6
  Read So(T),Sn(T)
Next T
Gosub Tuneup
'
'
'
'
'
Repeat
  Lbutton=Button
  If Playback=0
    Mouse X,Y,Button
    If Resolution%=Med%
      Y=Y*2
    Endif
    '
  Else
    Pn=Pn+1
    If Pn<Length
      X=Rx(Pn)
      Y=Ry(Pn)
      Button=Rb(Pn)
    Else
      Playback=0
      Py=56
      Gosub Light
    Endif
  Endif
  If Record=1
    Length=Length+1
    If Length<Mlen
      If X<308 Or X>368
        If Y<40 Or Y>118
          Rx(Length)=X
          Ry(Length)=Y
          Rb(Length)=Button
        Endif
      Endif
    Else
      Gosub No_notes
      Record=0
      Py=40
      Gosub Light
    Endif
  Endif
  '
  If Help=0
    If X>130 And X<482 And Y>168 And Y<264
      Gosub Play
    Endif
    '
    If X>104 And X<179 And Y>22 And Y<120 And Button=1
      Gosub Levels
    Endif
    '
    If Button=1 And Lbutton=0
      If X>572 And X<594 And Y>6 And Y<38
        Repeat
        Until Mousek=0
        Pause 10
        Help=1
        A$="'Help Mode' is now ON"
        Vsync
        Sget Screen$
        Deffill 1,2,4
        Graphmode 4
        Pbox 0,0,639,399
        Graphmode 1
        Alert 0,A$,1,"OK",B
        Ms=3
        Sput Screen$
      Endif
      '
      If X>532 And X<556 And Y>8 And Y<36
        Gosub Shownotes
      Endif
      '
      If X>484 And Y>4 And X<516 And Y<36
        Gosub Exiter
      Endif
      If X>186 And X<258 And Y>38 And Y<120
        Gosub Settings
      Endif
      '
      If X>86 And X<104 And Y>24 And Y<120
        Gosub Levon
      Endif
      '
      If X>610 And X<633 And Y>6 And Y<37
        Gosub Whodunnit
      Endif
      '
      If X>308 And X<368 And Y>40 And Y<120
        Gosub Recorder
      Endif
      '
      If X>72 And X<120 And Y>168 And Y<264
        Gosub Tuner
      Endif
      '
    Endif
    '
  Else
    If Help=1 And Button=1 And Lbutton=0
      Gosub Helper
    Endif
  Endif
  '
  '
Until Xit=1
'
'
'
Procedure Play
  If X>130 And Y>168 And X<482 And Y<264
    If Button<>0
      Mx=X-130+16
      My=Y-168
      String=Int(My/16)+1
      Let Addfreq=0
      Octave=O(String)
      If Button=1
        N=Int(Mx/16)+N(String)
      Endif
      If Button=2 Or Button=3
        N=N(String)
      Endif
      Repeat
        If N>12
          Octave=Octave+1
          N=N-12
        Endif
      Until N=<12
    Endif
    If Lbutton=0 And Button<>0
      Ff=0
      V=Volume
    Endif
    If Tap=1 And Ln<>N Or Lo<>O
      Ff=0
      V=Volume
    Endif
    If So=0 And Button=0
      Ff=0
      V=0
      Fb=0
    Endif
    Gosub Sound
    Ln=N
    Lo=O
    V=V-S
  Endif
Return
'
'
Procedure Sound
  If Vibrato=1
    Vb=Vb+4
    If Vb>10
      Vb=Vb-21
    Endif
    Let Addfreq=Addfreq+Abs(Vb)
  Endif
  If Flanger=1
    Ff=Ff+1
    If Ff=F Or Ff=F+1
      Sound 1,1,1,1
      Ff=0
    Else
      Sound 1,V,#Value(Octave,N)+Addfreq,1
    Endif
  Else
    Sound 1,V,#Value(Octave,N)+Addfreq,1
  Endif
  If V<0
    Sound 1,1,1,1
    V=0
  Endif
Return
'
'
Procedure Showtuning
  For T=1 To 6
    Gosub Showtune
  Next T
Return
'
Procedure Showtune
  Color 1
  Deftext 1,0,0,4
  If Resolution%=Hi%
    Text 91,String*16+162,N$(N(String))
  Else
    Text 91,(String*16+162)/2,N$(N(String))
  Endif
Return
'
'
Procedure Load_song
  Sget Screen$
  Graphmode 4
  Deffill 1,2,4
  Pbox 0,0,639,399
  Deftext 1,0,0,32
  Graphmode 1
  Text 0,30,"LOAD SONG"
  Fileselect "\*.SNG",Song$,Nm$
  If Nm$<>"" And Exist(Nm$)<>0
    Open "I",#1,Nm$
    Input #1,Length
    For T=1 To Length
      Input #1,Rx(T)
      Input #1,Ry(T)
      Input #1,Rb(T)
    Next T
  Endif
  Song$=Nm$
  Sput Screen$
  Close
Return
'
Procedure Save_song
  Sget Screen$
  Graphmode 4
  Deffill 1,2,4
  Pbox 0,0,639,399
  Deftext 1,0,0,32
  Graphmode 1
  Text 0,30,"SAVE SONG"
  Fileselect "\*.SNG",Song$,Nm$
  If Nm$<>""
    Open "O",#1,Nm$
    Print #1,Length
    For T=1 To Length
      Print #1,Rx(T)
      Print #1,Ry(T)
      Print #1,Rb(T)
    Next T
    Length=0
  Endif
  Sput Screen$
  Close
Return
'
'
'
Procedure Exiter
  Repeat
  Until Mousek=0
  Pause 10
  A$="Are you sure you want|to leave this GREAT|program?"
  Alert 0,A$,2,"Yep.|NO! WAIT!",B
  If B=1
    @Restorepal
    Edit
  Endif
  A$="I didn't think so!"
  Alert 0,A$,1,"Ok, ok.",B
Return
'
'
'
Procedure Tuner
  String=Int((Y-168)/16)+1
  If X<91
    N(String)=N(String)-1
    If N(String)<1
      O(String)=O(String)-1
      N(String)=12
    Endif
  Endif
  If X>103
    N(String)=N(String)+1
    If N(String)>12
      N(String)=1
      O(String)=O(String)+1
    Endif
  Endif
  Gosub Showtune
  While Button<>0
    Mouse X,Y,Button
    If Resolution%=Med%
      Y=Y*2
    Endif
    Sound 1,15,N(String),O(String),1
  Wend
  Sound 1,1,1,1
Return
'
'
'
Procedure Recorder
  Local Py,Lp
  Y=Y+2
  If Y>108
    Py=108
    Gosub Light
    Gosub Tuneup
    Py=108
    Gosub Light
  Endif
  If Y<56
    Record=Abs(Record-1)
    Py=40
    Gosub Light
    If Record=1
      Length=0
    Endif
  Endif
  If Y>56 And Y<76
    If Record=1
      Py=40
      Gosub Light
    Endif
    Playback=1
    Pn=0
    Record=0
    Py=56
    Gosub Light
  Endif
  If Y>76 And Y<89
    Gosub Save_song
  Endif
  If Y>90 And Y<108
    Gosub Load_song
  Endif
Return
'
Procedure Light
  If Resolution%=Med%
    Py=Py/2
    Put 311,Py,Light$,10
    Put 311,Py+4,Light$,10
  Else
    Put 311,Py+3,Light$,10
  Endif
Return
'
'
Procedure Tuneup
  For T=1 To 6
    O(T)=So(T)
    N(T)=Sn(T)
    String=T
    Gosub Showtune
  Next T
Return
'
'
'
Procedure Loadnotes
  Dim Value(10,13)
  Open "i",#1,"notefreq.dat"
  For Octave=1 To 8
    For N=1 To 12
      Input #1,Value(Octave,N)
    Next N
    Value(Octave,0)=Value(Octave-1,12)
  Next Octave
  Close #1
Return
'
'
'
Procedure Shownotes
  Sn=Abs(Sn-1)
  If Sn=1
    For String=1 To 6
      N=N(String)+1
      For P=1 To 22
        If N>12
          N=1
        Endif
        Deftext 1,0,0,4
        If Resolution%=Hi%
          Text P*16+117,String*16+166,N$(N)
        Else
          Text P*16+117,(String*16+166)/2,N$(N)
        Endif
        N=N+1
      Next P
    Next String
  Else
    If Resolution%=Hi%
      Put 130,168,Neck$
    Else
      Put 130,168/2,Neck$
    Endif
  Endif
Return
'
'
'
Procedure Helper
  Local Lx,Ly,Mx,My
  If Resolution%=Med%
    Y=Y/2 ! Change value back because "Hot box" values are already adjusted
  Endif
  For T=1 To 12
    If X>Lx(T) And X<Mx(T) And Y>Ly(T) And Y<My(T)
      Repeat
      Until Mousek=0
      Pause 10
      If T=3
        Help=0
      Endif
      Sget Screen$
      Get Lx(T),Ly(T),Mx(T),My(T),Hbox$
      Graphmode 4
      Deffill 1,2,4
      Pbox 0,0,639,399
      Put Lx(T),Ly(T),Hbox$
      Graphmode 1
      A$=Help$(T)
      Alert 0,A$,1,Alt$(T),B
      Sput Screen$
      If B>1
        Sget Screen$
        Deffill 1,2,4
        Graphmode 4
        Pbox 0,0,639,399
        Graphmode 1
        A$="Are you certain that|is a better title ?|"
        Vsync
        Alert 0,A$,1,"SURE AM!|Nah",B
        If B=1
          Logo=1
          Sput Screen$
          Gosub Change_name
          Sget Screen$
          Help$(T)="DON'T LOOK AT ME!|That's the way YOU|wanted it to be!|"
          Alt$(T)="OK-OK!"
        Endif
        Sput Screen$
      Endif
      T=14
    Endif
  Next T
  If T=13
    Repeat
    Until Mousek=0
    Pause 10
    Sget Screen$
    Deffill 1,2,4
    Graphmode 4
    Pbox 0,0,639,399
    Graphmode 1
    A$="Beats me.  Even I don't|know EVERYTHING!|"
    Vsync
    Alert 0,A$,1,"Shrug",B
    Sput Screen$
  Endif
Return
'
'
'
Procedure Change_name
  If Resolution%=Hi%
    Get 4,324,276,396,Logo$
    Deffill 1,Fill$
    Pbox 4,324,276,396
    Line 4,365,276,365
    Deftext 1,0,0,6
    Text 18,337,"  Electric Guitar Simulation"
    Text 18,347,"With Metaphoric User Interface"
    Text 18,357,"      Version 1.6776213"
    Text 18,379,"     CHANGED BY A WEIRDO"
    Text 18,389,"WHO DIDN'T LIKE THE OLD TITLE"
  Else
    Get 4,324/2,276,396/2,Logo$
    Deffill 1,Fill$
    Pbox 4,324/2,276,396/2
    Line 4,365/2,276,365/2
    Deftext 1,0,0,4
    Text 18,171,"        Electric Guitar Simulation"
    Text 18,177,"      With Metaphoric User Interface"
    Text 18,188,"           CHANGED BY A WEIRDO "
    Text 18,196,"      WHO DIDN'T LIKE THE OLD TITLE"
  Endif
Return
'
'
'
Procedure Read_help
  Restore Help_data
  For T=1 To 12
    Read Lx(T),Ly(T),Mx(T),My(T),Help$(T),Alt$(T)
    If Resolution%=Med%
      Ly(T)=Ly(T)/2
      My(T)=My(T)/2
    Endif
  Next T
  '
  Help_data:
  Data 540,164,638,268,"'Pickups' elctronic coils to| 'hear' the strings played.|","GOT IT"
  Data 530,6,556,36,"'Notes' displays notes| on the neck of the|       guitar. |","OKAY"
  Data 572,6,594,38,"Help Mode now OFF|","NOTED"
  Data 610,6,632,38,"'Whodunnit'|","OK..."
  Data 288,4,388,140,"'Recorder' allows recording| and playback + save and load.|"TUNER" auto-tunes guitar.|","GOTCHA"
  Data 186,22,258,120,"'Settings' guitar 'personality'| mess around, see what they do|","SURE!"
  Data 24,22,176,120,"'Amp' change volume, sustain,| Flanger (echo)|","OK..."
  Data 72,168,120,264,"'Tuning' tune each string| up or down 1/2 note|","ROGER"
  Data 128,168,482,264,"'Guitar Neck' top string is| highest pitch, bottom is low|","WOW!"
  Data 4,324,276,396,"Programs have to have a name!|Isn't it better than 'Electric|Guitar Simulation with Meta-|phoric User Interface' ?|","YUP|NO WAY"
  Data 506,316,636,340,"I think it says|'sratiuG redneB' ...|","I'm Hip"
  Data 484,4,516,36,"In case you want to|leave this program.|","Noted."
  '
Return
'
'
'
Procedure Settings
  Py=0
  If Y>38 And Y<54 And Tap=1
    Py=58
    Gosub Spotter(194)
    Tap=0
    Py=46
  Endif
  If Y>54 And Y<68 And Tap=0
    Tap=1
    Py=46
    Gosub Spotter(194)
    Py=58
  Endif
  If Y>70 And Y<84
    Py=76
  Endif
  If Y>84 And Y<98
    Py=88
  Endif
  If Y>100 And Y<120
    Vibrato=Abs(Vibrato-1)
    Py=107
  Endif
  '
  If Py<>0
    Gosub Spotter(194)
  Endif
Return
'
Procedure Spotter(X)
  If Resolution%=Med%
    Py=Py/2
  Endif
  '
  Put X,Py,Spot$,10
Return
'
'
'
Procedure Levon
  Py=0
  If Y>48 And Y<64
    So=Abs(So-1)
    Py=54
  Endif
  '
  If Y>76 And Y<92
    Py=82
  Endif
  '
  If Y>104 And Y<120
    Flanger=Abs(Flanger-1)
    Py=110
  Endif
  '
  If Py<>0
    Gosub Spotter(94)
  Endif
Return
'
'
'
Procedure Levels
  Hidem
  Lx=0
  Ly=0
  Repeat
    Mouse X,Y,Button
    If Resolution%=Med%
      Y=Y*2
    Endif
    If X>104 And X<176 And Y<120 And Y>24
      If X<>Lx Or Y<>Ly
        Deffill 1,2,8
        Py=Int((Y-22)/26)
        Ay=0
        If Py>1
          Ay=(Py-1)*2
        Endif
        Deffill 1,Fill$
        If Resolution%=Hi%
          Pbox 104,Py*26+22+Ay,176,Py*26+22+16+Ay
          Deffill 1,2,8
          Pbox 104,Py*26+22+Ay,X,Py*26+22+16+Ay
          Put 104,Py*26+22+Ay,Level$,1
        Else
          Pbox 104,(Py*26+22+Ay)/2,176,(Py*26+22+16+Ay)/2
          Deffill 1,2,8
          Pbox 104,(Py*26+22+Ay)/2,X,(Py*26+22+16+Ay)/2
          Put 104,(Py*26+22+Ay)/2,Level$,1
        Endif
      Endif
      Lx=X
      Ly=Y
    Endif
  Until Button=0
  If Py=1
    S=Abs((X-104)-75)*0.0133333
  Endif
  If Py=3
    F=Int(Abs((X-104)-75)/5)+1
  Endif
  If Py=0
    Volume=Int((X-104)/5)+1
  Endif
  Showm
Return
'
'
'
Procedure Whodunnit
  Repeat
  Until Mousek=0
  Pause 10
  Sget Screen$
  Graphmode 4
  Deffill 1,2,4
  Pbox 0,0,639,399
  Graphmode 1
  A$="       GUITAR  SOLO|Written by David Balmer Jr.|  "+Chr$(189)+" 1988 Antic Publishing"
  Vsync
  Alert 0,A$,1,"Noted",B
  Sput Screen$
Return
'
'
Procedure No_notes
  Repeat
  Until Mousek=0
  Pause 10
  A$="You are out of recording|room.  Recommend you SAVE it.|"
  Alert 1,A$,1,"shucks.",B
Return
'
' --------------------------- LOAD DEGAS TYPE PIC ---------------------
Procedure Degas(Filename$)
  Open "I",#1,Filename$
  Temp$=Input$(36,#1)
  Colr$=Mid$(Temp$,3,36)
  Close #1
  Void Xbios(6,L:Varptr(Colr$))
  Physbase=Xbios(2)
  Bload Filename$,Physbase-34
  Clr Temp$
Return
'
' ------------- SAVE ORIGINAL COLOR PALETTE -----------------------
Procedure Save_pal
  '
  ' Requires Dim Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,26
    Dpoke Contrl+2,0
    Dpoke Contrl+6,2
    Dpoke Intin,Z%
    Dpoke Intin+2,0
    Vdisys
    Spalette%(Z%,0)=Dpeek(Intout+2)
    Spalette%(Z%,1)=Dpeek(Intout+4)
    Spalette%(Z%,2)=Dpeek(Intout+6)
  Next Z%
Return
'
Procedure Restorepal
  ' --------------------- RESTORES PALLET -------------------
  ' Dimensions: Spalette%(16,3)
  '
  For Z%=0 To 15
    Dpoke Contrl,14
    Dpoke Contrl+2,0
    Dpoke Contrl+6,4
    Dpoke Intin,Z%
    Dpoke Intin+2,Spalette%(Z%,0)
    Dpoke Intin+4,Spalette%(Z%,1)
    Dpoke Intin+6,Spalette%(Z%,2)
    Vdisys
  Next Z%
Return
