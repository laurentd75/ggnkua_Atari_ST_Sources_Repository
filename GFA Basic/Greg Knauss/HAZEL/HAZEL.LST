' ******************************************************************************
' *                                                                            *
' *                                 H.A.Z.E.L.                                 *
' *     A Selective Loader for AUTO, ASSIGN.SYS, DA and DESKTOP.INF Files      *
' *                                                                            *
' *                    By Greg "Paranoid Jellyfish" Knauss                     *
' *                                                                            *
' ******************************************************************************
'
' H.A.Z.E.L. is in the public domain and may be freely distributed.  She always
' was sort of a tramp...
'
' Only do the program if they're holding down the left SHIFT key...
'
If (Bios(11,-1) And 2)=2 Then
  '
  ' Get the resolution
  '
  Old_res%=Xbios(4)
  Res%=Old_res%
  '
  ' If they're in low, move it to medium
  '
  If Res%=0 Then
    Void Xbios(5,L:-1,L:-1,1)
    Res%=1
    '
    ' And set color 3 to black
    '
    Setcolor 3,0
  Endif
  '
  ' Define escape
  '
  Esc$=Chr$(27)
  '
  ' Print credits
  '
  If Res%=1 Then
    '
    ' Medium res
    '
    Print Esc$;"b1            H.A.Z.E.L.";Esc$;"b3 AUTO, ASSIGN.SYS, DA and DESKTOP.INF Chooser!"
  Else
    '
    ' High res
    '
    Print "            ";Esc$;"c1";Esc$;"b0H.A.Z.E.L.";Esc$;"c0";Esc$;"b1 AUTO, ASSIGN.SYS, DA and DESKTOP.INF Chooser!"
  Endif
  Print "                            By Greg ";Chr$(34);"Maddog";Chr$(34);" Knauss"
  Print "                          Copyright 1989  Magnum Opus"
  Print
  '
  ' Dims and defines
  '
  Dim Filename$(136)
  Dim File_len%(136)
  Dim Short_name$(136)
  Dim Ext$(4)
  Dim Type%(136)
  Dim Orig_stat!(136)
  Dim Stat!(136)
  '
  ' Some stuff
  '
  North$=Chr$(0)+"H"
  South$=Chr$(0)+"P"
  East$=Chr$(0)+"M"
  West$=Chr$(0)+"K"
  '
  Auto_choose:
  '
  ' Legal AUTO extensions
  '
  Ext$(1)="PRG"
  Ext$(2)="APP"
  Ext$(3)="TOS"
  Ext$(4)="TTP"
  '
  ' Assume that nothing's there
  '
  Autos!=False
  Gdos!=False
  Accs!=False
  '
  ' Is there an AUTO folder?
  '
  If Exist("\AUTO\*.*") Then
    '
    ' **************************************************************************
    ' Do selects for AUTO programs
    ' **************************************************************************
    '
    ' Load directory into Filename$
    '
    Gosub Get_dir("\AUTO\*.*")
    '
    ' Sort out all non-PR?, non-AP?, non-TO?, non-TT? files
    '
    J%=0
    Repeat
      Temp%=Instr(Filename$(J%),".")
      Temp$=Mid$(Filename$(J%),Temp%+1,2)
      '
      ' It's not an executable file, so remove it from the list
      '
      If Temp$<>"PR" And Temp$<>"AP" And Temp$<>"TO" And Temp$<>"TT" Then
        For I%=J% To Num_of_files%
          Filename$(I%)=Filename$(I%+1)
        Next I%
        Dec Num_of_files%
      Else
        '
        ' We save it
        '
        Short_name$(J%)=Left$(Filename$(J%),Temp%-1)
        Type%(J%)=-(Temp$="PR")-2*(Temp$="AP")-3*(Temp$="TO")-4*(Temp$="TT")
        '
        ' It's alive!  (Or "active" if you wanna get picky.)
        '
        If Ext$(Type%(J%))=Right$(Filename$(J%),3) Then
          Stat!(J%)=True
          Orig_stat!(J%)=True
        Else
          '
          ' It's dead...  He was such a good file...
          '
          Stat!(J%)=False
          Orig_stat!(J%)=False
        Endif
        Inc J%
      Endif
      '
      ' Done yet?
      '
    Until J%=Num_of_files%+1
    '
    ' Are there any files for us to fiddle with?
    '
    If Num_of_files%>-1 Then
      '
      ' Yep!  Do choosing for AUTO programs
      '
      Autos!=True
      Pass%=1
      Cls
      Gosub Select
      Cls
      '
      ' Run through all the files for AUTO selection
      '
      For I%=0 To Num_of_files%
        '
        ' If'n they got GDOS on, remember it...
        '
        If Stat!(I%)=True And Left$(Filename$(I%),4)="GDOS" Then
          Gdos!=True
        Endif
        '
        ' Only change the name if it's status has been changed
        '
        If Stat!(I%)<>Orig_stat!(I%) Then
          Temp$="\AUTO\"+Short_name$(I%)+"."+Left$(Ext$(Type%(I%)),2-Stat!(I%))
          Name "\AUTO\"+Filename$(I%) As Temp$
        Endif
      Next I%
      '
      ' Do they have GDOS on?
      '
      If Gdos! Then
        '
        ' **********************************************************************
        ' Do selects for ASSIGN.SYS file
        ' **********************************************************************
        '
        Assign_choose:
        '
        ' Yep, any SYSs in HAZEL?
        '
        If Exist("\AUTO\HAZEL\*.SYS") Then
          '
          ' Get the directory
          '
          Gosub Get_dir("\AUTO\HAZEL\*.SYS")
          '
          ' Do the choosing for ASSIGN.SYS
          '
          Pass%=2
          Gosub Assign_desktop("\ASSIGN.SYS")
          '
          ' Back up
          '
          If Pass%=-1 Then
            Goto Auto_choose
          Endif
          '
        Else
          '
          ' No files in ASSIGNS directory...
          '
          Print "No ASSIGN.SYS files..."
          Pause (50)
        Endif
      Endif
    Else
      '
      ' No executable AUTO files...
      '
      Print "No executable AUTO files..."
      Pause (50)
    Endif
  Else
    '
    ' No AUTO folder...
    '
    Print "No AUTO files..."
    Pause (50)
  Endif
  '
  ' ****************************************************************************
  ' Do selects for DAs
  ' ****************************************************************************
  '
  Da_choose:
  '
  If Exist("\*.AC?") Then
    '
    ' Load in main boot drive directory *.AC? files
    '
    Gosub Get_dir("\*.AC?")
    '
    ' Loop through all filenames
    '
    For I%=0 To Num_of_files%
      Temp%=Instr(Filename$(I%),".")
      Short_name$(I%)=Left$(Filename$(I%),Temp%-1)
      '
      ' It lives!  It lives!
      '
      If Right$(Filename$(I%),3)="ACC" Then
        Stat!(I%)=True
        Orig_stat!(I%)=True
      Else
        '
        ' No, it doesn't!  No, it doesn't!
        '
        Stat!(I%)=False
        Orig_stat!(I%)=False
      Endif
      '
      ' Move to next one
      '
    Next I%
    '
    ' Do choosing for DAs
    '
    Accs!=True
    Pass%=3
    Cls
    Gosub Select
    Cls
    '
    ' Back up!
    '
    If Pass%=-1 Then
      '
      ' ...to ASSIGN.SYS choose
      '
      If Gdos!=True Then
        Goto Assign_choose
      Else
        '
        ' ...to AUTO choose
        '
        If Autos!=True Then
          Goto Auto_choose
        Else
          '
          ' ...to DA 'cause there ain't either of the previous
          '
          Goto Da_choose
        Endif
      Endif
    Endif
    '
    ' Make mods
    '
    For I%=0 To Num_of_files%
      '
      ' Only change the name if it's status has been changed
      '
      If Stat!(I%)<>Orig_stat!(I%) Then
        Temp$="\"+Short_name$(I%)+"."+Left$("ACC",2-Stat!(I%))
        Name "\"+Filename$(I%) As Temp$
      Endif
    Next I%
  Else
    '
    ' No desk accessories...
    '
    Print "No desk accessories..."
    Pause (50)
  Endif
  '
  ' ****************************************************************************
  ' Do selects for DESKTOP.INF file
  ' ****************************************************************************
  '
  ' Any DESKTOPs?
  '
  If Exist("\AUTO\HAZEL\*.INF") Then
    '
    ' Get a directory
    '
    Gosub Get_dir("\AUTO\HAZEL\*.INF")
    '
    ' Do the select
    '
    Pass%=4
    Gosub Assign_desktop("\DESKTOP.INF")
    '
    ' Back up!
    '
    If Pass%=-1 Then
      '
      ' ...to DA choose
      '
      If Accs!=True Then
        Goto Da_choose
      Else
        '
        ' ...to ASSIGN.SYS choose
        '
        If Gdos!=True Then
          Goto Assign_choose
        Else
          '
          ' ...to AUTO choose
          '
          If Autos!=True Then
            Goto Auto_choose
          Else
            '
            ' ...to DESKTOP choose
            '
            Goto Desktop_choose
          Endif
        Endif
      Endif
    Endif
  Else
    '
    ' No DESKTOP files...
    '
    Print "No DESKTOP.INF files..."
    Pause (50)
  Endif
  '
  ' If they started in low res...
  '
  If Old_res%=0 Then
    '
    ' Restore stuff
    '
    Void Xbios(5,L:-1,L:-1,Old_res%)
    '
    ' And other stuff
    '
    Setcolor 3,5,0,0
  Endif
Else
  '
  ' Let 'em know what they missed
  '
  Print "HAZEL bypassed. Left SHIFT on boot runs."
Endif
'
' We're outta here!  Back to the boot-up! **************************************
'
Edit
'
' ******************************************************************************
' Let's user select files he wants active
' ******************************************************************************
'
Procedure Select
  '
  ' Show the title
  '
  Gosub Title
  '
  ' Set up program selections screen
  '
  X%=-1
  Y%=7
  '
  ' Go through all files
  '
  For I%=0 To Num_of_files%
    '
    ' Position on screen
    '
    If X%>6 Then
      Inc Y%
      X%=-1
    Endif
    Inc X%
    '
    ' If they're selecting AUTO files or DAs, show them highlighted or not
    '
    If Pass% Mod 2=1 Then
      '
      ' Color monitor?
      '
      If Res%=1 Then
        '
        ' Yep!  Green if activated, red if not
        '
        Print Esc$;"b";Chr$(49-Stat!(I%));
      Else
        '
        ' Nope...  Highlighted if activated, not if not
        '
        Print Esc$;"b";Chr$(49+Stat!(I%));Esc$;"c";Chr$(48-Stat!(I%));
      Endif
    Endif
    '
    ' Otherwise, just...
    '
    ' Print the filename
    '
    Print At(X%*10+2,Y%);Short_name$(I%);
  Next I%
  '
  ' Set-ups
  '
  Cy%=7
  Cx%=0
  Print At(1,7);Esc$;"b?";Esc$;"c0";Chr$(175)
  '
  ' Do cursor movement
  '
  Repeat
    '
    ' Get a key...
    '
    Key$=Inkey$
    '
    ' They hit a key...
    '
    If Key$<>"" Then
      '
      ' Erase old cursor
      '
      Print At(Cx%*10+1,Cy%);Esc$;"b?";Esc$;"c0 "
      '
      ' Move up
      '
      If Key$=North$ And Cy%-1>6 Then
        Dec Cy%
      Else
        '
        ' Move down
        '
        If Key$=South$ And Cy%<Y% Then
          Inc Cy%
          If Cy%=Y% And Cx%>X% Then
            Cx%=X%
          Endif
        Else
          '
          ' Move right
          '
          If Key$=East$ And Cx%+1<8 Then
            Inc Cx%
            If Cy%=Y% And Cx%+1>X% Then
              Cx%=X%
            Endif
          Else
            '
            ' Move left
            '
            If Key$=West$ And Cx%>0 Then
              Dec Cx%
            Endif
          Endif
        Endif
      Endif
      '
      ' Print cursor
      '
      Print At(Cx%*10+1,Cy%);Chr$(175)
      '
      ' SPACE to toggle, if it's selection of AUTOs or DAs
      '
      If Key$=" " And Pass% Mod 2=1 Then
        Temp%=Cx%+(Cy%-7)*8
        Stat!(Temp%)=-1-Stat!(Temp%)
        Print At(Cx%*10+2,Cy%);
        '
        ' Color monitor?
        '
        If Res%=1 Then
          '
          ' Yep!  Green if activated, red if not
          '
          Print Esc$;"b";Chr$(49-Stat!(Temp%));
        Else
          '
          ' Nope...  Highlighted if activated, not if not
          '
          Print Esc$;"b";Chr$(49+Stat!(Temp%));Esc$;"c";Chr$(48-Stat!(Temp%));
        Endif
        Print Short_name$(Temp%)
      Endif
      '
      ' Backup?
      '
      If Key$=Chr$(8) And Pass%>1 Then
        Pass%=-1
      Endif
    Endif
    '
    ' Out?
    '
  Until Key$=Chr$(13) Or Pass%=-1
  '
  ' Out! Erase the cursor
  '
  Print At(Cx%*10+1,Cy%);" "
Return
'
' ******************************************************************************
' Print the title and instructions for each res and pass
' ******************************************************************************
'
Procedure Title
  '
  ' Title for medium and high res
  '
  Print Esc$;"c3";Esc$;"b0";
  Print At(1,1);Space$(80)
  Print At(1,2);Space$(35);"H.A.Z.E.L.";Space$(35)
  Print At(1,3);Space$(80)
  Print At(1,4);
  If Pass%=1 Then
    Print "                      Select which AUTO programs you want:                      "
  Else
    If Pass%=2 Then
      Print "                     Select which ASSIGN.SYS file you want:                     "
    Else
      If Pass%=3 Then
        Print "                    Select which Desk Accessories you want:                     "
      Else
        Print "                    Select which DESKTOP.INF file you want:                     "
      Endif
    Endif
  Endif
  Print At(1,5);Space$(80)
  '
  ' Print instructions for high and med res
  '
  If Pass% Mod 2=1 Then
    '
    ' For AUTO and DA select
    '
    Print At(2,25);"     Use the arrow keys to move cursor, SPACE to toggle, RETURN to exit.      ";
  Else
    '
    ' For ASSIGN.SYS and DESKTOP.INF selection
    '
    Print At(2,25);"              Use the arrow keys to move cursor, RETURN to choose.            ";
  Endif
  '
  ' Restore screen colors
  '
  Print Esc$;"b?";Esc$;"c0";
Return
'
' ******************************************************************************
' Choosing and copying for ASSING.SYS and DESKTOP.INF
' ******************************************************************************
'
Procedure Assign_desktop(File$)
  '
  ' And put 'em into Short_name$ and add "Keep old" as default
  '
  Short_name$(0)="Keep old"
  For I%=0 To Num_of_files%
    Temp%=Instr(Filename$(I%),".")
    '
    ' If there's a file extension, cut it off
    '
    If Temp%>0 Then
      Short_name$(I%+1)=Left$(Filename$(I%),Temp%-1)
    Else
      '
      ' If not, don't bother
      '
      Short_name$(I%+1)=Filename$(I%)
    Endif
  Next I%
  '
  ' Inc number of files 'cause of "Keep old"
  '
  Inc Num_of_files%
  '
  ' Find out which one they want
  '
  Cls
  Gosub Select
  Cls
  '
  ' Find which one they were on when they RETURNed
  '
  Temp%=Cx%+(Cy%-7)*8-1
  '
  ' Keeping old?
  '
  If Temp%>-1 And Pass%>-1 Then
    '
    ' Nope, copy file
    '
    Dumb$=Space$(File_len%(Temp%))
    Bload "\AUTO\HAZEL\"+Filename$(Temp%),Varptr(Dumb$)
    Bsave File$,Varptr(Dumb$),Len(Dumb$)
  Endif
Return
'
' ******************************************************************************
' Gets directory from disk to where we can use it
' ******************************************************************************
'
Procedure Get_dir(A$)
  '
  ' Set number of files
  '
  Num_of_files%=-1
  '
  ' Point to the Disk Transfer Address (Where filename comes back) and get
  ' File Not Found/End of Data status
  '
  Dta%=Gemdos(47)
  Temp%=Gemdos(78,L:Varptr(A$),-1)
  '
  ' Loop to get filenames
  '
  Do
    '
    ' Out if no more data
    '
    Exit If Temp%=-49
    '
    ' We've got another file
    '
    Inc Num_of_files%
    Filename$(Num_of_files%)=""
    '
    ' And get filename from memory
    '
    For I%=Dta%+30 To Dta%+41
      '
      ' Out if this is the end of filename
      '
      Exit If Peek(I%)=0
      '
      ' Add it in
      '
      Filename$(Num_of_files%)=Filename$(Num_of_files%)+Chr$(Peek(I%))
    Next I%
    '
    ' And the file length
    '
    File_len%(Num_of_files%)=Dpeek(Dta%+28)
    '
    ' Get the status for the next file
    '
    Temp%=Gemdos(79)
    '
    ' 'Round again
    '
  Loop
  '
  ' Make sure they're being reasonable...
  '
  If Num_of_files%>135 Then
    Print "HAZEL can only handle 136 of each type of file!"
    Pause (50)
    Num_of_files%=135
  Endif
Return
