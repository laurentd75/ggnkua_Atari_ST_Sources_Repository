'
'                         MORTGAGE CALCULATOR
'
'                          by Arthur Shipley
'
'                    Copyright 1988 Antic Publishing Inc.
'
Clear
'
Let Resolution=Xbios(4)
'
If Resolution=0
  Alert 1," Please change to|Medium Resolution!",1," Okay ",Button
  End
Endif
'
' ********************** MORTGAGE & AMORTIZATION PROGRAM **********************
'
If Resolution=1
  Oldcolor0=Xbios(7,0,-1)                          ! Saves existing pallet colors
  Oldcolor1=Xbios(7,1,-1)
  Oldcolor2=Xbios(7,2,-1)
  Oldcolor3=Xbios(7,3,-1)
  '
  ' ------------------------------ SETUP SYSTEM COLOR ----------------------------
  '
  ' set background color
  Setcolor 0,0,0,0
  ' set green letters
  Setcolor 1,0,7,0
  ' set blue colors
  Setcolor 2,0,7,7
  ' set normal text colors
  Setcolor 3,7,7,5
  '
  ' -------------------------------- END COLOR SETUP -----------------------------
  '
Endif
Hidem
Kdelay=12                                        !8-15 Sets keyboard delay  time
Krepeat=1                                        !0-7  Sets keyboard repeat rate
Void Xbios(35,Kdelay,Krepeat)
Gosub Titlescreen
'
Carryover$="False"
On Error Gosub Identerror
'
Dim A$(25)
A$(0)=" Desk  "
A$(1)="  About MORTCALC  "
A$(2)="--------------------"
A$(3)="-"
A$(4)="-"
A$(5)="-"
A$(6)="-"
A$(7)="-"
A$(8)="-"
A$(9)=""
A$(10)=" Mortgage  "
A$(11)="  Find Payment Amount      "
A$(12)="  Find Number of Payments  "
A$(13)="  Find Loan Amount         "
A$(14)="---------------------------"
A$(15)="           Quit            "
A$(16)=""
A$(17)=" Amortize "
A$(18)="  Show Results Only     "
A$(19)="  Show  Complete Table  "
A$(20)="  Print Complete Table  "
A$(21)="  Print Results Only    "
A$(22)="------------------------"
A$(23)=""
A$(24)=""
Begin:
Menu A$()
Print At(72,1);"b1";"MORTCALC";"b3";
On Menu  Gosub Info
Do
  On Menu
Loop
Procedure Info
  Menu Off
  Cls
  Err=0
  Value=Menu(0)
  If Value=1
    Gosub Help
  Endif
  If Value=11
    Gosub Paycalc
  Endif
  If Value=12
    Gosub Numpayments
  Endif
  If Value=13
    Gosub Valuecalc
  Endif
  If Value=15
    If Resolution=1
      Void Xbios(7,0,Oldcolor0)             !Resets old pallet to original values
      Void Xbios(7,1,Oldcolor1)
      Void Xbios(7,2,Oldcolor2)
      Void Xbios(7,3,Oldcolor3)
    Endif
    End
  Endif
  If Value=18
    Amort$="YES"
    Showall$="N"
    Gosub Amort
  Endif
  If Value=19
    Amort$="YES"
    Showall$="Y"
    Gosub Amort
  Endif
  If Value=20
    Showall$="Y"
    Gosub Tableprint
  Endif
  If Value=21
    Showall$="N"
    Gosub Tableprint
  Endif
  Value=0
  Cls
  Menu A$()
  Print At(72,1);"b1";"MORTCALC";"b3";
Return
'
' ------------------------------ SETS UP TITLE PAGE ----------------------------
'
Procedure Titlescreen
  '
  Cls
  Box 0,0,635,199
  Box 20,10,615,189
  Deftext 1,4,0,25
  Text 255,50,"START'S"
  Deftext 2,0,0,32
  Text 160,85,"MORTGAGE CALCULATOR"
  Deftext 1,1,0,13
  Text 240,110,"by Arthur Shipley"
  Text 170,135,"Copyright 1988 Antic Publishing Inc."
  Gosub Pauseroutine
  Cls
  '
Return
'
' ----------------------------- END TITLEPAGE SETUP ----------------------------
'
'
' ----------------------------- DISPLAY HELP SCREEN ----------------------------
'
Procedure Help
  Cls
  Deftext 2,0,0,12
  Text 245,20,"MORT-CALC"
  Deftext 2,0,0,6
  Text 250,32,"by Arthur Shipley"
  Text 175,42,"Copyright 1988 Antic Publishing Inc."
  Print At(1,6)
  Print "THIS PROGRAM IS DIVIDED INTO 2 PARTS : "
  Print
  Print "     1- The Mortgage Calculator Program"
  Print "     2- The Amortization Table Calculator Program"
  Print
  Print "  It is suggested that you run the Mortgage Calculator first in order to find"
  Print "  the correct variables to use on the Amortization Table Calculator."
  Print
  Print "    The Mortgae Calculator will prompt you to calculate any one of:"
  Print "      1- The AMOUNT OF LOAN under consideration"
  Print "      2- The PAYMENT AMOUNT on the loan"
  Print "      3- The NUMBER OF PAYMENTS needed to pay off the loan"
  Print
  Print "  The Amortization Table Calculator will let you enter and play with your own "
  Print "  numbers or automatically use those calculated by the Mortgage Calculator, but"
  Print "  if you enter numbers that don't agree with each other you will get an"
  Print "  error message."
  Gosub Pauseroutine
  Cls
Return
'
' --------------------------- END HELP SCREEN DISPLAY --------------------------
'
'
' ********************  BEGIN MORTGAGE CALCULATOR SECTION  ********************
'
'
' --------------------------- SELECT PROGRAM BRANCH ----------------------------
'
Procedure Mortprint
  Pprintout$="N"
  Print At(1,23);"J";
  Print ,,"p";" WANT PRINTOUT? ";"q"
  Print ,,"p";"     Y / N      ";"q";
  Do
    Pprintout$=Inkey$
    Exit If Pprintout$<>"" Or Mousek<>0
  Loop
  For I=1 To 1000
  Next I
  If Pprintout$="Y" Or Pprintout$="y" Then
    Cls
    Print At(29,6);"OUTPUTTING TO PRINTER"
    Do
      Exit If Out?(0)=-1
      A$=Inkey$
      Exit If A$<>""
      Print At(32,8);"Turn on printer"
      For I=1 To 3
        Sound 1,12,5,5,5
        Sound 1,0,0,0,5
      Next I
      For I=1 To 15000
      Next I
    Loop
    Lprint
    Lprint
    Lprint "                           MORTGAGE CALCULATOR PROGRAM"
  Endif
Return
'
' ------------------------- MORTGAGE CALCULATOR TITLE BLOCK -------------------
'
Procedure Morttitle
  Cls
  Deftext 2,0,0,8
  Text 160,20,"MORTGAGE  CALCULATOR PROGRAM"
  Print
  Print
  Print
Return
'
' ----------------------- BEGIN LOOKING FOR PAYMENT AMOUNT ---------------------
'
Procedure Paycalc
  Gosub Morttitle
  Print At(27,4);"Payment Amount Calculator"
  Print At(27,5);"-------------------------"
  Print
  Print
  Input "AMOUNT OF LOAN= $ ",V$
  If V$="" Or Val(V$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Paybottom
  Else
    V=Val(V$)
  Endif
  Input "INTEREST RATE=  % ",I$
  If I$="" Or Val(I$)<1 Or Val(I$)>30 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Paybottom
  Else
    I=Val(I$)
  Endif
  Input "NUMBER OF MONTHS= ",N$
  If N$="" Or Val(N$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Paybottom
  Else
    N=Val(N$)
  Endif
  J=I
  I=0.01*I/12
  P=V*(I/(1-(1+I)^-N))
  Print
  Print "b1"
  P=Int((P+0.005)*100)/100
  Print "PAYMENT=";"b1";" $ ";P;" PER MONTH";"b3"
  Carryover$="True"
  Gosub Mortprint
  If Pprintout$="Y" Or Pprintout$="y" Then
    Lprint
    Lprint "AMOUNT OF LOAN $ ";V
    Lprint "INTEREST RATE  % ";J
    Lprint "NUMBER OF MOS    ";N
    Lprint
    Lprint "PAYMENT =      $ ";P
    Lprint
    Lprint
  Endif
  ' for amortization table calculator
  A=P
  B=J
  C=N
  D=V
  Paybottom:
Return
'
' ----------------------- BEGIN LOOKING FOR NUMBER OF PAYMENTS -----------------
'
Procedure Numpayments
  Gosub Morttitle
  Print At(25,4);"Number of Payments Calculator"
  Print At(25,5);"-----------------------------"
  Print
  Print
  Input "AMOUNT OF LOAN = ";V$
  If V$="" Or Val(V$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Numbottom
  Else
    V=Val(V$)
  Endif
  Input "PAYMENT AMOUNT= ";P$
  If P$="" Or Val(P$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Numbottom
  Else
    P=Val(P$)
    If P>V Then
      Cls
      Print At(24,6);"PAYMENT GREATER THAN LOAN AMOUNT"
      Print At(32,8);"b1";"   TRY AGAIN";"b3";
      Gosub Shortpause
      Goto Numbottom
    Endif
  Endif
  Input "INTEREST RATE = ";I$
  If I$="" Or Val(I$)<1 Or Val(I$)>30 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Numbottom
  Else
    I=Val(I$)
  Endif
  J=I
  I=(0.01*I)/12
  If I*V>P Then
    Cls
    Print At(25,4);"b1";"*** NEGATIVE AMORTIZATION ***";"b3";
    Print At(10,6);"MONTHLY INTEREST TIMES VALUE IS GREATER THAN INDICATED PAYMENT"
    Print At(35,8);"b1";"TRY AGAIN";"b3";
    For I=1 To 3
      Sound 1,12,5,5,5
      Sound 1,0,0,0,5
    Next I
    Gosub Shortpause
    Gosub Shortpause
    Goto Paybottom
  Endif
  N=-((Log(1-I*V/P))/(Log(1+I)))
  If N<>Int(N) Then
    N=Int(N+1)
  Endif
  Print
  Print "b1";"NUMBER OF PAYMENTS= ";"b3";(N)
  Carryover$="True"
  Gosub Mortprint
  If Pprintout$="Y" Or Pprintout$="y" Then
    Lprint
    Lprint "PAYMENT AMOUNT  $ ";P
    Lprint "AMOUNT OF LOAN  $ ";V
    Lprint "INTEREST RATE   % ";J
    Lprint
    Lprint "NUMBER OF PAYMENTS = ";N
    Lprint
    Lprint
  Endif
  ' for amortization table calculator
  A=P
  B=J
  C=N
  D=V
  Numbottom:
Return
'
' ------------------------- BEGIN LOOKING FOR PRESENT VALUE -------------------
'
Procedure Valuecalc
  Gosub Morttitle
  Print At(27,4);"Original Value Calculator"
  Print At(27,5);"-------------------------"
  Print
  Print
  Input "PAYMENT AMOUNT.......$ ",P$
  If P$="" Or Val(P$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Valuebottom
  Else
    P=Val(P$)
  Endif
  Input "INTEREST RATE........% ",I$
  If I$="" Or Val(I$)<1 Or Val(I$)>30 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Valuebottom
  Else
    I=Val(I$)
  Endif
  Input "NUMBER OF MONTHS...... ",N$
  If N$="" Or Val(N$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Valuebottom
  Else
    N=Val(N$)
  Endif
  J=I
  I=0.01*I/12
  V=P*((1-(1+I)^-N)/I)
  Print
  Print
  Print
  Print
  V=Int((V+0.005)*100)/100
  Print "b1";"AMOUNT OF LOAN =  $ ";"b3";V
  Carryover$="True"
  Gosub Mortprint
  If Pprintout$="Y" Then
    Lprint
  Endif
  If Pprintout$="Y" Or Pprintout$="y" Then
    Lprint "PAYMENT AMOUNT $ ";P
    Lprint "INTEREST RATE  % ";J
    Lprint "NUMBER OF MOS.   ";N
    Lprint
    Lprint "AMOUNT OF LOAN  $ ";Int(V)
    Lprint
    Lprint
  Endif
  ' for amortization table calculator
  A=P
  B=J
  C=N
  D=V
  Valuebottom:
Return
'
' ************************** BEGIN AMORTIZATION PROGRAM ***********************
'
'
' ------------------------- AMORTIZATION TABLE TITLEBLOCK ---------------------
'
Procedure Amorttitle
  Cls
  Deftext 2,0,0,8
  Text 1,10,"                   AMORTIZATION PROGRAM"
  Print
  Print
  Print "b1";"AMOUNT OF LOAN.$ ";"b3";D                  !amount  borrowed
  '  Print Using "#######.##",D2
  Print "b1";"PAYMENT AMOUNT.$ ";"b3";A                  !principal + interest
  '  Print Using "#######.##",A
  Print "b1";"INTEREST RATE..% ";"b3";B                 !yearly  total
  Print "b1";"NUMBER OF MOS... ";"b3";C                 !years * 12
  Print
  Print
  Print
  Print "b1";
  Print "PAYMENT       PRINCIPAL         INT.THIS PAY     PRINC.THIS PAY    PRINC. LEFT"
  Print "-------      -----------        ------------     --------------    -----------"
  Print "b3";
Return
'
' ------------------------------- END TITLEBLOCK -------------------------------
'
' --------------------- BEGIN CALCULATING AMORTIZATION TABLE -------------------
'
Procedure Amort
  Cls
  Deftext 2,0,0,8
  Text 1,10,"                   AMORTIZATION PROGRAM"
  Print
  Print
  Print
  Last$="false"
  If Carryover$="True" Then
    Print At(21,6);"Use values from previous calculation?"
    Print At(37,8);"b1";"(Y/N)";"b3"
    Print
    Do
      A$=Inkey$
      Exit If A$<>""
    Loop
    If A$="Y" Or A$="y" Then
      Goto Skip
    Else
      Print At(1,5);"J"
    Endif
  Endif
  Input "AMOUNT OF LOAN ";D$                              !amount borrowed
  If D$="" Or Val(D$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    D=Val(D$)
  Endif
  Input "PAYMENT AMOUNT ";A$                              !principal + interest
  If A$="" Or Val(A$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    A=Val(A$)
  Endif
  Input "INTEREST RATE  ";B$                              !yearly total
  If B$="" Or Val(B$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    B=Val(B$)
  Endif
  Input "NUMBER OF MOS. ";C$                               !years * 12
  If C$="" Or Val(C$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    C=Val(C$)
  Endif
  ' -------------------- BEGIN CHECK FOR NUMBER AGREEMENT ---------------------
  E=0.01*B/12
  G=D
  H=C
  F=G*(E/(1-(1+E)^-H))
  Print
  F=Int((F+0.005)*100)/100
  If F<>A Then
    Cls
    Print At(16,6);"These numbers do not agree together properly"
    Print At(35,8);"b1";"Try Again";"b3";
    Gosub Shortpause
    Cls
    Goto Amortbottom
  Endif
  ' -
  Skip:
  A2=A
  B2=B
  C2=C
  D2=D
  If D>9999999 Or D2>9999999 Then
    Cls
    Print At(17,6);"PRINCIPAL LARGER THAN 9,999,999.00 NOT ALLOWED"
    Goto Amortbottom
  Endif
  Gosub Amorttitle
  Skipprint:
  E=(B2/100)/12                           !monthly interest rate
  Y=1                                     !starts  year  counter at  1
  L=1                                     !initialize  month  counter  beginning
  M=12                                    !initialize  months  in  year  counter
  K=C2/12                                 !determines  number  of  yearly cycles
  If K<>Int(K) Then                       !allows for partial years
    K=Int(K+1)
  Endif
  Carryover$="True"
  For J=1 To K                                           !year cycle counter
    If Showall$="Y" Or Showall$="y" Then
      Print At(1,8);"b1";"YEAR  ";"b3";Y;               !Prints Year Number
      Print At(66,8);"b1";"[ESC]";"b3";" to exit";
      Print At(1,11);
    Endif
    For N=L To M                                         !months per year loop
      If D2<=A2 Then
        A2=D2
        Last$="true"
      Endif
      F=D2*E                                      !interest this payment
      G=A2-F                                      !paid on principal this payment
      H=D2-G                                      !principal left unpaid
      D2=Int((D2+0.005)*100)/100
      If Showall$="Y" Or Showall$="y" Then
        Lasttime:
        Print Tab(2);N;
        Print Tab(13);"$";
        Print Using "#######.##",D2;
        Print Tab(33);"$";
        Print Using "######.##",Int((F+0.005)*100)/100;
        '        F=Int((F+0.005)*100)/100
        Print "       $";
        If Last$<>"true"
          Print Using "######.##",Int((G+0.005)*100)/100;
          '          G=Int((G+0.005)*100)/100
          Print "       $";
          Print Using "#######.##",Int((H+0.005)*100)/100
          '          H=Int((H+0.005)*100)/100
        Else
          Print Using "######.##",Int(((F+G)+0.005)*100)/100;
          Print "       $";
          Print Using "#######.##",Z
        Endif
      Endif
      D2=H                                      !initializes new principal amount
      Q=Q+F                                    !tabulates  total  interest  paid
      R=R+G                                    !tabulates  total  principal paid
      Exit If N=C
    Next N                                     !recycles  month  counter
    If J<>K Then                               !bypass  pause  for  printer
      L=L+12                         !advance month counter beginning each year
      M=M+12                         !advance month  counter  end of  each year
      Y=Y+1                          !advance  year  printer  value
    Endif
    If Showall$="Y" Or Showall$="y" Then
      If N=C Then
        Print "J";
      Endif
      Gosub Pauseroutine
    Endif
    Exit If A$=Chr$(27)
    Exit If N=C
  Next J                             !recycles  year  counter
  If A$=Chr$(27) Then
    Goto Amortbottom
  Endif
  Last:
  Print At(1,12);"J";
  Print
  Print "b1";"TOTAL PRINCIPAL PAID $ ";"b3";Int(((F+R)+0.005)*100)/100  !prints  total interest paid
  R=Int(((F+R)+0.005)*100)/100
  Print "b1";"TOTAL INTEREST  PAID $ ";"b3";Int((Q+0.005)*100)/100  !prints total principal paid
  Q=Int((Q+0.005)*100)/100
  S=R+Q
  Print "b1";"TOTAL DOLLARS   PAID $ ";"b3";S      !Prints Total Dollars Paid
  R=0
  Q=0
  S=0
  Gosub Pauseroutine
  Amortbottom:
Return
'
Procedure Tableprint
  Cls
  Deftext 2,0,0,8
  Text 1,10,"                   AMORTIZATION PROGRAM"
  Print
  Print
  Print
  Last$="false"
  If Carryover$="True" Then
    Print At(21,6);"Use values from previous calculation?"
    Print At(37,8);"b1";"(Y/N)";"b3"
    Print
    Do
      A$=Inkey$
      Exit If A$<>""
    Loop
    If A$="Y" Or A$="y" Then
      Goto Skip2
    Else
      Print At(1,5);"J"
    Endif
  Endif
  Input "AMOUNT OF LOAN ";D$                              !amount borrowed
  If D$="" Or Val(D$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    D=Val(D$)
  Endif
  Input "PAYMENT AMOUNT ";A$                              !principal + interest
  If A$="" Or Val(A$)=<0 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    A=Val(A$)
  Endif
  Input "INTEREST RATE  ";B$                              !yearly total
  If B$="" Or Val(B$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    B=Val(B$)
  Endif
  Input "NUMBER OF MOS. ";C$                               !years * 12
  If C$="" Or Val(C$)<1 Then
    Cls
    Print At(32,6);"INVALID NUMBER";
    Print At(32,8);"b1";"   TRY AGAIN";"b3";
    Gosub Shortpause
    Goto Amortbottom
  Else
    C=Val(C$)
  Endif
  Carryover$="True"
  ' -------------------- BEGIN CHECK FOR NUMBER AGREEMENT ---------------------
  E=0.01*B/12
  G=D
  H=C
  F=G*(E/(1-(1+E)^-H))
  Print
  F=Int((F+0.005)*100)/100
  If F<>A Then
    Cls
    Print At(16,6);"Inputted numbers do not agree together properly"
    Print At(35,8);"b1";"Try Again";"b3";
    Gosub Shortpause
    Cls
    Goto Amortbottom
  Endif
  ' ------------------ End checking for number agreement -----------------------
  If D>9999999 Or D2>9999999 Then
    Cls
    Print At(17,6);"PRINCIPAL LARGER THAN 9,999,999.00 NOT ALLOWED"
    Goto Amortbottom
  Endif
  Skip2:
  Last$="false"
  Cls
  Print At(29,6);"OUTPUTTING TO PRINTER"
  Do
    Exit If Out?(0)=-1
    A$=Inkey$
    Exit If A$<>""
    Print At(32,8);"Turn on printer"
    For I=1 To 3
      Sound 1,12,5,5,5
      Sound 1,0,0,0,5
    Next I
    For I=1 To 15000
    Next I
  Loop
  If Out?(0)=0 Then
    Goto Amortbottom2
  Endif
  Y=1
  L=1
  M=12
  A2=A
  B2=B
  C2=C
  D2=D
  K=C2/12
  If K<>Int(K) Then
    K=Int(K+1)
  Endif
  Pagefull=1
  Lprint "                         AMORTIZATION CALCULATOR PROGRAM"
  Lprint "                         *******************************"
  Lprint
  Lprint "AMOUNT OF LOAN..$ ";D2           !Prints  Principal       To Printer
  Lprint "PAYMENT AMOUNT..$ ";A2           !Prints  Payment  Amount To Printer
  Lprint "INTEREST RATE...% ";B2           !Prints  Interest  Rate  To Printer
  Lprint "NUMBER OF MOS.....";C2           !Prints Number Of Months To Printer
  Lprint
  For J=1 To K
    If Showall$="Y" Or Showall$="y" Then
      Lprint
      Lprint "YEAR ";Y                         !Print Year Counter To Printer
      Lprint "PAYMENT       PRINCIPAL        INT.THIS PAY     PRINC.THIS PAY     PRINC.LEFT"
      Lprint "-------      -----------       -------------    --------------    -----------"
    Endif
    For N=L To M                                 !months  per  year  loop
      If D2<=A2 Then
        A2=D2
        Last$="true"
      Endif
      F=D2*E                                     !interest  this  payment
      G=A2-F                                     !paid on principal this payment
      H=D2-G                                     !principal  left  unpaid
      D2=Int((D2+0.005)*100)/100
      If Showall$="Y" Or Showall$="y" Then
        Lasttime2:
        Lprint Tab(2);N;
        Lprint Tab(13);"$";
        Lprint Using "#######.##",D2;
        Lprint Tab(32);"$";
        Lprint Using "######.##",Int((F+0.005)*100)/100;
        Lprint "        $";
        If Last$<>"true"
          Lprint Using "#######.##",Int((G+0.005)*100)/100;
          Lprint "      $";
          Lprint Using "#######.##",Int((H+0.005)*100)/100
        Else
          Lprint Using "#######.##",Int(((F+G)+0.005)*100)/100;
          Lprint "      $";
          Lprint Using "#######.##",Z
        Endif
      Endif
      D2=H                                       !initializes new principal amount
      Q=Q+F                                      !tabulates  total  interest  paid
      R=R+G                                      !tabulates  total  principal paid
      A$=Inkey$
      Exit If A$=Chr$(27)
      Exit If N=C
    Next N                                       !recycles  month  counter
    If Showall$="Y" Or Showall$="y" Then
      If Pagefull=3 Then
        Lprint Chr$(12)
        Pagefull=1
      Else
        If Showall$="Y" Then
          Inc Pagefull
        Endif
      Endif
    Endif
    If J<>K Then                     !bypass  pause  for  printer
      L=L+12                         !advance month counter beginning each year
      M=M+12                         !advance month  counter  end of  each year
      Y=Y+1                          !advance  year  printer  value
    Endif
    Exit If A$=Chr$(27)
    Exit If N=C
  Next J
  If A$=Chr$(27) Then
    Goto Amortbottom2
  Endif
  Lprint
  Lprint "TOTAL PRINCIPAL PAID $ ";Int(((F+R)+0.005)*100)/100
  R=Int(((F+R)+0.005)*100)/100
  Lprint "TOTAL INTEREST  PAID $ ";Int((Q+0.005)*100)/100
  Q=Int((Q+0.005)*100)/100
  S=R+Q
  Lprint "TOTAL DOLLARS   PAID $ ";S
  R=0
  Q=0
  S=0
  Amortbottom2:
Return
'
' -------------------------- BEGIN PAUSEROUTINES -------------------------------
'
Procedure Pauseroutine                             ! Holds screen until released
  Print "b3"
  Print At(26,25);"p";"< HIT ANY KEY TO CONTINUE >";"q";
  Do
    A$=Inkey$
    Exit If A$<>"" Or Mousek<>0
  Loop
  For I=1 To 1000
  Next I
Return
'
' ------------------
'
Procedure Shortpause                               !Delays screen scrolloff
  For I=1 To 20000
  Next I
Return
'
' ----------------------------- END PAUSEROUTINES ------------------------------
'
Procedure Identerror
  On Error Gosub Identerror
  Cls
  If Err=6 Then
    Print At(18,6);"LOGARITHMS ONLY FOR NUMBERS LARGER THAN ZERO";
    Print At(35,8);"b1";"TRY AGAIN";"b3"
    Gosub Shortpause
  Endif
  Cls
  Resume Begin
Return
'
End
